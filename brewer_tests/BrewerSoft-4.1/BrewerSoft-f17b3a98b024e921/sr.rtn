10000 REM ************ sr.rtn 01/04/2011 12:00:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10001 REM     MKII/MKIII/MKIV Azimuth Steps/Revolution Determination
10002 REM
10003 REM  See Also: AZ.RTN
10004 REM **************************************************************
10005 DATA sr
16000 '
16001 ' *** Zeroing Azimuth Motor ***
16002 '
16003 ' Note: this subroutine is taken directly from AZ.RTN
16004 '
16010 IF Q2%=0 THEN 18000:REM was 17000 in AZ.RTN
16020 PRINT CL$:LOCATE ,SP:PRINT"Zeroing azimuth":IF Q14%=1 THEN FLAG=1:M1$="2":GOSUB 6550:MS%=AZ%-Y:GOTO 16330
16030 MS%=0:O1$="L,19413,0,19414,200":GOSUB 9400:REM Speed up motor for azimuth
16040 IF AZ%>(SR%*.8) THEN O1$="M,2,-"+STR$(INT(SR%*.2)):GOSUB 9450:MS%=(SR%*.2)
16050 :
16060 O1$="M,2,-300:G,800":GOSUB 9450:MS%=MS%+300
16070 GOSUB 2090:IF MS%>18000 OR TA%=1 THEN B$ = " after "+STR$(MS%)+" steps ( "+STR$(MS%/300)+" moves at 300 steps each": GOTO 16800
16080 IF (VA AND B5%)<>B5% THEN 16060
16090 :
16100 NLOOP%=0
16110 NLOOP%=NLOOP%+1:O1$="M,2,51:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-50
16120 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN B$ = " after "+STR$(MS%)+" steps ( "+STR$(NLOOP%)+" moves at 50 steps each": GOTO 16800
16130 IF (VA AND B5%)=B5% THEN 16110
16140 O1$="M,2,-101":GOSUB 9450:MS%=MS%+101
16150 :
16160 NLOOP%=0
16170 NLOOP%=NLOOP%+1:O1$="M,2,16:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-15
16180 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN B$ = " after "+STR$(MS%)+" steps ( "+STR$(NLOOP%)+" moves at 15 steps each": GOTO 16800
16190 IF (VA AND B5%)=B5% THEN 16170
16200 O1$="M,2,-36":GOSUB 9450:MS%=MS%+36
16210 :
16220 NLOOP%=0
16230 NLOOP%=NLOOP%+1:O1$="M,2,5:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-4
16240 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN B$ = " after "+STR$(MS%)+" steps ( "+STR$(NLOOP%)+" moves at 4 steps each": GOTO 16800
16250 IF (VA AND B5%)=B5% THEN 16230
16260 O1$="M,2,-11":GOSUB 9450:MS%=MS%+11
16270 :
16280 NLOOP%=0
16290 NLOOP%=NLOOP%+1:O1$="M,2,2:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-1
16300 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN B$ = " after "+STR$(MS%)+" steps ( "+STR$(NLOOP%)+" moves at 1 step each": GOTO 16800
16310 IF (VA AND B5%)=B5% THEN 16290
16320 :
16330 PRINT CL$:LOCATE ,SP:B$="Azimuth discrepancy = "+STR$(AZ%-MS%)
16340 PRINT#4,"Azimuth zeroed at ";TIME$;" ";B$
16350 GOSUB 3050:TA%=0:GOTO 17000
16360 :
16800 REM *** Azimuth zeroing (non-critical) failure
16810 LOCATE ,SP:AZ%=AZ%-MS%
16820 IF TA%=0 THEN B$="Azimuth tracker ZEROING FAILURE !! "+B$:HF%=1:PRINT B$:PRINT#4,B$:GOSUB 3050
16830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Azimuth zeroing aborted"
16840 X=1:GOSUB 7220:GOTO 18000:REM must quit here also
17000 '
17001 ' *** Calculate Steps/Revolution ***
17002 '
17010 PRINT CL$:LOCATE 10,SP:PRINT "Finding azimuth steps per revolution - press DEL to abort"
17020 IF Q14%=1 THEN O1$="STEPS":GOSUB 9450:O1$="?STEPS":GOSUB 9450:AS%=VA:GOTO 17100
17030 ST%=300:AS%=0
17040 AS%=AS%+ST%:AS$=STR$(AS%): IF AS%>18000 OR TA%=1 THEN B$ = " after "+STR$(AS%)+" steps doing 300 steps increment": GOTO 17800
17050   O1$="M,2,"+AS$+":G,800":GOSUB 9450
17060   GOSUB 2090:IF TA%=1 THEN 17200
17070 IF (VA AND B5%)<>B5% THEN 17040
17080 ST%=50:GOSUB 17400:ST%=15:GOSUB 17400:ST%=4:GOSUB 17400:ST%=1:GOSUB 17400
17100 B$="Azimuth Steps per revolution measured = "+STR$(AS%):GOSUB 3050
17110 PRINT CL$:LOCATE 10,SP:PRINT B$:LOCATE ,SP:PRINT "Old value is ";SR%
17115 IF ABS(AS%-SR%)>20 THEN LOCATE ,SP:COLOR 12:PRINT "New value is ";ABS(SR%-AS%);" steps different from the old!":COLOR 7
17120 LOCATE ,SP:PRINT"Do you want the new value saved (Y/N) ";:A$="":IF RM%=0 THEN GOSUB 2035
17130 IF ((A$="y") OR (A$="Y")) OR (Q14%=0 AND RM%=1 AND ABS(AS%-SR%)<20) THEN B$="Updating SR constant from "+STR$(SR%)+" to "+STR$(AS%): SR%=AS%: PRINT#4, B$:GOSUB 3050:GOSUB 5200
17140 B$="Azimuth steps per revolution = "+STR$(AS%)+" at "+TIME$:PRINT#4,B$
17150 :
17200 LOCATE ,SP:PRINT"*** Restoring motor positions ***"
17210 TA%=0:GOSUB 2080:FLAG=1:GOSUB 5800:O1$="M,2,"+STR$(AZ%):GOSUB 9450
17220 GOTO 18000
17230 :
17400 GOSUB 17600:IF (VA AND B5%)=B5% THEN 17400
17410 IF ST%<>1 THEN ST%=-2.0*ST%:GOSUB 17600
17420 RETURN 
17430 :
17600 AS%=AS%+ST%:AS$=STR$(AS%)
17610 O1$="M,2,"+AS$+":G,800":GOSUB 9450:RETURN 
17800 REM *** SR failure after successful zeroing 
17810 LOCATE ,SP
17820 IF TA%=0 THEN B$="Azimuth SR FAILURE !! "+B$:HF%=1:PRINT B$:PRINT#4,B$:GOSUB 3050
17830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Azimuth SR aborted"
17840 X=1:GOSUB 7220:GOTO 18000:REM must quit here also
18000 '
18001 ' *** Quit ***
18002 '
18010 IF Q14%=0 THEN O1$="L,19414,255":GOSUB 9450:REM restore slower motor speed
18020 RETURN
65529 REM proper last line
