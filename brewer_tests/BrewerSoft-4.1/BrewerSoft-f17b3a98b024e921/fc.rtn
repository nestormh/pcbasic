10000 REM ************ fc.rtn 05/04/2013 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10010 REM           MKII/MKIII/MKIV Standard Lamp Test Routine
10020 REM
10030 REM                (International Ozone Services, Inc. 2013)
10040 REM
10050 REM  See also:
10060 REM ****************************************************************
10070 REM
10080 REM  This routine performs a standard lamp test on the Brewer with a FW3 at different steps of FW3
10090 REM  
10100 REM
10110 REM  Side Effects:
10120 REM
10130 REM  Global Variables Referenced:
10140 REM    G$() MS() HF% LO% RM%
10150 REM    NOFW1 OZFW1 POFW2 SP TA TD
10160 REM    CL$ CZ$ H$ M4$ M5$ M9$ MDD$ O1$ WL$ WU$
10170 REM
10180 REM  Exits: 12150
10190 REM
10200 REM  Uses:  2450, 3215, 3230, 4200, 4400, 6610, 6650, 6660, 6700,
10210 REM         6800, 6900, 7000, 7050, 8000, 8200, 8600, 9650, 9670,
10220 REM         9710, 9820, 9840, 9860, 9900
10230 REM
10240 REM ****************************************************************
10250 REM  History: dd/mm/yy
55555 REM  13/08/14 - Fixed for MKIII
10260 REM  10/05/94 - New header, comments added, line numbers cleaned up
10270 REM
10280 REM ****************************************************************
10500 DATA fc
10501 IF Q9% = 0 THEN B$="FC needs FW#3 to run. Quitting.": GOSUB 3050: PRINT#4, B$: RETURN
10502 IF MDD$="o3" THEN FW3MIN%=MX%-12 ELSE FW3MIN%=NMX%-12
10503 FW3%=FW3MIN%-1
10504 REM IF TYP$="mkiii" THEN O1$="M,10,-" + MC$: GOSUB 9450:REM Remember pos of first micrometer
10505 IF TYP$="mkiv" THEN O1$="M,6,"+str$(FW3%) ELSE O1$="B,3:M,10,"+STR$(FW3%)
10506 GOSUB 9450
10507 C$ = "fc": GOTO 12000                                'Main Module
10510 '
10511 ' *** Get Standard Lamp Data ***
10512 '
10530 GOSUB 9900                                'Wait until finished then get data
10560 IF XN=1 OR ND%<26 THEN GOSUB 9710
10570 IF XN=1 THEN 10530                        'Restart Observation
10580 GOSUB 8300:GOSUB 8700:GOSUB 3215          'Calculate Ratios
10590 RETURN 
11000 '
11001 ' *** Filter Positions, Prism Rotation, Lamp Warm-Up ***
11010 GOSUB 6610
11020 IF MDD$="n2" THEN M4$=STR$(NOFW1) ELSE M4$=STR$(OZFW1)
11030 GOSUB 6660
11040 IF MDD$="o3" THEN M5$=STR$(POFW2) ELSE M5$=STR$(POFW2+64):REM     Make NOFW2 an instrument constant!
11050 GOSUB 6650:GOSUB 9860:GOSUB 6700:
11055 TD=1500:GOSUB 7000:REM set wait time
11060 LOCATE ,SP:PRINT "4 - Rotate director prism to lamps":GOSUB 9650
11070 T0=TA/3600:GOSUB 8600
11080 PRINT CL$
11085 IF Q14%=0 THEN O1$="L,20248,14,20249,255;Z":GOSUB 9450:GOSUB 9190:SVA=10.68*VA/1000:GOTO 11090
55555 REM set up request command, request and get value for channel 14 -- SL current on new electronics
11087 O1$="?ANALOG.NOW[14]":GOSUB 9450:GOSUB 9190: VA=(VA AND 1023)*5/1024:SVA=VA*0.47434
11090 LOCATE , SP:PRINT"Waiting until ";H$;" for lamp warmup (";INT(SVA*100)/100;" amps)":GOSUB 6900:RETURN
12000 '
12001 ' *** Standard Lamp Measurement ***
12002 '
12010 IF M9$<>"2" THEN GOSUB 9820			 'Standard Lamp On
12020 PRINT#4,
12040 GOSUB 11000: ' Setup
12050 IF LO%=1 THEN NEED.HG=1: HP.NOTDONE=1:GOTO 12130  'End if intensity low
12052 GOSUB 2450:GOSUB 4450:OPEN DD$+"dum.dta"FOR OUTPUT AS 8
12054 PRINT#8,"FW#3 SCAN-INST #"+NO$+" at "+TIME$
12055 IF TYP$="mkiv" THEN O1$="M,6,"+str$(FW3%) ELSE O1$="B,3:M,10,"+STR$(FW3%)
12057 GOSUB 9450
12060 GOSUB 6800:IF HF%<>0 THEN :STIME$="Aborted!":CLOSE 8: GOTO 12140    'End if Aborted
12070 GOSUB 2450                                         'Get Brewer Temperature
12075 IF MDD$="o3" THEN PRINT#8, "FW3    R6   R5       I(2)"  ELSE PRINT#8, "FW3   F     I(2)"
12080 CZ$=CY$:WL$="0":WU$="6"                            'Set sl Wavelengths 0 to 6
12090 GOSUB 9670:GOSUB 9710:GOSUB 8000:MS(0)=11:ND%=0    'Start 1st obs and zero sums
12100 FW3%=FW3%+1: IF TYP$="mkiv" THEN O1$="M,6,"+str$(FW3%) ELSE O1$="B,3:M,10,"+STR$(FW3%)
12105 GOSUB 9450: ND%=ND%+1:IF ND%<26 THEN GOSUB 10510                'Get data
12110 IF ND%=25 THEN O1$="":GOTO 12123
12120 GOSUB 9670:LOCATE 10, 5: PRINT USING "###  ######  ######  ######### ########"; FW3%-1; MS(9); MS(8); EXP(F(2)/P4%*CO); F(2)
12122 PRINT#8, USING "###  ######  ######  ######### ########"; FW3%-1; MS(9); MS(8); EXP(F(2)/P4%*CO); F(2):GOTO 12100 'Repeat
12123 PRINT#8,"end":CLOSE 8
12124 B$=JD$+YF$+"."+NO$
12125 PRINT#4,"FC scan finished at ";TIME$
12126 FTMP$ = DD$+"FC"+B$: GOSUB 4900: SHELL "copy "+FTMP$+"+"+DD$+"dum.dta "+DD$+"dum2.dta":  SHELL "copy "+DD$+"dum2.dta "+FTMP$
12130 GOSUB 3230:IF RM%=0 THEN GOSUB 9840:
12140 IF INSTR("dt rs sl fc",G$(JJ+1))=0 THEN GOSUB 9840
12145 IF MDD$="o3" THEN O1$=str$(MX%) ELSE O1$=str$(NMX%)
12147 IF TYP$="mkiv" THEN O1$="M,6,"+O1$ ELSE O1$="B,0:M,10,-"+MC$+";M,10,"+MC$
12148 GOSUB 9450 
12150 C$="fc": RETURN 
65529 REM proper last line
