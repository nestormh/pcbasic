10000 REM ************ sk.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10010 REM               MKII/MKIII/MKIV Schedule Routine
10020 REM
10030 REM  17/07/98 - Fixed strange abort problem
10040 REM  10/02/98 - Bug fix to handle all types of schedules
10050 REM  13/02/95 - Rewritten
10060 REM *************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA sk
11020 IF (Q1%+Q2%+Q3%+Q4%+Q5%)=5 THEN 11100
11030   B$="Brewer is not fully automatic - cannot run schedules":IF FP%<>0 THEN PRINT#4,B$
11040   PRINT B$:X=1:GOSUB 7220:RETURN
11100 FLAG=1:GOSUB 5800
11110 IF SK$="" THEN 12000
11120 IF ZF=180 THEN QC=0:QR=0:GOTO 21000
11130 GOTO 13000
12000 '
12001 ' *** Enter Schedule Name ***
12002 '
12010 CLS:PRINT CL$:G$(0)=C$:FILES "*.skd"
12020 PRINT "Enter schedule name (no extension)  ";:GOSUB 2000
12030 IF B$="" THEN 21000
12040 IF LEN(B$)>8 THEN 12600
12050 ON ERROR GOTO 12500
12060 CLOSE 8:OPEN B$+".skd" FOR INPUT AS 8
12070 ON ERROR GOTO 3100:CLOSE 8
12080 SK$=B$:DI$=C$:GOSUB 5200:IF FP%<>0 THEN PRINT#4,"Running schedule ";SK$
12090 GOSUB 2090:GOTO 13000
12500 REM  *** File error ***
12510 RESUME 12600
12600 PRINT "Bad schedule name ";B$
12610 X=2:GOSUB 7220:GOTO 12000
13000 '
13001 ' *** Find Next Entry in Schedule File ***
13002 '
13010 IF HF%<>0 THEN 21000
13020 FLAG=1:GOSUB 5800:IF A$=Q2$ THEN 21000
13030 ON ERROR GOTO 13900
13040 CLOSE 8:OPEN SK$+".skd" FOR INPUT AS 8
13100 IF EOF(8) THEN B$="No entries":GOTO 20000
13110 INPUT#8,I:IF EOF(8) THEN B$="No entry for angle"+STR$(I):GOTO 20000
13120 INPUT#8,B$:IF ZC<I THEN 13500
13130 IF I<=ZS THEN 13100
13140 ZS=I
13200 IF EOF(8) THEN B$="No more angles following angle"+STR$(ZS):GOTO 20000
13210 INPUT#8,ZF:IF EOF(8) THEN B$="No entry for angle"+STR$(ZF):GOTO 20000
13220 INPUT#8,ZB$:IF ZC<ZF AND ZC>ZS THEN CLOSE 8:GOTO 14000
13230 B$=ZB$:IF ZF=180 THEN CLOSE 8:QC=0:QR=0:GOTO 21000
13240 ZS=ZF:GOTO 13200
13500 ZS=I:IF EOF(8) THEN B$="No more angles following angle"+STR$(ZS):GOTO 20000
13510 INPUT#8,ZF:CLOSE 8:ON ERROR GOTO 3100
13520 GOTO 15000
13900 RESUME 13910
13910 B$="General file error"
13920 GOTO 20000
14000 '
14001 ' *** Decode Sequence and Run It ***
14002 '
14010 ON ERROR GOTO 3100:JJ=0:QC=1
14020 G$(QC)=LEFT$(B$,2):B$=RIGHT$(B$,LEN(B$)-2)
14030 QR=VAL(B$):IF (QR=0) AND (LEN(B$)>1) THEN QC=QC+1:GOTO 14020
14040 IF QR=0 THEN QR=1
14050 IF RM%=1 THEN RETURN 
14060 GOTO 3400
15000 '
15001 ' *** Wait Until Specific Angle Before Sequence ***
15002 '
15010 PRINT CL$:LOCATE 10,10
15020 PRINT USING "Waiting until ZA = ####.## for \  \";ABS(ZS);LEFT$(B$,2)
15030 LOCATE 12,24:PRINT USING "ZA = ####.## now";ZA:GOSUB 9891:GOSUB 50
15040 FLAG=1:GOSUB 5800:IF A$=Q2$ THEN HF%=1:GOTO 21000
15050 GOSUB 2090:IF HF%<>0 THEN 21000
15060 IF ZC<ZS THEN 15030
15070 GOTO 14000
20000 '
20001 ' *** General Error Handler ***
20002 '
20010 ON ERROR GOTO 3100
20020 CLOSE 8:B$=B$+" in schedule "+SK$
20030 C$="skc":GOSUB 3050:PRINT B$:IF FP%<>0 THEN PRINT#4,B$
20040 GOTO 21000
21000 '
21001 ' *** Exit to Menu ***
21002 '
21010 SK$="":DI$="menu":GOSUB 5200
21020 RETURN
65529 REM proper last line
