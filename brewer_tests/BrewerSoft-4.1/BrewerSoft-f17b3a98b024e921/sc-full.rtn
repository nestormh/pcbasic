10000 REM ************ sc.rtn 22/04/2014 18:00:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
55555 REM             MKII/MKIII/MKIV O3 Direct Sun Scan Test
55555 REM
55555 REM
55555 REM ***************************************************************
55555 REM  06/07/12 SC will quit if intensity check sets M5$ to 0 and counts are lower than 500/cy
10005 IF MDD$="o3" THEN 12000
10006 PRINT#4,"SC is not an NO2 routine": PRINT"SC is not an NO2 routine"
10007 FOR I=1 to 2000:NEXT:RETURN
12000 '
55555 ' *** Setup for Direct Sun Observation ***
12000 '
12000 DATA sc
12020 TR$="d"+"s": scquit=0
12022 if nds = 0 and nzs > 0 then print#4, "SC: sun scan is not done unless there is at least one good ds. Quitting.": return
55555 '  message
12030 UC%=0:GOSUB 9670 
55555  '  take brewer temp
12040 GOSUB 2450
12050 VAR8$=M8$:VAR1$=STR$(TE%)
12060 GOSUB 6610:GOSUB 6630:M5$=SQ$:GOSUB 6650
12070 GOSUB 7750:GOSUB 6690:TI=TIMER*60:TD=TI+1500
12080 LOCATE ,SP:PRINT "4 - Point Brewer at sun":GOSUB 9650
55555 REM  'Wait until ready, test intensity and quit if too low
12090 GOSUB 7750:GOSUB 7500:SQ$=M5$:IF FS<500 AND VAL(M5$)=0 THEN BC%=12: B$="SC intensity too low ("+STR$(FS)+"/cy), skipping.":GOSUB 3050:PRINT#4,B$:RETURN
12092 if za > 75 then print#4, "SC: sun scan is not done for ZA>75. Quitting.": return
55555 '
55555 ' *** User Input/Default Settings ***
55555 '
13010 DIM A(30,2),B(3,5),C%(30,1):MN=0:MU=0
13020 GOSUB 3220:WL$="0":WU$="6":M8$="-"+MC$:GOSUB 9805
13030 PRINT CL$
13040 LOCATE 10,SP+3:PRINT "Enter min step# ";VAL(MC$)-14;
13050 IF RM%=1 THEN W1=VAL(MC$)-14:GOTO 13080
13060   GOSUB 21000:IF RET=1 THEN W1=VAL(MC$)-14:GOTO 13080
13070   GOSUB 2000:W1=VB
13080 LOCATE ,SP+3:PRINT "Enter max step# ";VAL(MC$)+14;
13090 IF RM%=1 THEN W2=VAL(MC$)+14:GOTO 13120
13100   GOSUB 21000:IF RET=1 THEN W2=VAL(MC$)+14:GOTO 13120
13110   GOSUB 2000:W2=VB
13120 LOCATE ,SP+3:PRINT "Enter step increment ";2;
13130 IF RM%=1 THEN DW=2:GOTO 14000
13140   GOSUB 21000:IF RET=1 THEN DW=2:GOTO 14000
13150   GOSUB 2000:DW=VB
14000 '
55555 ' *** Proceed with Measurement ***
14000 '
14000 PRINT:PRINT#4,"Scanning test from micrometer step#";W1;"to";W2
14015 B$="start"+" "+str$(w1)+str$(w2)+str$(dw)+" "+da$+" "+mo$+" "+ye$: gosub 3050
55555  '  scan forward
14020 CZ$="10":GOSUB 20000
14030 IF HF%<>0 or scquit=1 THEN 14050
55555  '  scan backward
14040   DW=-DW:VB=W1:W1=W2:W2=VB:GOSUB 20000
14050 M8$=MC$:GOSUB 9805:GOSUB 30000
14060 GOSUB 40000
14070 ON ERROR GOTO 3100:ERASE A,B,C%
14080 RETURN
20000 '
55555 ' *** Scan Micrometer ***
20000 '
20000 M8$=STR$(W1):LOCATE 6,SP:PRINT "Setting micrometer to step ";M8$;"  "
20020 GOSUB 9805:GOSUB 9710
20030 FOR MJ=W1+DW TO W2 STEP DW
20040   M8$=STR$(MJ):GOSUB 3220:GOSUB 9900
20050   LOCATE 6,SP:PRINT "   Measuring at step number ";M8$;"  ":GOSUB 9805
20060   GOSUB 9710
20070   PRINT#4,"step#";MJ-DW:GOSUB 2090:gosub 4400:GOSUB 8200:GOSUB 20200:IF HF%<>0 THEN RETURN
55555     'Terminate measurement
20072   IF F(6)>4000000 THEN print#4, "SC: counts too high. Quitting.": scquit=1: RETURN
20080 NEXT
20090 GOSUB 9900:LOCATE 7+14,1:PRINT#4,"step# ";W2:gosub 4400:GOSUB 8200:GOSUB 20200:RETURN
20200 LOCATE 7+ABS(MJ-VAL(MC$)-DW+14)/2,1:MN=MN+1:C%(MN,0)=MJ-DW:A(MN,0)=MS(11):A(MN,1)=MS(10):MU=MU+M2
20210 PRINT MJ-DW,MS(11),MS(10),MN,M2
20215 LOCATE 7+ABS(MJ-VAL(MC$)+14-DW)/2+DW/2,1:PRINT SPACE$(75):RETURN
21000 '
55555 ' *** Wait for Return or Backspace ***
55555 '
21010 A$=INKEY$:IF A$="" THEN 21010
21020 IF ASC(A$)=13 THEN RET=1
21030 RETURN
22000 '
55555 ' *** Printer Positioning ***
22000 '
22000 IF FLAG THEN RETURN
22020 IF P%=0 THEN LL%=6 ELSE LL%=25
22030 FOR ROW=1 TO LL%
22040   FOR COL=1 TO 80
22050     CHAR=SCREEN(ROW,COL):PRINT#2,CHR$(CHAR);
22060 NEXT:PRINT#2,
22070 NEXT
22080 RETURN
23000 '
55555 ' *** Error Handlers ***
55555 '
23000 IF ERR=24 OR ERR=25 OR ERR=57 THEN CLOSE 2:FLAG=-1:RESUME NEXT
23020 RESUME 23030
23030 ON ERROR GOTO 23500:RETURN
23500 ERASE A,B,C%:GOTO 3100
30000 '
55555 ' *** Plot Scan Data, Regression, Find Max/Min ***
55555 '
30000 REM *** Unbias micrometer step and O3 values
30000 FOR I=1 TO MN
30030   C%(0,0)=C%(0,0)+C%(I,0):A(0,0)=A(0,0)+A(I,0)
30040 NEXT
30050 C%(0,0)=C%(0,0)/MN:A(0,0)=A(0,0)/MN
30060 FOR I=1 TO MN
30070   C%(I,0)=C%(I,0)-C%(0,0):A(I,0)=A(I,0)-A(0,0)
30080 NEXT
30090 FOR J=0 TO 1
30100   K=4+J:C%(0,1)=0:N=MN
30110   FOR I=0 TO 3:FOR L=0 TO 3
30120     B(I,L)=0
30130   NEXT:NEXT
30140   FOR I=1 TO N
30150     IF C%(I,1)=-999 THEN 30200
30160     B(1,0)=B(1,0)+C%(I,0)
30170     B(2,0)=B(2,0)+C%(I,0)^2
30180     B(1,1)=B(1,1)+C%(I,0)^3
30190     B(2,1)=B(2,1)+C%(I,0)^4
30200   NEXT
30210   FOR I=1 TO N
30220     IF C%(I,1)=-999 THEN 30260
30230     B(1,2)=B(1,2)+A(I,J)
30240     B(2,2)=B(2,2)+C%(I,0)*A(I,J)
30250     B(1,3)=B(1,3)+A(I,J)*(C%(I,0)^2)
30260   NEXT
55555   REM *** Find coefficients using Crammers method
30280   B(0,0)=N*(B(2,0)*B(2,1)-B(1,1)*B(1,1))+B(1,0)*(B(1,1)*B(2,0)-B(1,0)*B(2,1))+B(2,0)*(B(1,0)*B(1,1)-B(2,0)*B(2,0))      '  Delta
30290   B(0,1)=B(1,2)*(B(2,0)*B(2,1)-B(1,1)*B(1,1))+B(1,0)*(B(1,1)*B(1,3)-B(2,2)*B(2,1))+B(2,0)*(B(2,2)*B(1,1)-B(2,0)*B(1,3)) '  N1
30300   B(0,2)=N*(B(2,2)*B(2,1)-B(1,1)*B(1,3))+B(1,2)*(B(1,1)*B(2,0)-B(1,0)*B(2,1))+B(2,0)*(B(1,0)*B(1,3)-B(2,2)*B(2,0))      '  N2
30310   B(0,3)=N*(B(2,0)*B(1,3)-B(2,2)*B(1,1))+B(1,0)*(B(2,2)*B(2,0)-B(1,0)*B(1,3))+B(1,2)*(B(1,0)*B(1,1)-B(2,0)*B(2,0))      '  N3
30320   B(0,K)=B(0,1)/B(0,0):B(1,K)=B(0,2)/B(0,0):B(2,K)=B(0,3)/B(0,0)
30330   IF C%(0,1)=1 THEN 30480
55555   REM *** Calculate variance and mark bad data
30350   C%(0,1)=1
30360   FOR I=1 TO N
30370     A(I,2)=A(I,J)-B(0,K)-B(1,K)*C%(I,0)-B(2,K)*(C%(I,0)^2)
30380   NEXT
30390   FOR I=1 TO N
30400     B(2,3)=B(2,3) + A(I,2)^2
30410   NEXT
30420   B(2,3)=SQR(B(2,3)/(N-3))
30430   FOR I=1 TO N
30440     IF ABS(A(I,2))>(2*B(2,3)) THEN C%(I,1)=-999 ELSE C%(I,1)=0
30450     IF C%(I,1)=-999 THEN N=N-1
30460   NEXT
30470   GOTO 30110
30480   FOR I = 1 TO MN
30490     C%(I,1)=0
30500   NEXT
30510 NEXT
55555 '
55555 ' *** Initial Information ***
55555 '
31010 ON ERROR GOTO 23000
31020 FLAG=0:A$="LPT1:":CLOSE 2
31022 IF FP%<>1 THEN A$=DD$+"SC"+JD$+YF$+"."+NO$:OPEN A$ FOR APPEND AS 2:ELSE OPEN A$ FOR OUTPUT AS 2
31030 CLS
31040 IF NOT FLAG THEN PRINT#2,CHR$(12)
31050 LOCATE 1,2:PRINT "              Scanning Test for Brewer #";NO$
31060 LOCATE 3,2:PRINT "Date:";MP$;DB$;YE$;" Time: ";TIME$
31070 LOCATE 4,2:PRINT TF$
31080 LOCATE 5,2:PRINT USING "\         \#.###";"MU(AVG) = ";MU/MN
31090 LOCATE 6,2:PRINT "ND filter = ";AF%
31100 VAR2$=STR$(MU/MN):VAR3$=STR$(AF%)
31110 P%=0:GOSUB 22000
31120 C%(1,1)=C%(1,0):C%(2,1)=C%(1,0):B(0,0)=A(1,0):B(0,1)=A(1,0):B(0,2)=A(1,1):B(0,3)=A(1,1)
31130 FOR I=2 TO MN
31140   IF C%(I,0)>C%(1,1) THEN C%(1,1)=C%(I,0)
31150   IF C%(I,0)<C%(2,1) THEN C%(2,1)=C%(I,0)
31160   IF A(I,0)>B(0,0) THEN B(0,0)=A(I,0)
31170   IF A(I,0)<B(0,1) THEN B(0,1)=A(I,0)
31180   IF A(I,1)>B(0,2) THEN B(0,2)=A(I,1)
31190   IF A(I,1)<B(0,3) THEN B(0,3)=A(I,1)
31200 NEXT
55555 '
55555 ' *** Plot Raw Data ***
55555 '
32010 FOR K=4 TO 5
32020   CLS
32030   FOR I=1 TO MN
32040     X1%=10+INT(.5+(C%(I,0)-C%(2,1))*(60/(C%(1,1)-C%(2,1))))
32050     IF K=4 THEN  Y1%=20-INT(.5+(A(I,0)-B(0,1))*(17/(B(0,0)-B(0,1))))
32060     IF K<>4 THEN Y1%=20-INT(.5+(A(I,1)-B(0,3))*(17/(B(0,2)-B(0,3))))
32070     LOCATE Y1%,X1%:IF K=4 THEN PRINT "+" ELSE PRINT "*"
32080   NEXT
32090   IF K=4 THEN YM=0 ELSE YM=100
55555 :REM Plotting regression
32100   FOR I=0 TO 60
32110     X1%=I+10:X=C%(2,1)+I*(C%(1,1)-C%(2,1))/60
32120     Y=B(0,K)+B(1,K)*X+B(2,K)*X^2
32130     IF K=4  THEN Y1%=20-INT(.5+(Y-B(0,1))*(17/(B(0,0)-B(0,1))))
32140     IF K<>4 THEN Y1%=20-INT(.5+(Y-B(0,3))*(17/(B(0,2)-B(0,3))))
32150     LOCATE Y1%,X1%:PRINT "-"
55555     REM Finding max and min
32170     IF (K=4 AND Y>YM) OR (K<>4 AND Y<YM) THEN XM=X:X1M%=X1%:YM=Y:Y1M%=Y1%
32180   NEXT
55555   REM Marking max and min
32200   IF K<>4 THEN 32290
32210     LOCATE Y1M%+1,X1M%:PRINT "^":IF Y1M%>16 THEN 32230
32220       IF (X1M%<21) THEN LOCATE Y1M%+8,11 ELSE LOCATE Y1M%+8,X1M%-10
32230     PRINT "MAX: STEP = ";INT(.5+10*(XM+C%(0,0)))/10
32240     VAR4$=STR$(INT(.5+10*(XM+C%(0,0)))/10):IF Y1M%>15 THEN 32260
32250       IF (X1M%<21) THEN LOCATE Y1M%+9,18 ELSE LOCATE Y1M%+9,X1M%-3
32260     PRINT"O3 = ";INT(.5+10*(YM+A(0,0)))/10
32270     VAR5$=STR$(INT(.5+10*(YM+A(0,0)))/10)
32280     GOTO 32360
32290     LOCATE Y1M%+1,X1M%:PRINT "^":IF Y1M%<10 THEN 32310
32300       IF (X1M%<21) THEN LOCATE Y1M%-9,11 ELSE LOCATE Y1M%-9,X1M%-10
32310     PRINT "MIN: STEP = ";INT(.5+10*(XM+C%(0,0)))/10
32320     VAR6$=STR$(INT(.5+10*(XM+C%(0,0)))/10):IF Y1M%<9 THEN 32340
32330       IF (X1M%<21) THEN LOCATE Y1M%-8,18 ELSE LOCATE Y1M%-8,X1M%-4
32340     PRINT "SO2 = ";INT(.5+10*YM)/10
32350     VAR7$=STR$(INT(.5+10*YM)/10)
32360   GOSUB 35000
32370   P%=1:GOSUB 22000
32380 NEXT:IF NOT FLAG THEN PRINT#2,CHR$(12):CLOSE 2
32390 RETURN
35000 '
55555 ' *** Drawing Axes and Labelling ***
55555 '
35000 J=9
35020 FOR I=0 TO 60
35030   J=J+1:X=C%(2,1)+I*(C%(1,1)-C%(2,1))/60+C%(0,0)
35040   IF J<>10 THEN LOCATE 22,I+10:PRINT CHR$(196)
35050   IF J=10 THEN LOCATE 22,I+10:PRINT CHR$(207):LOCATE 23,I+8:PRINT USING "###.#";X:J=0
35060 NEXT
35070 J=4
35080 FOR I=20 TO 3 STEP -1
35090   J=J+1:IF J<>5 THEN 35140
35100   J=0:IF K=4 THEN Y=B(0,0)-(I-3)*(B(0,0)-B(0,1))/17+A(0,0)
35110   IF K<>4 THEN Y=B(0,2)-(I-3)*(B(0,2)-B(0,3))/17
35120   LOCATE I,9:PRINT CHR$(195)
35130   LOCATE I,2:PRINT USING "###.#";Y:GOTO 35150
35140   LOCATE I,9:PRINT CHR$(179)
35150 NEXT
35160 LOCATE 21,9:PRINT CHR$(179):LOCATE 22,9:PRINT CHR$(207)
35170 LOCATE 13,1:IF K=4 THEN PRINT "O3" ELSE PRINT "SO2"
35180 LOCATE 25,30:PRINT "MICROMETER STEP"
35190 RETURN
40000 '
55555 ' *** Record into Average File ***
55555 '
40000 AVGFILE$="SCOAVG"
55555 REM  VAR1$: Temp       VAR5$: O3
55555 REM  VAR2$: Mu         VAR6$: Min step
55555 REM  VAR3$: Filter     VAR7$: SO2
55555 REM  VAR4$: Max step   VAR8$: Micrometer step before measured
55555 REM
40070 CLOSE 6:OPEN DD$+AVGFILE$+"."+NO$ FOR APPEND AS #6
40080 VAR0$=VAR1$:N%=4:GOSUB 45000:VAR1$=VAR0$
40090 VAR0$=VAR2$:N%=8:GOSUB 45000:VAR2$=VAR0$
40100 VAR0$=VAR3$:N%=4:GOSUB 45000:VAR3$=VAR0$
40110 VAR0$=VAR4$:N%=8:GOSUB 45000:VAR4$=VAR0$
40120 VAR0$=VAR5$:N%=8:GOSUB 45000:VAR5$=VAR0$
40130 VAR0$=VAR6$:N%=8:GOSUB 45000:VAR6$=VAR0$
40140 VAR0$=VAR7$:N%=4:GOSUB 45000:VAR7$=VAR0$
40150 VAR0$=VAR8$:N%=6:GOSUB 45000:VAR8$=VAR0$
40160 PRINT#6,JD$+YE$;" ";VAR1$; " ";VAR2$; " ";VAR3$; " ";VAR4$;
40170 PRINT#6," ";VAR5$;" ";VAR6$; " ";VAR7$; " ";VAR8$
40180 CLOSE 6
40185 B$="end"+" "+VAR1$+" "+VAR2$+" "+VAR3$+" "+VAR4$+" "+VAR5$+" "+VAR6$+" "+VAR7$+" "+VAR8$: gosub 3050
40190 ON ERROR GOTO 23500:RETURN
45000 IF LEN(VAR0$)<N% THEN VAR0$=SPACE$(N%-LEN(VAR0$))+VAR0$
45010 RETURN
55555 '
65529 REM proper last line
