10000 REM ************ pb.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10010 REM               MKII/MKIII/MKIV Playback  Routine
10020 REM
10030 REM                (Sci-Tec Instruments, April 1995)
10040 REM
10050 REM  See Also:
10060 REM *************************************************************
10070 REM
10080 REM  This routine plays back recorded data (NO2 or O3).
10090 REM
10100 REM  Side Effects:
10110 REM
10120 REM  Global Variables Referenced:
10130 REM    F() MS() R%
10140 REM    CL$ CZ$ DA$ DB$ DD$ M5$ M8$ MD$ MDD$ MM$ MO$ MP$ NO$
10150 REM    Q2$ QB$ TE$ WL$ WU$ YE$
10160 REM
10170 REM  Exits: 15040
10180 REM
10190 REM  Uses: 2000, 2030, 2480, 2500, 3100, 3215, 3225, 4200, 4400,
10200 REM        4450, 7700, 8000, 8200
10210 REM
10220 REM **************************************************************
10230 REM  History: dd/mm/yy
10240 REM
10250 REM  04/07/94 - Code cleaned up, combined PB routine to work
10260 REM  with all Brewer models (MKII/MKIII/MKIV).
10270 REM  01/04/94 - Added testing to confirm whether we are in NO2
10280 REM  or O3 mode.
10290 REM **************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA pb
11020 NN$=NO$:ON ERROR GOTO 46000:DIM TT$(1000)
12000 '
12001 ' *** Main Loop ***
12002 '
12010 GOSUB 2500:GOSUB 3225
12020 IF MDD$="o3" THEN HD$="OP" ELSE HD$="NP"
12030 GOSUB 8000:R%=1:MS(0)=11:EZ%=0:REM def't for data file
12040 ON ERROR GOTO 37000
12050 GOSUB 44000
12060 PRINT CL$:FILES DD$+"B*."+NO$
12070 PRINT:LOCATE ,20:PRINT "Enter filename (return for menu) "
12080 GOSUB 2000:FI$=B$:IF FI$="" THEN 15000
12090 CLS:LOCATE 13,20:PRINT"Press HOME to end file "
12100 LOCATE ,20:PRINT"Press Control-END to return to menu"
13000 '
13001 ' *** Scan Playback File ***
13002 '
13010 DC1=0:DC2=0:CLOSE 8:OPEN DD$+FI$+"."+NO$ FOR INPUT AS 8
13020 ON ERROR GOTO 37500
13030 IF EOF(8) THEN 14000
13040 INPUT#8,A$
13050 IF MDD$="o3" THEN IF (A$="sl") OR (A$="ds") OR (LEFT$(A$,1)="z") THEN DC1=DC1+1
13060 IF MDD$="n2" THEN IF (A$="n2sl") OR (A$="n2ds") OR (LEFT$(A$,3)="n2z") THEN DC1=DC1+1
13070 IF MDD$="o3" THEN IF A$="summary" THEN GOSUB 47000
13080 IF MDD$="n2" THEN IF A$="n2summary" THEN GOSUB 47000
13090 GOTO 13030
14000 '
14001 ' *** Perform Playback ***
14002 '
14010 DC1=0:CLOSE 8:OPEN DD$+FI$+"."+NO$ FOR INPUT AS 8
14020 ON ERROR GOTO 37500
14030 IF NN$<>NO$ THEN I=1: GOSUB 5400
14040 GOSUB 4450
14050 IF EZ%=1 THEN 12000
14060 IF EOF(8) THEN 12000
14070 GOSUB 3215:INPUT#8,CC$:IF CC$="" THEN 14060
14080 A$=INKEY$:IF A$=Q2$ THEN 12000
14090 IF A$=QB$ THEN PRINT CL$:CLOSE 8:GOTO 15000
14100 IF MDD$="o3" THEN IF (CC$="sl") OR (CC$="ds") OR (LEFT$(CC$,1)="z") THEN GOSUB 30000: GOTO 14050
14110 IF MDD$="n2" THEN IF (CC$="n2sl") OR (CC$="n2ds") OR (LEFT$(CC$,3)="n2z") THEN GOSUB 30000: GOTO 14050
14120 GOSUB 43000:C$=CC$:IF C$="dh" THEN GOSUB 32000
14130 IF C$="co" THEN GOSUB 40000:CA=VA:GOSUB 40000:M8$=A$:GOSUB 34000:GOTO 14050
14140 IF LEFT$(C$,2)="co" THEN PRINT#4,RIGHT$(C$,LEN(C$)-2)
14150 IF C$="ed" THEN C$=CC$:GOTO 12000
14160 IF (MDD$="o3" AND C$<>"hg") OR (MDD$="n2" AND C$<>"n2hg") THEN 14050
14170 GOSUB 40000:T1$=A$:GOSUB 40000:C5=VA:GOSUB 40000:D5=VA
14180 GOSUB 40000:M8$=A$:GOSUB 40000:MS(15)=VA:GOSUB 40000:TE%=VA
14190 TE$=A$:GOSUB 34000:IF TE%<>0 THEN 14050
14200 C$=A$:GOTO 14100
15000 '
15001 ' *** Restore Old TC's etc and Return ***
15002 '
15010 PRINT CL$:R%=0
15020 IF NN$<>NO$ THEN NO$=NN$: I=1: GOSUB 5400
15030 GOSUB 3225:ERASE TT$
15040 RETURN
30000 '
30001 ' *** Handle SL/DS/Zx Command ***
30002 '
30010 C$=RIGHT$(CC$,2): DC1=DC1+1: TE$=TT$(DC1): TE%=VAL(TE$)
30020 GOSUB 40000:IF A$<>"a" THEN RETURN
30030 IF EOF(8) THEN EZ%=1 ELSE INPUT#8,A$:VA=VAL(A$)
30040 M5$=A$:GOSUB 40000:T0$=A$
30050 IF (VA-TQ)>CY/40+1 THEN A$=C$:C$=D$:GOSUB 43000:GOSUB 8000:D$=A$:C$=A$
30060 TQ=VA:GOSUB 40000:WL=VA:WL$=A$:GOSUB 40000:WU=VA:WU$=A$:GOSUB 40000:CY=VA
30070 CZ$=A$:FOR I=WL TO WU:GOSUB 40000:F(I)=VA:NEXT:GOSUB 4400:GOSUB 8200
30080 FOR I=1 TO 5:GOSUB 40000:NEXT
30090 RETURN
32000 '
32001 ' *** Handle DH Command ***
32002 '
32010 DI%=0:GOSUB 40000:IF DA$<>A$ THEN DI%=1
32020 DA$=A$:GOSUB 40000:IF MO$<>A$ THEN DI%=1
32030 MO$=A$:GOSUB 40000:IF YE$<>A$ THEN DI%=1
32040 YE$=A$:IF DI%=1 THEN DI%=0:GOSUB 41000
32050 GOSUB 40000:IF LO$(0)<>A$ THEN DI%=1
32060 LO$(0)=A$:GOSUB 40000:IF VA<>LA THEN DI%=1
32070 L1$(0)=A$:LA=VA:GOSUB 40000:IF VA<>LO THEN DI%=1
32080 L2$(0)=A$:LO=VA:GOSUB 40000:TE$=A$
32090 IF VA>5 OR VA<0 THEN TE$=STR$((30+VAL(TE$))/16)
32100 R%=1:GOSUB 2480:R%=1
32110 GOSUB 40000:GOSUB 40000
32120 IF (DI%=1) OR (VA<>PZ%) THEN PZ%=VA:L3$(0)=A$:L%=0:GOSUB 42000
32130 RETURN
34000 '
34001 ' *** Print Out Information ***
34002 '
34010 PRINT#4,USING "\      \###C";T1$;TE%;
34020 PRINT#4,USING " ( #.#### ) hg cal'd at step ###.## \ \";C5;D5;
34030 PRINT#4," set to step #";M8$;MS(15):RETURN
37000 '
37001 ' *** File Not Found Error Handler ***
37002 '
37010 IF ERR<>53 THEN 37030
37020 LOCATE ,SP:PRINT "file not found":FOR I=1 TO 1000:NEXT
37030 RESUME 37040
37040 ON ERROR GOTO 37500:GOTO 12070
37500 IF ERR<24 OR ERR>27 THEN ERASE TT$
37510 GOTO 3100
40000 '
40001 ' *** Read the Keyboard ***
40002 '
40010 IF EZ%=1 THEN RETURN
40020 IF EOF(8) THEN EZ%=1:RETURN
40030 INPUT#8,A$:VA=VAL(A$):RETURN
41000 '
41001 ' *** Date Input ***
41002 '
41010 DB$=DA$+"/":DA%= VAL(DB$)
41020 MM=VAL(MO$)*3-2:MO%=VAL(MID$(MD$,MM,3)):MP$=MID$(MN$,MM,3)+" ":YE%=VAL(YE$)
41030 IF R%=1 AND MDD$="o3" THEN PRINT#4,"ozone observations for ";MP$;DB$;YE$
41040 IF R%=1 AND MDD$="n2" THEN PRINT#4,"  NO2 observations for ";MP$;DB$;YE$
41050 GOSUB 7700
41060 RETURN 
42000 '
42001 ' *** Location Input ***
42002 '
42010 LB$=LO$(L%)+" lat= "+L1$(L%)+" long= "+L2$(L%)+R$+"pressure = "+L3$(L%)
42020 PZ$=L3$(L%):PRINT#4,"obs made at ";LB$
42030 LC$=LO$(L%)
42040 RETURN
43000 '
43001 ' *** Summarize If Needed ***
43002 '
43010 IF S(0)<2 THEN RETURN
43020 GOSUB 4200:GOSUB 8000:RETURN
44000 '
44001 ' *** Enter Instrument Number ***
44002 '
44010 PRINT CL$
44020 LOCATE 10,20:PRINT "Instrument selected - ";NO$
44030 LOCATE 11,20:PRINT "If correct press return"
44040 LOCATE 13,20:PRINT "Enter new number nnn - ";:GOSUB 2030
44050 IF A$=CR$ THEN RETURN
44060 NO$=A$:GOSUB 2030:NO$=NO$+A$:GOSUB 2030:NO$=NO$+A$
44070 GOTO 44000
46000 '
46001 ' *** Error handler ***
46002 '
46010 RESUME 12000
47000 '
47001 ' *** Read Temp ***
47002 '
47010 FOR I=1 TO 7:INPUT #8,A$:NEXT
47020 FOR I=DC2 TO DC1:TT$(I)=A$:NEXT:DC2=DC1+1
47030 RETURN
65529 REM proper last line
