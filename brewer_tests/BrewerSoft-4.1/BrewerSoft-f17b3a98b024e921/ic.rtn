10000 REM ************ ic.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10001 REM         MKII/MKIII/MKIV Instrument Configuration
10002 REM         modified 12/01/2003 for IOS uart board Q16%
10003 REM
10004 REM **************************************************************
10005 DATA ic
10010 CLS:PRINT CL$:FG%=0:I11 = 0
10100 OP=17:DIM ZB$(50,1):GOSUB 20000
10110 ON ERROR GOTO 10200: OPEN DD$+NO$+"\op_st."+NO$ FOR INPUT AS 1
10120 FOR I=21 TO 37:INPUT#1,ZB$(I,1):NEXT
10130 zb$(17,1) = "N":zb$(16,1) = "N": FOR I=1 TO 15:INPUT#1,J:ZB$(I,1)="N":IF J=1 THEN ZB$(I,1)="Y": else if i=11 then i11=j
10140 NEXT
10150 IF EOF(1) THEN ZB$(OP+1,1)="menu" ELSE INPUT#1,ZB$(OP+1,1)
10160 IF EOF(1) THEN ZB$(OP+2,1)="o3"   ELSE INPUT#1,ZB$(OP+2,1)
10170 IF EOF(1) THEN ZB$(OP+3,1)=""     ELSE INPUT#1,ZB$(OP+3,1)
10180 CLOSE 1:IF LEFT$(ZB$(OP+2,1),1)="n" THEN ZB$(OP+2,1)="n2" ELSE ZB$(OP+2,1)="o3"
10185 IF I11>1 then Q17%=INT(I11/4): Q16%=INT((I11-Q17%*4)/2): Q11%=I11-Q16%*2-Q17%*4:
10186 IF Q17%=1 THEN zb$(17,1)="Y"
10187 IF Q16%=1 THEN zb$(16,1)="Y"
10188 IF Q11%=1 THEN zb$(11,1)="Y"
10190 GOTO 11000
10200 '
10201 ' *** File Read Error ***
10202 '
10210 RESUME 10220
10220 CLS:PRINT CL$
10230 LOCATE 10,PP:PRINT "op state file was not found"
10240 LOCATE ,PP:PRINT "Attempt to edit file ended"
10250 FOR I=1 TO 5000:NEXT:REM delay until read
10260 CLOSE 1: ERASE ZB$
10270 ON ERROR GOTO 3100: RETURN
11000 '
11001 ' *** Editor ***
11002 '
11100 LOCATE 4,PP:PRINT "  Press Control-END for the menu"
11110 LOCATE ,PP:PRINT " ";B1$;LY$;B2$;LX$;B3$
11120 LOCATE ,PP:PRINT " ";BR$;"             option           ";BR$;"  y/n  ";BR$
11130 LOCATE ,PP:PRINT " ";B7$;LY$;B8$;LX$;B9$
11140 FOR I=1 TO OP+2:LOCATE ,PP:PRINT " ";BR$;"                              ";BR$;"       ";BR$:NEXT
11150 LOCATE ,PP:PRINT " ";B4$;LY$;B5$;LX$;B6$
11160 P=0:V=1
11170 GOSUB 31000
11180 T$=ZB$(V+P,1)
11190 OV=V:VP=4+V:HP=35:GOSUB 30000:COLOR 0,7:LOCATE ,PP+35:PRINT T$;
11200 COLOR 7,0:A$=INKEY$:IF A$="" THEN 11200
11210 A=ASC(A$):IF A$=QB$ THEN 12000
11240 IF A$=Q2$ THEN V=1
11250 IF A$=Q3$ THEN V=1:P=0:GOSUB 31000
11260 IF A$=Q8$ THEN V=V+1:IF V>OP THEN V=1:GOSUB 31000
11270 IF A$=Q7$ THEN V=V-1:IF V<1 THEN V=OP:GOSUB 31000
11280 IF A$="y" OR A$="Y" THEN ZB$(V+P,1)=LEFT$("Y",1):IF V+P=2 THEN FG%=1
11285 IF A$="n" OR A$="N" THEN ZB$(V+P,1)=LEFT$("N",1)
11290 VP=4+OV:HP=35:GOSUB 30000
11300 LOCATE ,PP+34:PRINT " ";ZB$(OV+P,1)" "
11310 GOTO 11180
12000 '
12001 ' *** Save Instrument Configuration ***
12002 '
12110 LOCATE 12,1
12120 OPEN "op_st.fil" FOR OUTPUT AS 1
12130 FOR I=21 TO 37:PRINT#1,ZB$(I,1):NEXT
12135 Q11N% = INT(ASC(ZB$(11,1))/84) + 2*INT(ASC(ZB$(16,1))/84)+ 4*INT(ASC(ZB$(17,1))/84): REM 0,1,2 or 3. ZB$(16,1) is IOS board bit ZB$(17,1) is Proportional Control Board bit
12140 FOR I=1 TO 15:if i=11 then PRINT#1,Q11N%:goto 12146
12145 PRINT#1,INT(ASC(ZB$(I,1))/84)
12146 next 
12150 PRINT#1,ZB$(OP+1,1):PRINT#1,ZB$(OP+2,1):PRINT#1,ZB$(OP+3,1)
12160 CLOSE 1:IF OP<16 THEN OP=16
12170 CLS:PRINT CL$
12180 SHELL "copy op_st.fil "+DD$+NO$+"\op_st."+NO$
12190 GOSUB 5300
13000 '
13001 ' *** Save Average File and Quit ***
13002 '
13010 AVGFILE$ = "OPAVG"
13020 CLOSE 8 : OPEN DD$ + AVGFILE$ + "." + NO$ FOR APPEND AS #8
13030 PRINT#8,"IC,";
13040 PRINT#8, NO$;","; DD$;",";ICF$;",";ZSF$;",";
13050 PRINT#8,DCF$;","; DA$;","; MO$;","; YE$;",";
13060 PRINT#8, LO$;","; L1$;","; L2$;","; L3$;",";
13070 PRINT#8, TE$;","; NC%;","; HC%;","; SR%;",";
13080 PRINT#8, Q1%;","; Q2%;","; Q3%;","; Q4%;",";
13090 PRINT#8, Q5%;","; Q6%;","; Q7%;","; Q8%;",";
13100 PRINT#8, Q9%;",";Q10%;",";Q11%+2*Q16%+4*Q17%;",";Q12%;",";
13110 PRINT#8,Q13%;",";Q14%;",";Q15%;
13120 FOR I=1 TO 3:PRINT#8,ZB$(OP+I,1);",";:NEXT
13130 PRINT#8,TIME$:CLOSE 8:ERASE ZB$:IF FG%=1 THEN C$="az"
13140 GOSUB 4482:RETURN
20000 '
20001 ' *** Options ***
20002 '
20100 ZB$(1,0)="Zenith drive motor   ( Q1%)"
20105 ZB$(2,0)="Azimuth drive motor  ( Q2%)"
20110 ZB$(3,0)="Iris drive motor     ( Q3%)"
20115 ZB$(4,0)="Filter#1 drive motor ( Q4%)"
20120 ZB$(5,0)="Filter#2 drive motor ( Q5%)"
20125 ZB$(6,0)="Clock board          ( Q6%)"
20130 ZB$(7,0)="A/D board            ( Q7%)"
20135 ZB$(8,0)="UVB port             ( Q8%)"
20140 ZB$(9,0)="Filter#3 drive motor ( Q9%)"
20145 ZB$(10,0)="New temp circuit     (Q10%)"
20150 ZB$(11,0)="Second film polarizer(Q11%)"
20155 ZB$(12,0)="Reserved             (Q12%)"
20160 ZB$(13,0)="Wide HG slit present (Q13%)"
20165 ZB$(14,0)="New electronics board(Q14%)"
20170 ZB$(15,0)="Moisture sensor      (Q15%)"
20175 ZB$(16,0)="IOS uart board       (Q16%)"
20176 ZB$(17,0)="PCB board            (Q17%)"
20180 RETURN 
30000 '
30001 ' *** Cursor Positioning ***
30002 '
30110 IF VP<0 THEN 30140
30120 IF HP<0 THEN 30140
30130 LOCATE VP+1,HP+1
30140 RETURN
31000 '
31001 ' *** Output Table of Schedules ***
31002 '
31100 VP=5:HP=0:GOSUB 30000:K=OP:IF K>P+18 THEN K=P+18
31110 FOR I=P+1 TO K:LOCATE ,PP+5:PRINT ZB$(I,0);
31120 LOCATE ,PP+35:PRINT ZB$(I,1):NEXT:RETURN
65529 REM proper last line
