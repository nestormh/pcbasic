10000 REM ************ se.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10001 REM                  MKII/MKIII/MKIV Schedule Editor
10002 REM
10003 REM
10004 REM **************************************************************
10005 DATA se
10100 MAXSKD=100: DIM ZB$(MAXSKD,1): REM Maximum number of entries
10110 D$=""+"":EA$=D$:H(0)=3:H(1)=14:L(0)=8:L(1)=32:CF=0
10120 OP=0:FOR I=1 TO MAXSKD:FOR J=0 TO 1:ZB$(I,J)=LEFT$(SP$,L(J)):NEXT:NEXT 
10130 CLS:PRINT CL$:FILES "*.skd"
10140 PRINT "Enter schedule name (no extension)  ";:GOSUB 2000
10150 IF B$="" OR LEN(B$)>8 THEN ON ERROR GOTO 3100:ERASE ZB$:RETURN
10160 ON ERROR GOTO 10500
10180 D$=B$:EA$=B$
10190 CLOSE 6:OPEN B$+".skd" FOR INPUT AS 6
10210 OP=OP+1:INPUT#6,ZB$:ZB$(OP,0)=LEFT$(ZB$+SP$,L(0)):IF ZB$="180" THEN 10230
10220 INPUT#6,ZB$:ZB$(OP,1)=LEFT$(ZB$+SP$,L(1)):GOTO 10210
10230 OP=OP-1:INPUT#6,D$
10240 CLOSE 6:GOTO 10520
10500 '
10501 ' *** File not Found ***
10502 '
10510 RESUME 10520
10520 ON ERROR GOTO 10600: REM reset error vector
10530 GOTO 11000: REM Fall through to the editor
10600 ERASE ZB$: GOTO 3100
11000 '
11001 ' *** Editor ***
11002 '
11100 CLS:LOCATE ,PP:PRINT "  Schedule - ";D$;:LOCATE ,53:PRINT "Control END"
11105 LOCATE ,PP:PRINT "Number of operations: ";op;:LOCATE 2,54:PRINT "to end"
11110 LOCATE ,PP:PRINT B1$;LEFT$(LY$,10);B2$;LEFT$(LY$,10);LY$;B3$
11120 LOCATE ,PP:PRINT BR$;" za start ";BR$;"       Command Sequence       ";spc(10);BR$
11130 LOCATE ,PP:PRINT B7$;LEFT$(LY$,10);B8$;LEFT$(LY$,10);LY$;B9$
11140 FOR I=1 TO 17:LOCATE ,PP:PRINT BR$;SPC(10);BR$;SPC(40);BR$:NEXT
11150 LOCATE ,PP:PRINT B4$;LEFT$(LY$,10);B5$;LY$;LEFT$(LY$,10);B6$
11160 P=0:V=1:H=0:C=1:IF OP<1 THEN OP=1
11170 GOSUB 31000
11180 ZB$=ZB$(V+P,H):LL$=LEFT$(ZB$,C-1)
11185 T$=MID$(ZB$,C,1):RR$=RIGHT$(ZB$,L(H)-C)
11190 OV=V:OH=H:OC=C:VP=5+V:HP=H(H)+C-1:GOSUB 30000:COLOR 0,7:PRINT T$;
11200 COLOR 7,0:A$=INKEY$:IF A$="" THEN 11200
11210 A=ASC(A$):IF A$=QB$ THEN 12000
11220 IF A$="^" THEN A=0:CF=ABS(CF-1):LOCATE 6,62:PRINT CHR$(32-(CF=1)*62)
11230 IF A$=QF$ THEN A=0:IF OP>P+17 THEN P=OP-17:GOSUB 31000
11240 IF A$=Q2$ THEN V=1:H=0:C=1:REM home
11250 IF A$=Q3$ OR A$=QE$ THEN V=1:H=0:C=1:P=0:A=0:GOSUB 31000
11260 IF A$<>CR$ and A$<>QC$ THEN 11280
11270 H=H+1:C=1
11275 IF H>1 THEN H=0:A$=Q8$:IF V+P=OP AND OP<MAXSKD THEN OP=OP+1:GOSUB 31000
11280 IF A$=QD$ THEN H=H-1:C=1:IF H<0 THEN H=1:A$=Q7$
11290 IF A$=Q8$ AND OP>V+P THEN V=V+1:IF V>17 THEN V=17:P=P+1:GOSUB 31000
11300 IF A$=Q7$ AND V+P>1 THEN V=V-1:IF V<1 THEN V=1:P=P-1:GOSUB 31000
11310 IF A$<>Q0$ OR OP=MAXSKD OR CF=0 THEN 11340:REM on insert a char (ins key)
11320 FOR I=OP TO P+V STEP -1:FOR J=0 TO 1:ZB$(I+1,J)=ZB$(I,J):NEXT:NEXT 
11330 C=1:OP=OP+1:FOR J=0 TO 1:ZB$(P+V,J)=LEFT$(SP$,L(J)):NEXT:GOSUB 31000
11340 IF ((A$<>Q1$) AND (A$<>Q5$)) OR (OP<1) OR (CF=0) THEN 11400
11350 IF P+V>=MAXSKD THEN 11360
11355 FOR I=P+V TO OP-1:FOR J=0 TO 1:ZB$(I,J)=ZB$(I+1,J):NEXT:NEXT 
11360 FOR J=0 TO 1:ZB$(OP,J)=LEFT$(SP$,L(J)):NEXT:GOSUB 31000
11370 OP=OP-1:LOCATE 2,PP+23:PRINT OP:IF OP<1 THEN OP=1
11380 IF (OP-P<17) AND (P>0) THEN P=P-1
11390 IF V>OP THEN V=V-1
11400 IF A$=Q6$ THEN C=C+1
11410 IF A$=Q4$ THEN C=C-1
11420 IF A$=Q0$ AND RIGHT$(RR$,1)=" " AND CF=0 THEN ZB$(V+P,H)=LL$+" "+T$+RR$
11430 IF (A$=Q1$ OR A$=Q5$) AND CF=0 THEN ZB$(V+P,H)=LL$+RR$+" "
11440 A=(A AND 127):IF (A>31) AND (A<123) THEN ZB$(V+P,H)=LL$+A$+RR$:C=C+1
11450 IF C<1 THEN C=1
11460 IF C>L(H) THEN C=L(H)
11470 ZB$(V+P,H)=LEFT$(ZB$(V+P,H)+SP$,L(H))
11480 VP=5+OV:HP=H(OH):GOSUB 30000
11490 PRINT ZB$(OV+P,OH)
11500 GOTO 11180
12000 '
12001 ' *** Save Schedule ***
12002 '
12100 PRINT CL$:LOCATE 10,SP:PRINT "Next day's schedule name? ";D$;:B$=D$
12110 LE=10:GOSUB 20110:D$=B$:ON ERROR GOTO 12140
12120 PRINT CL$:LOCATE 10,SP
12125 PRINT "Schedule name to be saved? ";EA$;:B$=EA$:LE=10:GOSUB 20110
12130 OPEN B$+".skd" FOR OUTPUT AS 6:GOTO 12150
12140 CLOSE 6:LOCATE 12,SP
12145 PRINT "Disk error number ";ERR:FOR I=1 TO 5000:NEXT:RESUME 12120
12150 ON ERROR GOTO 10600:A$=" "+"":FOR I=1 TO OP:J=1:L=0:B$=ZB$(I,J)
12155 L=L+1:IF MID$(B$,L,1)<>A$ THEN IF L<=L(J) THEN 12155
12156 ZB$(I,J)=LEFT$(B$,L-1)
12160 IF ZB$(I,0)="" OR ZB$(I,1)="" THEN 12170
12165 PRINT#6,ZB$(I,0):PRINT#6,ZB$(I,1)
12170 NEXT:PRINT#6,"180":PRINT#6,D$:CLOSE 6
12180 ON ERROR GOTO 3100: ERASE ZB$: RETURN 
20000 '
20001 ' *** Input Subroutine ***
20002 '
20100 B$=""
20110 L=LEN(B$):A$=INKEY$:A=ASC(A$+CHR$(0))
20120 IF (A=13) THEN RETURN 
20130 IF ((A$=Q1$) OR (A$=Q5$)) AND L>0 THEN B$=LEFT$(B$,L-1):PRINT BS$;
20140 A=(A AND 127):IF (A>31) AND (L<LE) THEN B$=B$+CHR$(A):PRINT A$;
20150 GOTO 20110
30000 '
30001 ' *** Cursor Positioning ***
30002 '
30110 IF VP<0 THEN 30140
30120 IF HP<0 THEN 30140
30130 LOCATE VP,PP+HP
30140 RETURN
31000 '
31001 ' *** Output Table of Entries ***
31002 '
31100 VP=2:HP=22:GOSUB 30000:K=OP:IF K>P+17 THEN K=P+17
31110 LOCATE 2,PP+23:PRINT OP:LOCATE 6,PP::FOR I=P+1 TO K
31120 FOR J=0 TO 1:LOCATE ,PP+H(J):PRINT ZB$(I,J);:NEXT:PRINT:NEXT 
31150 RETURN 
65529 REM proper last line
