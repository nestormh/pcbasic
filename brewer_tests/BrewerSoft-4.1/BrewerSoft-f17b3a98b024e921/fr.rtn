10000 REM ************ fr.rtn 09/04/2012 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
55555 REM         MKII/MKIII/MKIV Micrometer and FW#3 Reset
55555 REM
55555 REM             (SCI-TEC Instruments, August 1998)
55555 REM
55555 REM  See also:
55555 REM ************************************************************
55555 REM
55555 REM  This routine performs a reset of the micrometer(s) (and
55555 REM  filterwheel #3 if present) for both O3 and NO2 modes of
55555 REM  operation.
55555 REM
55555 REM  Side Effects:
55555 REM
55555 REM  Global Variables Referenced:
55555 REM    SPACE$()
55555 REM    PC MX% MY% MZ% NMX% NMZ% TE%
55555 REM    DD$ JD$ M8$ MC$ MDD$ NO$ O1$ TYP$ YF$
55555 REM
55555 REM  Exits: 12040
55555 REM
55555 REM  Uses:  3100, 9190, 9450
55555 REM
55555 REM ************************************************************
55555 REM  History: dd/mm/yy
55555 REM  09/04/12 - Fixed mioavg output
55555 REM  25/06/02 - Added temperature check to fix MIOAVG file
55555 REM  22/05/98 - Replaced new electronics reset algorithm
55555 REM  18/02/98 - Q9% support added
55555 REM  14/07/95 - Fixed MIOAVG file output, based on Brewer type
55555 REM  19/01/95 - Supports new Brewer electronics
55555 REM  14/09/94 - Corrected operation of routine on MKIII Brewer
55555 REM  18/08/94 - MX%/MY%/MZ% confusion done away with
55555 REM  07/07/94 - Merged all FR and MR routines into one version
55555 REM  24/05/94 - References to mode.fil removed, modified to
55555 REM  handle both o3 and no2 modes using MDD$, MIOAVG.nnn file
55555 REM  now contains mode distinction o3 or n2
55555 REM ************************************************************
10700 DATA fr
11000 VAR3$ = "-999":VAR4$ = "-999":VAR5$ = "-999":VAR6$ = "-999":IF Q14% = 1 THEN GOSUB 15000: IF MO = 0 THEN B$="MOTOR ORIGINS CORRUPTED. QUITTING.": PRINT#4, B$: PRINT B$: GOSUB 3050: HF%=1: RETURN
11001 GOSUB 2450 	'get temperature
11002 ' *** Perform Search and Reset Micrometers ***
11003 GOSUB 16000: IF Q9%=1 AND TYP$ <> "mkii" THEN GOSUB 30000
11004 GOSUB 32000:REM Save Average File
11005 NEED.FR=2:NEED.HG=1:HP.NOTDONE=1:RETURN
11010 B$="Reset for "+TYP$+" "+MDD$+" - Searching for micrometer #"+MICN$+" reference"
11020 PRINT#4,:PRINT#4,B$:PRINT B$:Y=0:VA=0
11030 '
11040 O1$=GETLED$:GOSUB 9450:GOSUB 9190: GOSUB 18000: IF BLOCKED%=0 THEN 11070
11050 O1$="M,"+MIC$+",-500":Y=Y-500:GOSUB 9450:GOSUB 9190:O1$=GETLED$:GOSUB 9450:GOSUB 9190
11060 IF Y<=-3000 THEN B$="-3000 steps - QUIT - Micrometer #"+MICN$+" jammed at MIN. end ?": PRINT#4, B$: GOSUB 3050:NEED.FR=2: RETURN
11070 GOSUB 18000: IF BLOCKED%=1 THEN 11050
11080 O1$="M,"+MIC$+",-1:M,"+MIC$+",501":Y=Y-1+501:GOSUB 9450:GOSUB 9190:O1$=GETLED$:GOSUB 9450:GOSUB 9190
11090 IF Y>11000 THEN B$="11000 steps - QUIT - Micrometer #"+MICN$+" reference not found": PRINT#4, B$: GOSUB 3050:NEED.FR=2: RETURN
11100 GOSUB 18000: IF BLOCKED%=0 THEN 11080
11110 O1$="M,"+MIC$+",-500":GOSUB 9450
11120 Y=Y-500
11130 O1$="M,"+MIC$+",-1:M,"+MIC$+",35":Y=Y-1+35:GOSUB 9450:GOSUB 9190:O1$=GETLED$:GOSUB 9450:GOSUB 9190
11140 GOSUB 18000: IF BLOCKED%=0 THEN 11130
11150 O1$="M,"+MIC$+",-100":GOSUB 9450
11160 Y=Y-100
11170 YY%=0
11180 YY%=YY%+1
11190 O1$="M,"+MIC$+","+MID$(STR$(YY%),2):Y=Y+1:LOCATE 22,30:GOSUB 9450:GOSUB 9190:O1$=GETLED$:GOSUB 9450:GOSUB 9190
11200 GOSUB 18000: IF BLOCKED%=0 THEN 11180
11210 IF MIC = 10 THEN B$ = " (+CAL)": YMC$=STR$(INT(Y)+VAL(MC$)) ELSE B$ = "": YMC$=STR$(INT(Y))
11220 B$="Micrometer #"+MICN$+" reference found at step"+STR$(Y)+B$+"="+YMC$
11230 B$=B$+" Now set to "+MDD$+" offset ="+STR$(OFFSET)
11240 PRINT#4,B$:PRINT B$
11250 M8$=STR$(-OFFSET):IF MIC=10 THEN GOSUB 9810 ELSE GOSUB 9815
11270 IF MIC = 10 THEN M8$=MC$:GOSUB 9810 ELSE M8$="-"+MC$: GOSUB 9815: M8$=MC$:GOSUB 9815
11280 GOSUB 3050:RETURN
15000 '
15005 O1$="?MOTOR.ORIGIN[10]":GOSUB 9450:GOSUB 9190:B$ = "MOTOR.ORIGIN[10]= "+I$: PRINT#4, B$: MO=VAL(I$)
15010 IF TYP$="mkiii" THEN O1$="?MOTOR.ORIGIN[9]":GOSUB 9450:GOSUB 9190:B$="MOTOR.ORIGIN[9]= "+I$: PRINT#4, B$: MO=MO*VAL(I$)
15099 RETURN
55555 '--------------------------------  Set up variables for LED, MIC and such ------------------------------------
55555 '
55555 ' Micrometer #1
55555 '
16000 LEND$="HIGH":MIC=10:MIC$="10":MICN$="1":
16010 IF MDD$ = "o3" THEN OFFSET=(MZ%) ELSE OFFSET=(MZ%+SWITCH%)
16020 IF Q14%=1 THEN GETLED$ = "?MOTOR.REF."+LEND$+"["+MIC$+"]" ELSE GETLED$ = "G,544"
16030 GOSUB 11010: VAR3$=YMC$: VAR4$=STR$(OFFSET):IF  TYP$<>"mkiii" THEN RETURN
55555 '
55555 ' Micrometer #2
55555 '
16040 LEND$="HIGH":MICN$="2"
16045 IF Q14%=1 THEN MIC=9:MIC$="9" ELSE MIC=6:MIC$="6"
16050 OFFSET=(MY%)
16060 IF Q14%=1 THEN GETLED$ = "?MOTOR.REF."+LEND$+"["+MIC$+"]" ELSE GETLED$ = "G,800"
16070 GOSUB 11010: VAR5$=YMC$: VAR6$=STR$(OFFSET):
16099 RETURN
55555 '--------------------------------  END Set up variables for LED, MIC and such ------------------------------------
55555 '
55555 '
55555 '
55555 '--------------------------------  Checking if LED is blocked ------------------------------------
55555 '
18000 IF MIC=10 THEN LED=4 ELSE LED=32
18005 IF Q14%=1 THEN IF INSTR(I$,"ON") = 0 THEN BLOCKED% = 0 ELSE BLOCKED% = 1 ELSE IF (VA AND LED)<>LED THEN BLOCKED% = 0 ELSE BLOCKED% = 1
18007 IF LED=32 AND Q14% <> 1 THEN IF BLOCKED%=0 THEN BLOCKED%=1 ELSE BLOCKED%=0
18010 PRINT Y,BLOCKED%
18100 RETURN
55555 '
55555 '--------------------------------  END Checking if LED is blocked --------------------------------
30000 '
30001 ' *** Zero Filterwheel #3 ***
30002 '
30010 IF Q14%=1 THEN FLAG=1:M1$="6":GOSUB 6550:GOTO 30230
30020 IF TYP$="mkiii" THEN O1$="M,10,-" + MC$: GOSUB 9450:REM Remember pos of first micrometer
30030 PRINT CL$;"Zeroing filterwheel #3":PRINT#4,"Zeroing filterwheel #3":Y=9
30040 IF TYP$="mkiv" THEN O1$="M,6,-1:M,6,10:G,800" ELSE O1$="B,3:M,10,-1:M,10,10:G,544"
30050 GOSUB 9450:GOSUB 9190
30060 YY=VA:PRINT Y;" => ";VA
30070 IF TYP$="mkiv" THEN O1$="M,6,-25:G,800" ELSE O1$="B,3:M,10,-25:G,544"
30080 Y=Y-25:GOSUB 9450:GOSUB 9190
30090 IF Y<-600 THEN B$ = "Filterwheel #3 reference not found":PRINT#4,B$: GOSUB 3050: GOTO 31000
30100 IF(YY-VA)<>32 THEN 30060
30110 IF TYP$="mkiv" THEN O1$="M,6,25:G,800" ELSE O1$="B,3:M,10,25:G,544"
30120 Y=Y+25:GOSUB 9450:GOSUB 9190
30130 YY=VA:PRINT Y:PRINT Y;" => ";VA
30140 IF TYP$="mkiv" THEN O1$="M,6,-10:G,800" ELSE O1$="B,3:M,10,-10:G,544"
30150 Y=Y-10:GOSUB 9450:GOSUB 9190
30160 IF(YY-VA)<>32 THEN 30130
30170 IF TYP$="mkiv" THEN O1$="M,6,10:G,800" ELSE O1$="B,3:M,10,10:G,544"
30180 Y=Y+10:GOSUB 9450:GOSUB 9190
30190 YY=VA:PRINT Y:PRINT Y;" => ";VA
30200 IF TYP$="mkiv" THEN O1$="M,6,-1:G,800" ELSE O1$="B,3:M,10,-1:G,544"
30210 Y=Y-1:GOSUB 9450:GOSUB 9190
30220 IF(YY-VA)<>32 THEN 30190
30230 B$="Found reference at step "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
30240 IF Q14%=1 THEN B$="FW#3 discrepancy "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
30250 IF MDD$="o3" THEN B$=B$+STR$(MX%) ELSE B$=B$+STR$(NMX%)
30260 PRINT B$:PRINT#4,B$:PRINT#4,
31000 '
31010 VAR7$=STR$(Y)                 'Step for FW#3
31020 IF TYP$="mkiii" AND Q14%=0 THEN 31030 ELSE 31070
31030   VAR8$=MID$(STR$(MX%),2)             'FW#3 Position
31040   O1$="M,10,"+VAR8$:GOSUB 9450:GOSUB 9840:O1$="M,10,-1;M,10,2;M,10,-1:M,10,"+MC$
31060   GOSUB 9450:GOTO 31200:REM Restore first micrometer pos
31070 IF MDD$="o3" THEN 31080 ELSE 31100
31080   VAR8$=MID$(STR$(MX%),2)             'FW#3 Position
31090   O1$="M,6,"+VAR8$:GOSUB 9450:GOTO 31200
31100   VAR8$=MID$(STR$(NMX%),2)            'FW#3 Position
31110   O1$="M,6,"+VAR8$:GOSUB 9450
31200 RETURN
32000 '
32010 ' ***  Append to Average File ***
32020 '
32030 ON ERROR GOTO 32300
32040 AVGFILE$="MIOAVG"                 'Average File Name
32050 VAR1$=STR$(TE%)                   'Temperature
32060 VAR2$=M8$                         'Last Cal Step
32070 OPEN DD$+AVGFILE$+"."+NO$ FOR APPEND AS #6
32080 IF LEN(VAR1$) < 10 THEN VAR1$=SPACE$(7-LEN(VAR1$))+VAR1$
32090 IF LEN(VAR2$) < 10 THEN VAR2$=SPACE$(10-LEN(VAR2$))+VAR2$
32100 IF LEN(VAR3$) < 10 THEN VAR3$=SPACE$(10-LEN(VAR3$))+VAR3$
32110 IF LEN(VAR4$) < 10 THEN VAR4$=SPACE$(10-LEN(VAR4$))+VAR4$
32120 IF LEN(VAR5$) < 10 THEN VAR5$=SPACE$(10-LEN(VAR5$))+VAR5$
32130 IF LEN(VAR6$) < 10 THEN VAR6$=SPACE$(10-LEN(VAR6$))+VAR6$
32140 IF LEN(VAR7$) < 10 THEN VAR7$=SPACE$(10-LEN(VAR7$))+VAR7$
32150 IF LEN(VAR8$) < 10 THEN VAR8$=SPACE$(10-LEN(VAR8$))+VAR8$
32160 '
32170 PRINT#6,JD$+YF$;" ";MDD$;" ";VAR1$;" ";VAR2$;" ";VAR3$;" ";VAR4$;
32180 IF TYP$="mkii"  THEN PRINT#6," "
32190 IF TYP$="mkiii" THEN PRINT#6," ";VAR5$;" ";VAR6$;
32200 IF TYP$<>"mkii" THEN IF Q9%=1 THEN PRINT#6," ";VAR7$;" ";VAR8$ ELSE PRINT#6," "
32210 CLOSE 6
32220 ON ERROR GOTO 3100 : RETURN
32300 IF ERR = 53 AND ERL = 32070 THEN RESUME 32210 ELSE RESUME 32220
55555 '
65529 REM proper last line
