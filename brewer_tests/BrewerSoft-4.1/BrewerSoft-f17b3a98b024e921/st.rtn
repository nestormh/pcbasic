10000 REM ************ st.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10001 REM             MKII/MKIII/MKIV Brewer Status Tests
10002 REM
10003 REM 17/07/98: Fixed azimuth/zenith position bug
10004 REM *************************************************************
10005 DATA st
16200 REM -16599 Brewer status and control
16210 PRINT CL$:LOCATE 4,SP+6:PRINT "Brewer Status and Control"
16220 LOCATE ,SP:PRINT B1$;LEFT$(LY$,24);B2$;LEFT$(LY$,11);B3$
16230 LOCATE ,SP:PRINT BR$;"        Function        ";BR$;"   State   ";BR$
16240 LOCATE ,SP:PRINT B7$;LEFT$(LY$,24);B8$;LEFT$(LY$,11);B9$
16250 RESTORE:READ PC$:FOR I=1 TO 8:READ A$:LOCATE ,SP:PRINT BR$;"  ";A$;
16255 GOSUB 16390:LOCATE ,SP+25:PRINT BR$;"  ";B$;:LOCATE ,SP+37:PRINT BR$:NEXT
16260 LOCATE ,SP:PRINT B4$;LEFT$(LY$,24);B5$;LEFT$(LY$,11);B6$
16270 LOCATE ,SP+8:PRINT "   Press control-END for menu":P=1:C=1
16280 I=P:GOSUB 16390:GOSUB 16380
16282 LE$=LEFT$(B$,C-1):MI$=MID$(B$,C,1):RI$=RIGHT$(B$,LEN(B$)-C)
16284 GOSUB 16396:PRINT B$:GOSUB 16395:COLOR 0,7:PRINT MI$;:COLOR 7,0
16290 G$=INKEY$:GOSUB 9890:IF G$="" THEN 16290
16291 G=(F=1) AND (G$=CR$ OR G$=Q5$ OR G$=Q2$ OR G$=Q8$ OR G$=Q7$)
16292 IF G THEN GOSUB 16395:ON P-5 GOSUB 29000,29100,29200:F=0:C=1:GOTO 16330
16293 IF G$=QB$ THEN RETURN:REM back arrow
16294 IF G$=Q2$ THEN P=1:C=1:REM home to top of screen
16295 IF G$=Q8$ OR G$=CR$ THEN C=1:P=P+1:IF P>8 THEN P=1:REM cursor down
16296 IF G$=Q7$ THEN C=1:P=P-1:IF P<1 THEN P=8:REM cursor up
16297 IF B$="n/a" THEN 16330
16298 IF G$=Q6$ AND P>5 THEN IF C<9 THEN C=C+1:REM cursor right
16299 IF G$=Q4$ AND P>5 THEN IF C>1 THEN C=C-1:REM cursor left
16300 IF G$=Q0$ AND P>5 AND RIGHT$(B$,1)=" " THEN B$=LEFT$(LE$+" "+MI$+RI$,9):F=1
16301 IF (G$=Q1$ OR G$=Q5$) AND P>5 AND C>0 THEN B$=LE$+RI$+" ":F=1
16303 FG%=0:IF G$="p" OR G$="P" THEN FG%=-64
16304 IF G$="n" OR G$="N" THEN FG%=64
16305 IF (FG%<>0) AND (P<6) THEN ON I GOSUB 16500,16500,16560,16580,16590
16306 FG%=0
16310 IF P<6 THEN 16330
16315 G=(G$>="0" AND G$<="9" OR P>6 AND G$="." OR G$=" " OR P=7 AND G$="-" OR P=6 AND G$="-")
16320 IF G THEN B$=LE$+G$+RI$:C=C+1:F=1
16330 IF C<1 THEN C=1
16340 IF C>9 THEN C=9
16350 GOSUB 16396:PRINT B$;:IF F=1 THEN 16282
16360 GOTO 16280
16380 IF P>5 OR B$="n/a" THEN 16386
16382 LOCATE 21,SP+8:COLOR ,1:PRINT "N";:COLOR ,0:PRINT " for next state"
16384 LOCATE ,SP+8:COLOR ,1:PRINT "P";:COLOR ,0:PRINT " for previous state"
16385 RETURN
16386 LOCATE 21,SP+8:PRINT SP$
16388 LOCATE ,SP+8:PRINT SP$:RETURN
16390 ON I GOTO 16420,16400,16440,16460,16470,16475,16480,16490
16395 LOCATE I+7,SP+27+C:RETURN
16396 LOCATE I+7,SP+28:RETURN
16399 :
16400 B$="off":IF M9$="1" OR M9$="3" THEN B$="on "
16410 RETURN 
16420 B$="off":IF M9$="2" OR M9$="3" THEN B$="on "
16430 RETURN 
16440 B$="n/a":IF Q3%=1 THEN B$="open ":IF M3$="0" THEN B$="close"
16450 RETURN 
16460 B$="n/a":IF Q4%=1 THEN M4%=VAL(M4$):B$=CHR$(53-M4%/64)
16465 RETURN 
16470 B$="n/a":IF Q5%=1 THEN M5%=VAL(M5$):B$=CHR$(48+M5%/64):RETURN
16472 RETURN 
16475 M8%=VAL(M8$):M8$=MID$(STR$(M8%),2+(M8%<0)):B$=LEFT$(M8$+SP$,9):RETURN
16480 B$="n/a":ZA=180-360*(ZE%-HC%-.5)/ER%
16482 IF Q1%=1 THEN B$=MID$(STR$(INT(ZA*100+.5)/100)+SP$,2,9):RETURN
16485 RETURN 
16490 B$="n/a":AZ=360*(AZ%-NC%-UC%-.5)/SR%
16492 IF Q2%=1 THEN B$=MID$(STR$(INT(AZ*100+.5)/100)+SP$,2,9):RETURN
16495 RETURN 
16499 :
16500 IF M9$="0" THEN A$="1":IF P=1 THEN A$="2"
16510 IF M9$="1" THEN A$="0":IF P=1 THEN A$="3"
16520 IF M9$="2" THEN A$="3":IF P=1 THEN A$="0"
16530 IF M9$="3" THEN A$="2":IF P=1 THEN A$="1"
16540 M9$=A$+"":GOSUB 9850:RETURN 
16560 IF M3$="0" THEN GOSUB 9750:RETURN 
16570 GOSUB 9740:RETURN 
16580 M4%=VAL(M4$)-FG%:IF M4%<0 OR M4%>320 THEN RETURN
16585 M4$=MID$(STR$(M4%),2):GOSUB 9790:RETURN
16590 M5%=VAL(M5$)+FG%:IF M5%<0 OR M5%>320 THEN RETURN
16595 M5$=MID$(STR$(M5%),2):GOSUB 9800:RETURN
16599 : 
29000 M8%=VAL(B$):M8$=MID$(STR$(M8%),2+(M8%<0)):GOSUB 9810:RETURN
29100 REM -29199 adjust zenith motor
29120 ZA=VAL(B$)
29130 ZE%=ER%*(1-ZA/180)/2+.5+HC%
29140 O1$="M,1,"+STR$(ZE%):GOSUB 9450
29150 RETURN 
29199 : 
29200 REM -29299 adjust azimuth motor
29220 AZ=VAL(B$)
29230 AZ%=SR%*AZ/360+.5+NC%+UC%:IF AZ%>SR% THEN AZ%=AZ%-SR%
29240 IF AZ%<0 THEN AZ%=AZ%+SR%
29250 O1$="M,2,"+STR$(AZ%):GOSUB 9450
29260 RETURN 
29299 : 
30000 REM data
30010 DATA standard lamp
30020 DATA mercury lamp
30030 DATA iris
30040 DATA filter #1 (position)
30050 DATA filter #2 (position)
30060 DATA micrometer (step)
30070 DATA zenith (degree)
30080 DATA azimuth (degree)
65529 REM dummy last line
