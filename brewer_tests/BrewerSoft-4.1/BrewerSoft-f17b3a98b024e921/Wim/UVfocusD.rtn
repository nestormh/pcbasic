10000 REM ********** UV focus Recombining routine 30/10/10 ***********
10010 REM            MKIII Alignment of Two Spectrometers
10020 REM        
10040 REM
10050 REM  See also:
10060 REM ************************************************************
10070 REM
10260 REM
10270 REM ************************************************************
10280 REM  History: dd/mm/yy
10290 REM  First version
10350 REM ************************************************************
10500 DATA UVfocusd
11000 '
11001 ' *** Initialization ***
11002 '
11010 IF TYP$<>"mkiv" THEN RETURN
11015 ON ERROR GOTO 3100
11020 IF cts=1 THEN GOTO 11030 'The cts flag prevents redimentioning of the arrays
11025 DIM CTS(400):cts=1
11030 GOSUB 9830: GOSUB 9860: GOSUB 6700 'setting Lamp, Iris and zenith to correct position
11040 GOSUB 6900:IF HF%=1 THEN RETURN 'Lamp test
11050 GOSUB 2450:HLAST%=TE%:HTIME$=TIME$:GOSUB 3900:GOSUB 50'lamp warmup
11060 TI=TIMER*60:TD=TI+1500
11070 T0=TA/3600:GOSUB 8600:PRINT CL$:LOCATE ,SP
11080 PRINT "Waiting until ";H$;" for lamp warmup":GOSUB 6800
11090 MAXVA=1:FW2F%=192
11501 ' Measurement
11510 FOR I = FW2F%-12 TO FW2F%+12
11520 	M4$=STR$(I): GOSUB 9790
11530 	O1$="R,4,4,10;O":GOSUB 9450
11540 	CTS(I)=VA:IF VA>MAXVA THEN MAXVA=VA:MAXI=I:
11550 	PRINT USING "####   ########";I;VA:
11560 NEXT
11600 'Calculate right 50% point
11610 RIGHTI=1:RIGHT=1:
11620 FOR I = FW2F%-15 TO MAXI
11630 	RIGHTTEMP=ABS(CTS(I)/MAXVA-0.5): 
11640 	IF RIGHTTEMP<RIGHT THEN RIGHT=RIGHTTEMP: RIGHTI=I:RIGHTVA=CTS(I)
11650 NEXT
11700 'Calculate left 50% point
11710 LEFTI=1:LEFT=1:
11720 FOR I = MAXI TO FW2F%+15
11730 	LEFTTEMP=ABS(CTS(I)/MAXVA-0.5): 
11740 	IF LEFTTEMP<LEFT THEN LEFT=LEFTTEMP: LEFTI=I:LEFTVA=CTS(I)
11750 NEXT
11800 'Print results
11810 PRINT#4, "Points for UV focus test"
11820 PRINT#4, "POSITION: STEP   INTENSITY"
11830 PRINT#4,   USING "LEFT    : ####   #########";LEFTI;LEFTVA
11840 PRINT#4,   USING "CENTRE  : ####   #########";MAXI;MAXVA
11850 PRINT#4,   USING "RIGHT   : ####   #########";RIGHTI;RIGHTVA
12000 'Centre
12010 M4$=STR$(MAXI):GOSUB 9790:HP%=0
12020 PRINT#4, "Centre:"
12030 IL=1:GOSUB 13100
12100 'Left
12110 M4$=STR$(LEFTI):GOSUB 9790:HP%=0
12120 PRINT#4, "Left:"
12130 IL=2:GOSUB 13100
12200'Centre
12210 M4$=STR$(MAXI):GOSUB 9790:HP%=0
12220 PRINT#4, "Centre:"
12230 IL=3:GOSUB 13100
12300 'Right
12310 M4$=STR$(RIGHTI):GOSUB 9790:HP%=0
12320 PRINT#4, "Right:"
12330 IL=4:GOSUB 13100
12400'Centre
12410 M4$=STR$(MAXI):GOSUB 9790:HP%=0
12420 PRINT#4, "Centre:"
12430 IL=5:GOSUB 13100
12510 PRINT#4, "UV focus test summary - Dispersion spectrometer"
12520 PRINT#4, "POSITION: STEP   INTENSITY"
12530 PRINT#4,   USING "CENTRE  : ###.#   #########";CTS(1);CTS(6)
12540 PRINT#4,   USING "LEFT    : ###.#   #########";CTS(2);CTS(7)
12550 PRINT#4,   USING "CENTRE  : ###.#   #########";CTS(3);CTS(8)
12560 PRINT#4,   USING "RIGHT   : ###.#   #########";CTS(4);CTS(9)
12570 PRINT#4,   USING "CENTRE  : ###.#   #########";CTS(5);CTS(10)
12580 GOSUB 9840 'turning lamp off
12590 RETURN 'EXIT 
13100 '
13101 ' *** Main Loop ***
13102 '
13120 IF Q13%=1 THEN 13200
13130   REM Narrow HG slit spectrum
13140   SS(9) =96  :SS(10)=588 :SS(11)=1223:SS(12)=1850:SS(13)=2419:SS(14)=2610
13150   SS(15)=2742:SS(16)=2477:SS(17)=1937:SS(18)=1362:SS(19)=686 :SS(20)=373
13160   GOTO 13300
13200   REM Wide HG slit spectrum
13210   SS(9) =923 :SS(10)=1321:SS(11)=1695:SS(12)=2101:SS(13)=2518:SS(14)=2665
13220   SS(15)=2719:SS(16)=2559:SS(17)=2276:SS(18)=1917:SS(19)=1527:SS(20)=1162
13300 SUMSS=0:SUMS=0:FOR I=9 TO 20:SUMSS=SUMSS+SS(I)^2:SUMS=SUMS+SS(I):NEXT:SA=SUMSS
13310 PRINT#4,:PRINT#4,"**** HG Calibration ****"
13320 PRINT CL$:GOSUB 20000
13330 GOSUB 2080:GOSUB 3230
13340 FH%=0
13350 RETURN
20000 '
20001 ' *** HG Wavelength Calibration ***
20002 '
20010 IF M4$=STR$(MAXI) THEN PRINT "Centre": ELSE IF M4$=STR$(LEFTI) THEN PRINT "Left": ELSE PRINT "Right"
20020 GOSUB 6610:
20500 REM
20501 REM Begin
20502 REM
20510 D7 = 4: REM max passes before mic reset
20520 WL$="0":WU$="0":CZ$="4":REM set to hg wavelength
20530 IF HF%=1 THEN GOSUB 2090: PRINT CL$;C$;" terminated":RETURN
20540 M8$="-"+MC$:GOSUB 9805:GOSUB 8000:REM move micrometer back and zero sums
20550 W1=50:W2=240:WS=10:GOSUB 30000:REM scan forward
20560 IF HF%=1 THEN GOSUB 2090:PRINT CL$;C$;" terminated":M8$=MC$:GOSUB 9805:RETURN
20570 W1=240:W2=50:WS=-10:GOSUB 30000:REM scan backward
20580 IF HF%=1 THEN GOSUB 2090:PRINT CL$;C$;" terminated":M8$=MC$:GOSUB 9805:RETURN
20590 REM
20591 REM Compare measured scan to standard
20592 REM
20600 NOISE=1.0E10
20610 FOR J=0 TO 4
20620   SUMMM=0:SUMM=0:SUMSM=0
20630   FOR I=9 TO 20
20640     K=I+2*J-4:IF MS(K) > 0 AND NOISE > MS(K) THEN NOISE = MS(K)
20650     SUMSM=SUMSM+SS(I)*MS(K):SUMMM=SUMMM+MS(K)*MS(K):SUMM=SUMM+MS(K)
20660   NEXT
20670   RNOISE=(SUMS*SUMMM-SUMSM*SUMM)/(SUMS*SUMM-12*SUMSM)
20680   IF RNOISE<NOISE AND RNOISE>10 THEN NOISE=RNOISE
20690   S(J)=(SUMSM-NOISE*SUMS)^2/(SUMSS*(SUMMM-2*NOISE*SUMM+12*NOISE^2))
20700   IF J=2 THEN SNR = SUMM/(12*ABS(NOISE))
20710   LOCATE JC+2+J,JR:PRINT S(J)
20720 NEXT
20730 REM
20731 REM Find best fit
20732 REM
20740 D3=0
20750 FOR I=0 TO 2
20760   D1=S(I)-S(I+1):D2=S(I+1)-S(I+2):IF D1<0 AND D2>0 THEN D3=D1:D4=D2:CA=I+1
20770 NEXT
20780 IF D3=0 OR SNR<4 THEN 20820
20790   CC=(D3-D4)/2:BC=-D3-CC*(2*CA-1):AC=S(CA)-BC*CA-CC*CA*CA
20800   CA=-BC/2/CC:C5=AC+BC*CA+CC*CA*CA
20810   C5=INT(P4%*C5)/P4%:D5=CA*20+VAL(MC$)-40:GOTO 20860
20820   D5=0:D6=0:C5=0
20830   FOR I=5 TO 28
20840     IF MS(I)>D6 THEN D6=MS(I):D5=10*I-OF%
20850   NEXT
20860 M8$=STR$(INT(D5+0.5)):GOSUB 9805:GOSUB 6850
20870 LOCATE 23,5:PRINT USING"est step####.## target \  \";D5;MC$;
20880 PRINT#4,USING"\      \ ( #.#### ) ";TIME$;SQR(C5);
20890 PRINT#4,USING" corr est for step####.## target is step \  \";D5;MC$;
20900 PRINT#4,USING"####### ###.##";MS(15);SNR
20905 PRINT#4,USING" micro is moved by ### steps";-VAL(MC$)+INT(D5+.5)
20910 TG%=TE%:TH%=1:IF R%=1 THEN RETURN
20920 A1$(IO)=LF$+"hg"+CR$+TIME$+CR$+STR$(C5)+CR$+STR$(D5)+CR$+M8$+CR$
20930 A1$(IO)=A1$(IO)+STR$(MS(15))+CR$+STR$(TE%)+CR$
20935 IF D7=4 THEN CTS(IL)=D5:CTS(IL+5)=MS(15):
20940 IF IO>15 THEN GOSUB 3200
20950 IO=IO+1
20960 IF C5>.9 AND SNR>4 AND ABS(VAL(M8$)-VAL(MC$))<1.5 THEN GOSUB 3200:GOSUB 9805:RETURN
20970 IF D7<1 THEN M8$=MC$:GOSUB 9805:GOSUB 6850:GOSUB 31000:GOSUB 3200:GOTO 20510
20980 D7=D7-1: GOTO 20520
30000 '
30001 ' *** Wavelength Scan Routine ***
30002 '
30010 GOSUB 9670:M8$=STR$(W1):GOSUB 9805:GOSUB 9710
30020 JN=0:JR=2:FOR MI=W1+WS TO W2 STEP WS
30030   GOSUB 2090:IF HF%=1 THEN MI=W2:GOTO 30120
30040   LI=(MI-WS)/ABS(WS)
30050   O1$="O:M,10,"+MID$(STR$(MI),2):IF TYP$="mkiii" THEN O1$=O1$+SE$+"M,"+N9$+","+MID$(STR$(MI),2)
30060   O1$=O1$+":R":GOSUB 1990:GOSUB 9450:GOSUB 9940
30070   IF XN<>1 THEN 30100
30080     O1$="O:M,10,"+MID$(STR$(LI*ABS(WS)),2):IF TYP$="mkiii" THEN O1$=O1$+SE$+"M,"+N9$+","+MID$(STR$(LI*ABS(WS)),2)
30090     O1$=O1$+":R":GOSUB 9450:GOTO 30030
30100   JC=10+JN:IF JC=22 THEN JR=JR+15:JN=0:JC=10
30110   MS(LI)=MS(LI)+F(WL):LOCATE JC,JR:PRINT LI;MS(LI):JN=JN+1
30120 NEXT:IF HF%=1 THEN RETURN
30130 GOSUB 9900:MS(W2/ABS(WS))=MS(W2/ABS(WS))+F(WL)
30140 JC=10:JR=JR+15:RETURN
31000 '
31001 ' *** Measure Offset ***
31002 '
31010 IF MZ%<>0 OR Q14%=1 THEN 31100
31020   B$="HG failed - no offset constant":PRINT#4,B$:GOSUB 3050
31030   HF%=1:IF RM%=0 THEN GOSUB 9805:GOSUB 3230:FH%=0
31040   IF G$(JJ+1)<>"lo" THEN GOSUB 9840
31050   RETURN
31100 T0=TIMER/60:GOSUB 8600:IF FH%<1 THEN 31200
31110   HF%=1:B$= "*** Micrometer reset attempted - HG cal still failed":PRINT#4,B$:GOSUB 3050
31120   GOSUB 32000:RETURN
31200 B$="HG cal too far off limits - resetting micrometer(s)"
31210 FH%=FH%+1:PRINT#4,B$:GOSUB 3050
31230 RETURN
32000 '
32001 ' *** Diagnostic Message ***
32002 '
32010 B$="HG failed - standard offset apparently incorrect"
32020 PRINT#4,B$:GOSUB 3050
32030 PRINT#4,"Check micrometer mechanism and reset by hand **":RETURN
65529 REM proper last line
