10000 REM ********** UV focus Recombining routine 30/10/10 ***********
10010 REM            MKIII Alignment of Two Spectrometers
10020 REM        
10040 REM
10050 REM  See also:
10060 REM ************************************************************
10070 REM
10260 REM
10270 REM ************************************************************
10280 REM  History: dd/mm/yy
10290 REM  First version
10350 REM ************************************************************
10500 DATA UVfocusr
11000 '
11001 ' *** Initialization ***
11002 '
11010 IF TYP$<>"mkiii" THEN RETURN
11015 ON ERROR GOTO 3100
11020 IF cts=1 THEN GOTO 11030 'The cts flag prevents redimentioning of the arrays
11025 DIM CTS(200):cts=1
11030 GOSUB 9820: GOSUB 9860: GOSUB 6700 'setting iris, lamp and zenith to correct position
11040 MAXVA=1:
11501 ' Measurement
11510 FOR I = 115 TO 140
11520 	M4$=STR$(I): GOSUB 9790
11530 	O1$="R,5,5,5;O":GOSUB 9450
11540 	CTS(I)=VA:IF VA>MAXVA THEN MAXVA=VA:MAXI=I:
11550 	PRINT USING "####   ########";I;VA:
11560 NEXT
11600 'Calculate right 50% point
11610 RIGHTI=1:RIGHT=1:
11620 FOR I = 115 TO MAXI
11630 	RIGHTTEMP=ABS(CTS(I)/MAXVA-0.5): 
11640 	IF RIGHTTEMP<RIGHT THEN RIGHT=RIGHTTEMP: RIGHTI=I:RIGHTVA=CTS(I)
11650 NEXT
11700 'Calculate left 50% point
11710 LEFTI=1:LEFT=1:
11720 FOR I = MAXI TO 140
11730 	LEFTTEMP=ABS(CTS(I)/MAXVA-0.5): 
11740 	IF LEFTTEMP<LEFT THEN LEFT=LEFTTEMP: LEFTI=I:LEFTVA=CTS(I)
11750 NEXT
11800 'Print results
11810 PRINT#4, "Points for UV focus test"
11820 PRINT#4, "POSITION: STEP   INTENSITY"
11830 PRINT#4,   USING "LEFT    : ####   #########";LEFTI;LEFTVA
11840 PRINT#4,   USING "CENTRE  : ####   #########";MAXI;MAXVA
11850 PRINT#4,   USING "RIGHT   : ####   #########";RIGHTI;RIGHTVA
12000 'Centre
12010 M4$=STR$(MAXI):GOSUB 9790:HP%=0
12020 PRINT#4, "Centre:"
12030 IL=1:GOSUB 12600
12100 'Left
12110 M4$=STR$(LEFTI):GOSUB 9790:HP%=0
12120 PRINT#4, "Left:"
12130 IL=2:GOSUB 12600
12200'Centre
12210 M4$=STR$(MAXI):GOSUB 9790:HP%=0
12220 PRINT#4, "Centre:"
12230 IL=3:GOSUB 12600
12300 'Right
12310 M4$=STR$(RIGHTI):GOSUB 9790:HP%=0
12320 PRINT#4, "Right:"
12330 IL=4:GOSUB 12600
12400'Centre
12410 M4$=STR$(MAXI):GOSUB 9790:HP%=0
12420 PRINT#4, "Centre:"
12430 IL=5:GOSUB 12600
12510 PRINT#4, "UV focus test summary - Recombining spectrometer"
12520 PRINT#4, "POSITION: STEP   INTENSITY"
12530 PRINT#4,   USING "CENTRE  : ###.#   #########";CTS(1);CTS(6)
12540 PRINT#4,   USING "LEFT    : ###.#   #########";CTS(2);CTS(7)
12550 PRINT#4,   USING "CENTRE  : ###.#   #########";CTS(3);CTS(8)
12560 PRINT#4,   USING "RIGHT   : ###.#   #########";CTS(4);CTS(9)
12570 PRINT#4,   USING "CENTRE  : ###.#   #########";CTS(5);CTS(10)
12580 GOSUB 9840 'turning lamp off
12590 RETURN 'EXIT 
12600 '
12601 ' *** Setup for HP Alignment ***
12602 '
12610 ON ERROR GOTO 3100
12620 X(1)=1:Y(1)=1:ERASE X,Y:DIM X(100),Y(100)
13000 '
13001 ' *** Perform HP Alignment ***
13002 '
13020 PRINT#4,:PRINT#4,"**** 2nd Spectrometer Alignment - HP Test ****"
13030 CX$="4":TR$="s"+"a":REM cycle count and track mode
13040 GOSUB 2450:GOSUB 3225
13050 GOSUB 6610:M5$="192":GOSUB 6650
13080 HP%=HP%+1:
13090 IF HP%=5 THEN B$="Spectrometer alignment trouble, aborting":PRINT#4,B$:GOSUB 3050:HF%=1:GOTO 14000
13100 DW=10:SQ=5:DQ$=STR$(SQ+1):GOSUB 9670
13105 IF M4$=STR$(MAXI) THEN PRINT "Centre": ELSE IF M4$=STR$(LEFTI) THEN PRINT "Left": ELSE PRINT "Right"
13110 GOSUB 31000:GOSUB 32000
13120 O1$="M,"+N9$+","+STR$(INT(VAL(MAX$)+.5)):GOSUB 9450
13130 M8$=STR$(OF%):GOSUB 9805:M8$=MC$:GOSUB 9805:GOSUB 9500
13140 IF HF%=1 THEN PRINT "Aborted": PRINT#4, "Aborted":GOTO 14000
13150 IF C1/C9<.97 OR ABS(VAL(MAX$)-80)>10 THEN 13080
14000 '
14001 ' *** Clean Up and Exit ***
14002 '
14010 ERASE A,D,TTT,W,X,Y,ZI#:RETURN
31000 '
31001 ' *** Scan W1 to W2 Step DW on Slit SQ ***
31002 '
31005 M8$="-"+MC$:GOSUB 9805
31006 M8$=STR$(-OF%):GOSUB 9805
31010 M8$="-80":GOSUB 9815:W1=0:W2=160:MAX=0:MAX$=""
31012 N=0:FOR WV=W1 TO W2 STEP DW
31020 SN$=STR$(WV):N=N+1
31030 O1$="M,"+N9$+","+SN$+";R,"+DQ$+","+DQ$+","+CX$+";O":GOSUB 9450
31040 PRINT SN$,VA:IF VA>MAX THEN MAX=VA:MAX$=SN$
31050 X(N)=VAL(SN$):Y(N)=VA
31060 NEXT:RETURN
32000 IF MAX$="0" OR MAX$="160" THEN 32420
32010 P=4:A(1,1)=1:TTT(1)=1:D(1)=1:W(1)=1:ZI#(1,1)=1:ERASE A,TTT,D,W,ZI#
32020 DIM A(P,P),TTT(P),D(P),W(P),ZI#(P,P)
32030 FOR I=1 TO P
32040 FOR I1=1 TO N:TTT(I)=TTT(I)+X(I1)^I:NEXT
32050 TTT(I)=TTT(I)/N
32060 NEXT
32070 Y1=0
32080 FOR I1=1 TO N:Y1=Y1+Y(I1):NEXT:Y1=Y1/N
32090 FOR J=1 TO P:S1=0
32100 FOR I=1 TO N:S1=S1+(Y(I)-Y1)*(X(I)^J-TTT(J)):NEXT
32110 D(J)=S1:S1=0
32120 FOR K=J TO P:S1=0
32130 FOR I=1 TO N:S1=S1+(X(I)^J-TTT(J))*(X(I)^K-TTT(K)):NEXT
32140 A(J,K)=S1:A(K,J)=S1
32150 NEXT
32160 NEXT
32170 NT=N:N=P
32180 FOR R=1 TO N:FOR C=1 TO N:ZI#(R,C)=A(R,C):NEXT C,R
32190 FOR R=1 TO N:X#=ZI#(R,1)
32200 FOR C=1 TO N
32210 IF C<N THEN ZI#(R,C) = ZI#(R,C+1)/X# ELSE ZI#(R,C)=1/X#
32220 NEXT
32230 FOR R2=1 TO N
32240 IF R2=R THEN 32300 ELSE X#=ZI#(R2,1)
32260   FOR C=1 TO N: Y#=ZI#(R,C)*X#
32270   IF C<N THEN ZI#(R2,C)=ZI#(R2,C+1)-Y# ELSE ZI#(R2,C)=-Y#
32280   NEXT
32300 NEXT
32310 NEXT
32320 N=NT
32330 FOR I=1 TO P:TTT=0:FOR J=1 TO P:TTT=TTT+ZI#(I,J)*D(J):NEXT:W(I)=TTT:NEXT
32340 M1!=0:FOR I=1 TO P:M1!=M1!+W(I)*TTT(I):NEXT
32350 W(0)=Y1-M1!
32360 C9=0:FOR I=1 TO N:C9=C9+(Y(I)-Y1)^2:NEXT
32370 C1=0:FOR I=1 TO P:C1=C1+W(I)*D(I):NEXT
32380 MAX=0:FOR I=X(1) TO X(N) STEP .1:YMAX=W(0)
32390 FOR J=1 TO P:YMAX=YMAX+W(J)*I^J:NEXT
32400 IF MAX<YMAX THEN MAX=YMAX:XMAX=I
32410 NEXT:MAX$=STR$(INT(XMAX*100+.5)/100)
32420 PRINT #4,"Maximum intensity found at step: ";MAX$;MAX;"R^2: ";C1/C9
32430 PRINT "Maximum intensity found at step: ";MAX$;MAX;"R^2: ";C1/C9
32435 IF HP%=1 THEN CTS(IL)=VAL(MAX$):CTS(IL+5)=MAX:
32440 A1$(IO)=LF$+"hp"+CR$+TIME$+CR$+STR$(C1/C9)+CR$+STR$(MAX)+CR$+MAX$+CR$+STR$(TE%)
32450 IO=IO+1:GOSUB 3200
32460 RETURN
65529 REM proper last line


