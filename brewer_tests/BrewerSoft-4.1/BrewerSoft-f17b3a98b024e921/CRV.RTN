10000 REM *************** cr routine 23/09/98 *****************
10010 REM     MKIV Cosine Response Lamp Scanning Routine
10015 REM
10020 REM            TEST ROUTINE - NOT FOR RELEASE
10025 REM
10030 REM        (SCI-TEC Instruments, September 1998)
10040 REM  
10050 REM 
10060 REM *****************************************************
10070 REM
10150 REM
10160 REM  Side Effects:
10170 REM
10180 REM  Global Variables Referenced:
10230 REM
10240 REM  Exits:
10250 REM
10260 REM  Uses:
10290 REM *****************************************************
10300 REM  History: dd/mm/yy
10360 REM *****************************************************
10500 DATA CR
10510 IF Q8%=0 THEN RETURN                      'if no uvb port
10520 IF DN%<>8 THEN RETURN                     'if no disk recording
10540 PRINT : INPUT;"Enter FW2 Position ==> ";FW2
10550 PRINT : INPUT;"Enter Lamp Distance ==> ";DI
10560 PRINT : INPUT;"Enter Start Angle ==> ";SANG
10570 PRINT : INPUT;"Enter End   Angle ==> ";EANG
10580 PRINT : INPUT;"Enter Angle Increment ==> ";IANG
14000 '
14001 ' *** Prepare and Write File Header Info ***
14002 '
14030 CX$="1":TR$="s"+"a"
14040 GOSUB 2450		'temperature
14045 GOSUB 4450		'time and location stamp
14047 OPEN DD$+"dum.dta"FOR OUTPUT AS 8
14048 PRINT#8,"Cosine Response Data File"
14050 PRINT#8,"Integration time is 0.2294 seconds per cycle"
14060 PRINT#8,"number of cycles = ";CX$
14070 PRINT#8,"lamp distance (cm) = ";DI
14080 PRINT#8,A1$(0)            'printing time and location stamp
14500 '
14510 ' *** Move The Motors and Filter Wheels *** 
14520 '
14530 GOSUB 6610 : GOSUB 6635                   'filter wheel 1
14560 M5$=STR$(FW2*64): GOSUB 6650		'filter wheel 2
14610 M1$=STR$(ZU%): GOSUB 9870                 'zenith prism to uvb dome
14620 TD=1500:GOSUB 7000			'delay times
14630 GOSUB 7800				'determine solar angles
14640 GOSUB 6700				'close iris
14650 LOCATE ,SP:PRINT "4 - Brewer azimuth to sun"
14660 LOCATE ,SP:PRINT "5 - Director prism to uvb sensor":GOSUB 9650
14670 PRINT#4,"Cosine Response scan started at ";TIME$
14680 '
14685 K = ZERO + VAL(MC$)
14690 FOR LL = 1 TO 5
14700    WL = INT((DC(LL,1)+K*DC(LL,2)+K*K*DC(LL,3))*100+.5)/1000
14710    PRINT#8, USING "   ####.### ";WL;
14715    PRINT USING "   ####.### ";WL;
14720 NEXT LL
14730 PRINT #8, : PRINT
14740 '
14800 FOR ANGLE = SANG TO EANG STEP IANG
14810    PRINT "Set Angular to Position ==> ";ANGLE: : O1$="M,3,0":GOSUB 9450
14820    GOSUB 25000
14830    IF (Q14%=0 AND (VA AND 64)=64) OR (Q14%=1 AND (VA AND 4)=4) THEN GOSUB 31000 ELSE GOTO 14820
14840 NEXT ANGLE
14850 '
14860 PRINT#8,"end":CLOSE 8
14900 '
14901 ' *** Record data on cr file ***
14902 '
14930 B$=JD$+YF$+"."+NO$
14940 PRINT#4,"CR scan finished at ";TIME$
14950 SHELL "copy "+DD$+"CR"+B$+"+"+DD$+"dum.dta "+DD$+"CR"+B$
14960 PRINT#4,"CR";B$;" data file extended at ";TIME$:RETURN
14998 :
14999 :
20500 '
20501 ' *** Read Push Buttons ***
20502 '
20505 IF IS%=42 THEN GOSUB 9550:GOTO 20505
20515 O1$=":G,800":IF Q14%=1 THEN O1$="?TRACKER.SWITCHES"
20540 GOSUB 9400:IF HF%=1 AND A$="" THEN A$=INKEY$
20550 RETURN
31000 '
31001 ' *** Collect The Counts From Each Slit and Record ***
31002 O1$="M,3,"+STR$(IRIS):GOSUB 9450
31010 O1$="R,1,1,40;O":GOSUB 9450:GOSUB 9190:VA = INT(VA*100/40)/100: DARK = VA
31030 CX$ = "40" : VA = 0
31032 PRINT#8, USING " ###.##  ####.### ";ANGLE;DARK;
31035 PRINT USING " ###.##  ####.### ";ANGLE;DARK;
31040 FOR LL = 1 TO 5
31050    DQ$=STR$(LL+1)
31060    GOSUB 2090:IF HF%=0 THEN 31080
31070    PRINT "aborted" : PRINT#8,"aborted";CR$; : JT=NANG : RETURN
31080    O1$="R,"+DQ$+","+DQ$+","+CX$+";O"
31090    TI=TIMER*60:TM=TI:GOSUB 9450:TM=INT((TM+TI)/72)/100
31100    VA = INT(VA*100/VAL(CX$))/100
31120    PRINT#8, USING "  #####.### ";VA;
31130    PRINT USING "  #####.### ";VA;
31140 NEXT LL
31150 PRINT #8, : PRINT: O1$="M,3,0":GOSUB 9450
31180 RETURN
31190 REM
65529 REM proper last line
