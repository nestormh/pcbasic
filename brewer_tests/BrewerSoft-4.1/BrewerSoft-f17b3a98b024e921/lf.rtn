10000 REM ************ lf.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10001 REM                MKII/MKIII/MKIV Location File
10002 REM
10003 REM
10004 REM ***************************************************************
10005 DATA lf
10006 key off
10010 :
10030 ON ERROR GOTO 10300
10100 DIM ZB$(30,3)
10110 CF=0:H(0)=1:H(1)=18:H(2)=26:H(3)=35:L(0)=16:L(1)=7:L(2)=8:L(3)=4
10120 FOR I=1 TO 30:FOR J=0 TO 3:ZB$(I,J)=LEFT$(SP$,L(J)):NEXT:NEXT 
10130 OPEN "loc.fil" FOR INPUT AS 1
10140 INPUT#1,OP$:OP=VAL(OP$):IF OP<1 OR OP>30 THEN 10160
10150 FOR I=1 TO OP:FOR J=0 TO 3:INPUT#1,ZB$:ZB$(I,J)=LEFT$(ZB$+SP$,L(J)):NEXT:NEXT
10160 CLOSE #1:GOTO 11000
10300 '
10301 ' *** File not Found ***
10302 '
10312 IF ERL<>10130 THEN 10320
10315 CLS:LOCATE 10,SP:PRINT "Location file not found -- lf routine terminated"
10316 RESUME 10330
10320 IF ERR=62 THEN RESUME 10325
10325 LOCATE ,PP: PRINT "file not found"
10330 FOR I=1 TO 3000:NEXT
10350 ON ERROR GOTO 3100:ERASE ZB$:RETURN
10500 '
10501 ' *** Wrong Number of Entries ***
10502 '
10510 CLS
10520 LOCATE 10,5
10530 PRINT "Invalid number of entries on file -- probable formatting error"
10540 CLOSE #1
10550 ERASE ZB$: RETURN
11000 '
11001 ' *** Editor ***
11002 '
11030 CLS
11100 VP=24:HP=39:GOSUB 30000:LOCATE 1,PP-3:PRINT "Number of locations: ";OP;
11105 PRINT"Control-END to exit"
11110 LOCATE ,PP:PRINT B1$;LEFT$(LY$,16);B2$;LEFT$(LY$,7);B2$;
11115 PRINT LEFT$(LY$,8);B2$;LEFT$(LY$,4);B3$
11117 LOCATE ,PP
11120 PRINT BR$"    location    ";BR$;"  lat  ";BR$;"  long  ";BR$;"pres";BR$
11130 LOCATE ,PP:PRINT B7$;LEFT$(LY$,16);B8$;LEFT$(LY$,7);B8$;
11135 PRINT LEFT$(LY$,8);B8$;LEFT$(LY$,4);B9$
11140 FOR I=1 TO 17:LOCATE ,PP:PRINT BR$;"                ";BR$;"       ";BR$;
11145 PRINT "        ";BR$;"    ";BR$:NEXT
11150 LOCATE ,PP:PRINT B4$;LEFT$(LY$,16);B5$;LEFT$(LY$,7);B5$;
11155 PRINT LEFT$(LY$,8);B5$;LEFT$(LY$,4);B6$
11160 P=0:V=1:H=0:C=1:IF OP<1 THEN OP=1
11170 GOSUB 31000
11180 ZB$=ZB$(V+P,H):LL$=LEFT$(ZB$,C-1):T$=MID$(ZB$,C,1):RR$=RIGHT$(ZB$,L(H)-C)
11190 OV=V:OH=H:OC=C:VP=3+V:HP=PP+H(H)+C-2:GOSUB 30000:COLOR 0,7:PRINT T$;:COLOR 7,0
11200 A$=INKEY$:IF A$="" THEN 11200
11210 A=ASC(A$):IF A$=QB$ THEN 12000
11220 IF A$="^" THEN A=0:CF=ABS(CF-1):LOCATE 6,62:PRINT CHR$(32-(CF=1)*62)
11230 IF A$=QF$ AND OP>P+17 THEN A=0:P=OP-17:GOSUB 31000
11240 IF A$=Q2$ THEN V=1:H=0:C=1
11250 IF A$=Q3$ OR A$=QE$ THEN V=1:H=0:P=0:C=1:GOSUB 31000
11260 IF A$<>CR$ AND A$<>QC$ THEN 11280
11270 H=H+1:C=1
11275 IF H>3 THEN H=0:A$=Q8$:IF V+P=OP AND OP<30 THEN OP=OP+1:GOSUB 31000
11280 IF A$=QD$ THEN H=H-1:C=1:IF H<0 THEN H=3:A$=Q7$
11290 IF A$=Q8$ AND OP>V+P THEN V=V+1:IF V>17 THEN V=17:P=P+1:GOSUB 31000
11292 IF CF<>1 OR A$<>Q8$ OR OP>=30 THEN 11300
11294 OP=OP+1:FOR J=0 TO 3:ZB$(OP,J)=LEFT$(SP$,L(J)):NEXT
11296 P=P+1:GOSUB 31000
11300 IF A$=Q7$ AND V+P>1 THEN V=V-1:IF V<1 THEN V=1:P=P-1:GOSUB 31000
11310 IF A$<>Q0$ OR OP=30 OR CF=0 THEN 11340
11320 FOR I=OP TO P+V STEP -1:FOR J=0 TO 3:ZB$(I+1,J)=ZB$(I,J):NEXT:NEXT 
11330 C=1:OP=OP+1:FOR J=0 TO 3:ZB$(P+V,J)=LEFT$(SP$,L(J)):NEXT:GOSUB 31000
11340 IF (A$<>Q1$ AND A$<>Q5$) OR OP<1 OR CF=0 THEN 11400
11350 IF P+V>=30 THEN 11360
11355 FOR I=P+V TO OP-1:FOR J=0 TO 3:ZB$(I,J)=ZB$(I+1,J):NEXT:NEXT 
11360 FOR J=0 TO 3:ZB$(OP,J)=LEFT$(SP$,L(J)):NEXT:OP=OP-1:IF OP<1 THEN OP=1
11370 IF OP-P<17 AND P>0 THEN P=P-1
11380 IF V>OP THEN V=V-1
11390 GOSUB 31000
11400 IF A$=Q6$ THEN C=C+1
11410 IF A$=Q4$ THEN C=C-1
11420 IF A$=Q0$ AND RIGHT$(RR$,1)=" " AND CF=0 THEN ZB$(V+P,H)=LL$+" "+T$+RR$
11430 IF (A$=Q1$ OR A$=Q5$) AND C>=1 AND CF=0 THEN ZB$(V+P,H)=LL$+RR$+" ":C=C-1
11440 A=(A AND 127):IF A>31 AND A<123 THEN ZB$(V+P,H)=LL$+A$+RR$:C=C+1
11450 IF C<1 THEN C=1
11460 IF C>L(H) THEN C=L(H)
11470 ZB$(V+P,H)= LEFT$(ZB$(V+P,H)+SP$,L(H))
11480 VP=3+OV:HP=H(OH)+PP-1:GOSUB 30000
11490 PRINT ZB$(OV+P,OH)
11500 GOTO 11180
12000 '
12001 ' *** Save Location File ***
12002 '
12100 OPEN "loc.fil" FOR OUTPUT AS 1
12120 PRINT#1,OP
12130 FOR I=1 TO OP:FOR J=0 TO 3:PRINT#1,ZB$(I,J):NEXT:NEXT
12140 CLOSE #1
12150 KEY OFF: ERASE ZB$: PRINT "": RETURN
30000 '
30001 ' *** Cursor Positioning ***
30002 '
30110 IF VP<0 THEN 30140
30120 IF HP<0 THEN 30140
30130 LOCATE VP+1,HP+1
30140 RETURN
31000 '
31001 ' *** Output Table of Locations ***
31002 '
31100 VP=2:HP=22:GOSUB 30000:K=OP:IF K>P+17 THEN K=P+17
31120 FOR I=P+1 TO K:FOR J=0 TO 3:LOCATE 4+I-P,H(J)+PP:PRINT ZB$(I,J);:NEXT:NEXT
31130 IF K=>17 THEN RETURN 
31140 FOR I=K+1 TO 17:LOCATE 5+K,PP:PRINT BR$;"                ";BR$;"       ";
31145 PRINT BR$;"        ";BR$;"    ";BR$:NEXT
31150 RETURN 
65529 REM proper last line
