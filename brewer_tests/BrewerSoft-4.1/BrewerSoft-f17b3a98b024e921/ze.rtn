10000 REM ************ ze.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
55555 REM                MKII/MKIII/MKIV Zero Zenith
55555 REM
55555 REM             (Sci-Tec Instruments, March 1998)
55555 REM
55555 REM  See Also:  AZ.RTN, RE.RTN
55555 REM *************************************************************
55555 REM
55555 REM  This routine searches for the zeroing sensor on the zenith
55555 REM  motor, and calculates a discrepancy value which is the
55555 REM  difference between the zenith actual position and the
55555 REM  expected position.
55555 REM
55555 REM  Side Effects:
55555 REM
55555 REM  Global Variables Referenced:
55555 REM    B6% ER% Q1% TA% ZE% ZO%
55555 REM    VA  O1$
55555 REM
55555 REM  Exits: 16010
55555 REM
55555 REM  Uses:  2090, 3220, 9190, 9450
55555 REM
55555 REM **************************************************************
55555 REM  History: dd/mm/yy
55555 REM  26/01/11 - Does not erase discrepancy on screen when new ZE test is started
55555 REM  23/03/98 - Supports new Brewer electronics
55555 REM  05/01/94 - Writes ZE information to B file
55555 REM  05/12/94 - Created by TM; original algorithm obtained from
55555 REM  RE.RTN; based on AZ algorithm
55555 REM **************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA ze
12000 UC% = 0
15000 '
15001 ' *** Zeroing Zenith Motor ***
15002 'goto 15800
15010 IF Q1%=0 THEN 16000:REM No zenith motor present
15020 IF G$(JJ-1)<>"ze" THEN PRINT CL$ 
15025 LOCATE 5,SP:PRINT"Zeroing zenith":MS%=0:A$="101":IF ZE%>ER%*2/3 THEN A$="1"
15030 IF Q14%=1 THEN FLAG=1:M1$="1":GOSUB 6550:MS%=ZE%-Y:GOTO 15310
15040 O1$="M,1,-1:M,1,"+A$+":G,800":MS%=MS%-VAL(A$)+1:GOSUB 9450:GOSUB 9190:PRINT #4, "Moving ";A$;" steps UP"
15045 IF (VA AND B6%)<>B6% THEN PRINT #4, "LED6 is closed": ELSE PRINT #4, "LED6 is open"
15050 GOSUB 2090:IF (VA AND B6%)<>B6% OR TA%=1 THEN 15800
15060 :
15070 NLOOP%=0
15080 NLOOP%=NLOOP%+1:O1$="M,1,-50:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+50:PRINT #4, "Moving 50 steps DOWN"
15090 GOSUB 2090:IF NLOOP%>ER%/50 OR TA%=1 THEN 15800
15095 IF (VA AND B6%)<>B6% THEN PRINT #4, "LED6 is closed": ELSE PRINT #4, "LED6 is open"
15100 IF (VA AND B6%)=B6% THEN 15080
15110 O1$="M,1,51:M,1,-1":GOSUB 9450:MS%=MS%-50:PRINT #4, "Moving 50 steps UP"
15120 :
15130 NLOOP%=0
15140 NLOOP%=NLOOP%+1:O1$="M,1,-15:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+15:PRINT #4, "Moving 15 steps DOWN"
15150 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15155 IF (VA AND B6%)<>B6% THEN PRINT #4, "LED6 is closed": ELSE PRINT #4, "LED6 is open"
15160 IF (VA AND B6%)=B6% THEN 15140
15170 O1$="M,1,16:M,1,-1":GOSUB 9450:MS%=MS%-15:PRINT #4, "Moving 15 steps UP"
15180 :
15190 NLOOP%=0
15200 NLOOP%=NLOOP%+1:O1$="M,1,-4:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+4:PRINT #4, "Moving 4 steps DOWN"
15210 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15215 IF (VA AND B6%)<>B6% THEN PRINT #4, "LED6 is closed": ELSE PRINT #4, "LED6 is open"
15220 IF (VA AND B6%)=B6% THEN 15200
15230 O1$="M,1,9:M,1,-1":GOSUB 9450:MS%=MS%-8:PRINT #4, "Moving 8 steps UP"
15240 :
15250 NLOOP%=0
15260 NLOOP%=NLOOP%+1:O1$="M,1,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+1:PRINT #4, "Moving 1 step DOWN"
15270 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15275 IF (VA AND B6%)<>B6% THEN PRINT #4, "LED6 is closed": ELSE PRINT #4, "LED6 is open"
15280 IF (VA AND B6%)=B6% THEN 15260
15290 O1$="M,1,-"+STR$(ZO%):GOSUB 9450:MS%=MS%+ZO%
15300 :
15310 PRINT CL$:LOCATE ,SP:B$="Zenith discrepancy = "+STR$(ZE%-MS%)
15320 PRINT B$:PRINT#4,"Zenith zeroed at ";TIME$;" ";B$
15330 GOSUB 3050
15340 TA%=0:ZE%=0:GOTO 16000
15350 :
15800 REM *** Zenith zeroing (non-critical) failure
15810 LOCATE ,SP:ZE%=ZE%-MS%
15820 IF TA%=0 THEN B$="Zenith ZEROING FAILURE !!":PRINT B$:PRINT#4,B$:GOSUB 3050
15825 IF TA%=0 AND UC%=0 THEN B$="Turning AZ 30 degrees and repeating ZE.":PRINT B$:PRINT#4,B$:GOSUB 3050: UC%=SR%/12:IF AZ%>SR%/2 THEN UC%=-SR%/12: GOSUB 7800:GOSUB 7950:GOTO 15000
15830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Zenith zeroing aborted"
15840 X=1:GOSUB 7220:GOTO 16000
16000 '
16001 ' *** Quit ***
16002 '
16010 GOSUB 3220:UC%=0:RETURN
55555 '
65529 REM proper last line
