55555 REM Please keep lines 1 and 2 updated when changes are made. Volodya Savastiouk
55555 REM Now with cubic dispersion implementation! - Volodya
1 CLS:LOCATE 4,12: VERD$="24/06/2014 01:00:00"
2 VER$="4.10"
55555 ' The following settings in lines 4, 5, 6 and 8 are default values. They can now be changed for each instrument in the icf file
55555 ' set to 0/1
4 AUTOHG=1
55555 ' FW2: set HIFW2 to 0/1/-1 for going to LOWER/OSCILATE/HIGHER neutral density when testing intensity
55555 ' FW2 for HG: set HGFW2 the desired attenuation for HG
5 HIFW2=0:  HGFW2 = 0
55555 ' LATTN! variable sets the lower limit for count rate at 1 cycle that is acceptable before switching to higher FW2 position. STANDARD is 80000 
55555 ' UATTN! variable sets the upper limit for count rate at 1 cycle that is acceptable before switching to lower  FW2 position. STANDARD is 25000
55555 REM unrem the second assignment to go to standard attenuation decisions
6 LATTN!=35000!: UATTN! = 125000!: LATTN!=25000!: UATTN! = 80000!
7 REFLAG% = 0: NEED.HG = 0: NEED.FR = 0: HP.NOTDONE=1: RELH=-99: ATP=-99: RH=-99: AH=-99: EXT=-99: BC%=7: BC0%=7
55555 REM
55555 REM OPT.LOWDS: if 1 then DS will be skipped if less than 500 counts measured in 1 cycle at ND = 0; 
8 OPT.LOWDS = 1: CUB.U=1 
55555 REM
55555 REM
10 LOCATE ,12:PRINT STRING$(47,42)
12 LOCATE ,12:PRINT "*";STRING$(45,32);"*"
13 LOCATE ,12:PRINT"*                  MSC/EC/K&Z/IOS             *"
14 LOCATE ,12:PRINT"*          ALL Brewers Operating Program      *"
15 LOCATE ,12:PRINT"*         Version "+VER$+" - Full Release (IOS)   *"
16 LOCATE ,12:PRINT"*               "+VERD$+"           *"
17 LOCATE ,12:PRINT "*";STRING$(45,32);"*"
19 LOCATE ,12:PRINT"*     Originally written by  Dr. JB Kerr      *"
20 LOCATE ,12:PRINT"* Adapted for the IBM PC by  Dr. CT McElroy   *"
21 LOCATE ,12:PRINT"*       Maintained by Volodya Savastiouk      *"
22 LOCATE ,12:PRINT STRING$(47,42)
23 LOCATE ,12:PRINT"** Copyright(C) 1994 SCI-TEC Instruments Inc.**"
25 LOCATE ,12:PRINT STRING$(47,42)
55555 '
55555 ' Works until 2050!!
55555 '
55555 REM DosBox is running if Q20% = 1
40 Q20%=0:A$=ENVIRON$("COMSPEC"):IF A$="Z:\COMMAND.COM" THEN Q20%=1
41 BREWDIR$=ENVIRON$("BREWDIR"):IF BREWDIR$<>"" THEN 45
42   PRINT:PRINT SPC(12);"No environment variable BREWDIR"
43   PRINT SPC(25);"set BREWDIR=<path> to fix"
44   SYSTEM
45 X=1:GOSUB 7220: IF RIGHT$(BREWDIR$,1)="\" THEN BREWDIR$=LEFT$(BREWDIR$,LEN(BREWDIR$)-1)
46 Q12%=0:A$=ENVIRON$("NOBREW"):IF A$<>"" THEN Q12%=1
47 CLS:GOTO 100
50 I=CSRLIN:J=POS(0):SM%=49
51 LOCATE 10,SM%:PRINT "Ú";STRING$(27,196);"¿"
52 LOCATE ,SM%:PRINT USING"³ DS  O3 ######.# /######.# ³";CDS;MDS
54 LOCATE ,SM%:PRINT USING"³ ZS  O3 ######.# /######.# ³";CZS;MZS
56 LOCATE ,SM%:PRINT USING"³ DS SO2 ######.# /######.# ³";CSO2;MSO2
57 IF CAOD <> 0 AND MAOD <> 9999 THEN LOCATE ,SM%:PRINT USING"³ AOD320  #####.# / #####.# ³";CAOD;MAOD
58 LOCATE ,SM%:PRINT "Ã";STRING$(27,196);"´"
59 LOCATE ,SM%:IF RELH <> -99 AND ATP <> -99 THEN PRINT USING"³ RH / Pr   ###.# /  ####.# ³";RELH;ATP
60 LOCATE ,SM%:PRINT USING"³ DUV     at \      \ ###.# ³";UTIME$;ULAST
61 IF INSTR( STIME$, "Fail" ) OR INSTR( STIME$, "dead" ) OR INSTR( STIME$, "Abort" )  THEN COLOR 12
62 LOCATE ,SM%:PRINT USING"³ SL R6   at \      \ ##### ³";STIME$;SLAST:COLOR 7
63 IF INSTR( HTIME$, "Fail" ) THEN COLOR 12
64 LOCATE ,SM%:PRINT USING"³ Last HG at \      \ ###øC ³";HTIME$;HLAST%:COLOR 7
66 LOCATE ,SM%:PRINT USING"³ Current temperature ###øC ³";TE%
68 LOCATE ,SM%:PRINT "À";STRING$(27,196);"Ù"
69 LOCATE ,SM%:COLOR BC0%: PRINT BW$:COLOR 7
70 LOCATE I,J:RETURN
100 DN%=8:DD$="\bdata\":CT$(0)="E  ":CT$(1)="I  "
110 E$=".rtn":READ PC$
120 ON ERROR GOTO 0
55555 ' init
140 C$="init":GOSUB 6000:CHAIN MERGE C$+E$,150,ALL,DELETE 10000-65529
150 READ PC$:GOSUB 1000
55555 ' program dir
180 NO$="-1":GOSUB 5300: NOR%=1: GOSUB 4520: NOR%=0
185 PZ$=L3$: LA=VAL(L1$):LO=VAL(L2$):PZ%=VAL(L3$):LC$=LO$:GOSUB 6200:C$="pd":GOSUB 2900
186 BT$ = " PROGRAM start :  "+MP$+DB$+YE$+" at "+TIME$:PRINT#4,BT$
190 COMSET$=",n,8,1,rs,ds,cs,cd":COMBR$=":1200":COMBR0$=":300":IF Q16%=1 THEN COMBR$=":9600":COMBR0$=":1200"
220 I=0:GOSUB 5400:GOSUB 9000:IF Q14%=0 THEN SHELL("copy re-mb.rtn re.rtn") ELSE SHELL("copy re-sb.rtn re.rtn")
55555 IF Q14%=1 THEN DELETE 1261-1291
55555 IF Q14%=1 THEN DELETE 2453-2477
55555 IF Q14%=1 THEN DELETE 2110-2140
280 LF$=CHR$(10):CR$=CHR$(13):R$=CR$+LF$:HD$="B":IF Q16%=1 THEN EL$="-> " ELSE EL$=CHR$(0)+"-> "
290 LM$="???":DI=5
310 A$=INKEY$:ZS=-200
330 ON ERROR GOTO 400:OPEN DD$+"TODAY.TMP" FOR INPUT AS 1
340 INPUT#1,JDAY$,MDS,MZS,MSO2,NDS,NZS,UTIME$,ULAST,STIME$,SLAST,HTIME$,HLAST%,CDS,CZS,CSO2,CAOD,MAOD,MSK$
350 CLOSE 1:ON ERROR GOTO 0
360 GOTO 1200
390 CLOSE 1:JDAY$="000":GOSUB 3900: GOTO 350
400 RESUME 390
55555 ' don't track the sun/moon/azimuth for these routines if schedule is not cal
1000 TRP$=TR$: IF INSTR( "hg hp ql xl rs sl dt ca cb dsp dsp1 g1 g5 ga ni fi", C$ ) AND SK$<>"cal" <>0 THEN  TR$=""
1010 GOSUB 10000:TR$=TRP$: RETURN
55555 ' Save the rtn's first line to b-file
1111 '
1150 IF Q20%=0 THEN SHELL "dir *.rtn /l /o:n /b >dir.tmp" ELSE SHELL "dir *.rtn /b >dir.tmp"
1152 ON ERROR GOTO 1185:CLOSE 9:OPEN "dir.tmp" FOR INPUT AS 9:CLOSE 9
1154 CLOSE 9:OPEN "dir.tmp" FOR INPUT AS 9: ON ERROR GOTO 1190
1155 IF EOF(9) THEN 1185
1156 INPUT #9, CC$
1160 CLOSE 1:OPEN CC$ FOR INPUT AS 1:INPUT #1, B$: CLOSE 1:A1$(IO)=LF$+"vers"+CR$+TIME$+CR$+CC$+CR$+B$:IO=IO+1:GOSUB 3225
1175 GOTO 1155
1180 ON ERROR GOTO 0
1185 CLOSE 9: RETURN
1190 RESUME 1175
55555 '
55555 ' brewer up?
1200 M9$="0"
1210 IF MDD$<>"o3" AND MDD$<>"n2" THEN MDD$="o3":GOTO 1300
1220 O1$="F,0,2:V,120,1":IF Q16%=1 THEN O1$="F,0,2:L,16820,141"
1225 GOSUB 9400:TD=120:GOSUB 7000
1230 IF IS%=32 THEN GOSUB 3300:GOTO 1260
1240 TI=TIMER*60:IF TI<TD THEN 1230
1250 IS%=32:GOTO 1300
1260 IF Q14%=1 THEN 1320
55555 ' Check for UART
1261 O1$="D,2955,2956":GOSUB 9450:UARTB=0:IF INSTR(I1$,"85")<>0 AND INSTR(I1$,"65")<>0 THEN UARTB=1:
1262 IF UARTB=1 AND Q16%=0 THEN Q16%=1:GOSUB 5200:PRINT#4, "Q16% now set for IOS board, fixing, restarting..":CLOSE 4:RUN
1270 C$="da_lo":GOSUB 6000:CHAIN MERGE C$+E$,1280,ALL,DELETE 10000-65529
1280 READ PC$:GOSUB 1000
1290 R%=2:GOSUB 2450:R%=0
1291 IF A$="p" THEN C$="pb":GOTO 2700
1292 GOTO 1350
55555 ' reset
1300 :
1300 NEED.HG = 1
1310 C$="re":B$="Running "+C$+" from main.asc": GOSUB 3050:GOSUB 6000:CHAIN MERGE C$+E$,1315,ALL,DELETE 10000-65529
1315 READ PC$:GOSUB 1000
1320 C$="da_lo":GOSUB 6000:CHAIN MERGE C$+E$,1325,ALL,DELETE 10000-65529
1325 READ PC$:GOSUB 1000:GOSUB 2300
1340 GOSUB 9880:GOSUB 3300
1350 :
1350 O1$="B,0":GOSUB 9450:IF Q14%=0 THEN O1$="L,19413,0,19414,255":GOSUB 9450
55555 ' schedule?
1410 IO=0
1420 IF SK$="" THEN 1470
1430 C$="ab_sk":B$="Running "+C$+" from main.asc": GOSUB 3050:CHAIN MERGE C$+E$,1440,ALL,DELETE 10000-65529
1440 READ PC$:GOSUB 1000:IF SK$="" THEN 1470
1450 C$=DI$:IF DI$="menu" THEN 2600
1460 GOTO 2870
1470 DI$="menu":GOTO 2600
1500 :
55555 ' menus
1500 :
1500 CHDIR BREWDIR$
1504 PRINT CL$:F$="command":LOCATE 6,SP: GOTO 1720
1701 A$=Q2$:GOTO 2093
1720 :
1720 PRINT:LOCATE ,SP:PRINT "The Brewer is not taking any measurements now. Enter a desired command."
1790 RETURN
1800 :
2000 B$="":GOSUB 2080
2005 IF RM%<>0 THEN RETURN
2010 GOSUB 2035:IF A$=CR$ THEN 2070
2015 IF A$<>BS$ THEN 2020
2016 IF B$<>"" THEN B$=LEFT$(B$,LEN(B$)-1):GOTO 2010
2017 GOTO 2010
2020 B$=B$+A$:GOTO 2010
2030 GOSUB 2080
2035 A$=INKEY$:IF A$="" THEN GOSUB 9891:GOTO 2035
2036 IF A$=Q3$ THEN RUN
2037 IF A$=Q4$ OR A$=Q1$ OR A$=Q5$ THEN A$=BS$:GOTO 2045
2040 IF LEN(A$)>1 THEN 2035
2045 PRINT A$;:VA=VAL(A$):RETURN
2050 GOSUB 2030
2060 B$=A$:GOSUB 2035:B$=B$+A$
2070 VB=VAL(B$):RETURN
2080 IF INKEY$<>"" THEN 2080
2085 RETURN
2090 A$=INKEY$:IF A$="" THEN RETURN
55555 '
2093 IF A$=Q5$ THEN HF%=2: B$ = "BACKSPACE key pressed": GOSUB 3050:
2094 IF A$=Q1$ THEN TA%=1: B$ = "DEL key pressed": GOSUB 3050
2095 IF A$=Q2$ THEN HF%=1: B$ = "HOME key pressed": GOSUB 3050
2096 IF A$=Q3$ THEN RUN
2097 RETURN
55555 'Get MB temperatures/humidity voltages and convert them to degrees
2100 :
2100 IF Q14%=1 THEN RETURN
2110 O1$="L,20248,"+T$+",20249,255:Z":GOSUB 9450:IF LEN(I$)<=6 THEN RETURN
2120 TE.TMP$=STR$(INT(VAL(LEFT$(I$,5))*500/255)/100)
2130 TE.TMP%=-33.27+VAL(TE.TMP$)*18.64:IF Q10%=0 THEN TE.TMP%=-30+VAL(TE.TMP$)*16+.5
55555 ' next line is for a combination of OLD clock board T circuit and NEW SPS T circuit (have to manually set Q10% to 2 in op_st
2135 IF Q10%=2 THEN TE.TMP%=-30+VAL(TE.TMP$)*16*2.2+.5
2140 RETURN
55555 'Get Temperature and convert to voltage (for SB)
2150 '
2150 GOSUB 9450: TE.TMP% = VA
2155 TE.TMP$=STR$((TE.TMP%+33.27)/18.64): IF Q10%=0 THEN TE.TMP$=STR$((TE.TMP%+30)/16)
2160 RETURN
55555 ' change tracker
2300 :
2300 FLAG=0:IF Q14%=0 OR CP%=0 THEN RETURN
2310 O1$="?MOTOR.CLASS[2]":GOSUB 9450
2315 O1$="!MOTOR.CLASS[2] ":IF INSTR(I$,"NOMOTOR")>0 THEN 2330
2320 IF Q2%=0 THEN O1$=O1$+"NOMOTOR":GOSUB 9450
2325 RETURN
2330 IF Q2%=1 THEN LOCATE ,SP:PRINT "Enabling tracker":O1$=O1$+"TRACKERMOTOR":GOSUB 9450
2335 IF Q2%=1 THEN GOSUB 2350:FLAG=1
2340 RETURN
55555 ' reset tracker
2350 :
2350 M1$="2":GOSUB 6500:IF Y>=13500 AND Y<16000 THEN O1$="M,2,13400":GOSUB 9450:AZ%=13400
2360 O1$="I,2":GOSUB 9450
2365 RETURN
55555 REM convert Relative Humidity to Absolute Humidity
2370 RELH=RH:AH=RH*13.239*EXP(17.27*TE.T2%/(237.7+TE.T2%))/(273.15+TE.T2%)
2375 RETURN
55555 REM ---------
55555 ' DOS shell
2400 :
2400 CLS:PRINT"Entering DOS, type EXIT to return...":SHELL:RETURN
55555 ' moisture
2440 :
2440 IF Q15%=0 OR Q12%=1 THEN RETURN
2442 IF RS=0 THEN O1$="?RH.SLOPE":GOSUB 9450:RS=VA:IF RS=0 THEN RS=.029
2443 IF RO=0 THEN O1$="?RH.ORIGIN":GOSUB 9450:RO=VA:IF RO=0 THEN RO=.868
2444 O1$="?ANALOG.NOW[20]":GOSUB 9450:VA=(VA AND 1023)*5/1024:RH=(VA-RO)/RS
2445 O1$="?TEMP[FAN]":GOSUB 9450:RH=RH/(1.0546-.00216*VA):TE.T2%=VA
2446 GOSUB 2370
2447 SH=0:IF AH>2 AND VA<>0 THEN SH=LOG(AH/2)*10000/66/VA
2449 RETURN
55555 ' temp
2450 IF Q12%=1 THEN RETURN
2451 IF Q7%=0 THEN 2460
2452 IF Q14%=1 THEN R%=3:GOTO 2480
2453 T$="0":GOSUB 2100:IF LEN(I$)<=6 THEN 2460
2454 TE$=TE.TMP$: TE%=TE.TMP%: T$="1":GOSUB 2100:TE.T2$=TE.TMP$: TE.T2%=TE.TMP%: T$="2":GOSUB 2100:TE.T3$=TE.TMP$: TE.T3%=TE.TMP%:R%=3
2455 IF Q15%=1 THEN T$="9":GOSUB 2100:RH=(VAL(TE.TMP$)/5-0.16)/0.0062: GOSUB 2370
2456 IF Q17%=1 THEN T$="9":GOSUB 2100:RH=VAL(TE.TMP$)*20: GOSUB 2370
2457 IF Q17%=1 THEN T$="10":GOSUB 2100:AtP=VAL(TE.TMP$)/0.0196*3.13725+400:T$="11":GOSUB 2100:ExT=VAL(TE.TMP$)/0.0196*0.39215-50
2459 GOTO 2480
2460 IF Q12% = 1 THEN B$="1": VB=1: GOTO 2477
2465 PRINT CL$;TF$:PRINT RC$;"Brewer temp (0 - 5 Volts) ";
2470 GOSUB 2000:CLS:IF LEN(B$)=0 THEN 2496
2475 IF (VB<0) OR (VB>5) THEN 2460
2477 TE$=B$
2480 IF Q14%=0 THEN 2483
2481 O1$="?TEMP[PMT]":GOSUB 2150:TE%=TE.TMP%:TE$=TE.TMP$:O1$="?TEMP[FAN]":GOSUB 2150:TE.T2$=TE.TMP$:TE.T2%=TE.TMP%:O1$="?TEMP[BASE]":GOSUB 2150:TE.T3$=TE.TMP$:TE.T3%=TE.TMP%:O1$="?TEMP[EXTERNAL]":GOSUB 2150:ExT$=TE.TMP$:ExT=TE.TMP%
55555 ' Get humidity from SB electronics
2482 GOSUB 2440:
2483 IF AH*RH <> 99*99 THEN A$=" moisture = "+MID$(STR$(AH+100.001),3,5)+" g/m3 ("+MID$(STR$(RH+1000.01),4,4)+"%)"
2484 IF Q14%=0 THEN TE%=-33.27+VAL(TE$)*18.64:IF Q10%=0 THEN TE%=-30+VAL(TE$)*16+.5
2485 TADD$ = "": IF TE.T2%*TE.T3% <> 0 THEN TADD$ = " (T2 = "+STR$(TE.T2%)+"C  T3= "+STR$(TE.T3%)+"C)"
2486 TF$=TIME$+" "+MP$+DB$+YE$+" Brewer temp ="+STR$(TE%)+"C ("+TE$+"V)"+TADD$:IF (Q14%=1 AND Q15%=1) OR RH*AH<>99*99 THEN TF$=TF$+A$
2488 TG=(TE%-TG%)*PC+.5:IF TH%=0 THEN TG=.5
2490 IF C$<>"um" AND C$<>"up" THEN IF R%<>2 THEN PRINT#4,TF$:IF R%=0 THEN 2460
2492 IF C$="um" OR C$="up" THEN IF TE%<>TF% THEN PRINT#4,TF$
2494 TF%=TE%:R%=0:A1$(IO)=R$+"hk"+CR$+TIME$+CR$+STR$(TE%)+CR$+STR$(TE.T2%)+CR$+STR$(TE.T3%)+CR$+STR$(int(RH*10+0.5)/10)+CR$+STR$(int(AH*100+0.5)/100)+CR$+STR$(int(AtP*10+0.5)/10)+CR$+STR$(int(ExT*10+0.5)/10):IO=IO+1:GOSUB 3225
2495 GOSUB 3160: RETURN
2496 GOSUB 3260:GOSUB 5200:RETURN
55555 ' set CL$
2500 :
2502 ON ERROR GOTO 3296
2505 PRINT CL$:LOCATE 10,SP
2506 PRINT:LOCATE ,SP+20
2530 BF$=""
2547 MEM=FRE(0):MEM=FRE("")
2550 CL$=CHR$(12)+"  "+MP$+DB$+YE$+" day= "+JD$+"  "+MDD$+"   #"+NO$+" * "+LO$+LF$
2555 IF LEN(BF$)<19 THEN BF$=STRING$(19-LEN(BF$)," ")+BF$
2560 CL1$=SP$:IF DN%=8 THEN CL1$=CL1$+BF$ ELSE CL1$=CL1$+" DISK TURNED OFF !!"
2563 IF LEN(CL1$)<49 THEN CL1$=CL1$+STRING$(49-LEN(CL1$)," ") ELSE CL1$=RIGHT$(CL1$,49)
2565 CL$=CL$+CL1$+" in:   out:"+CR$
2570 MEM=FRE(0):MEM=FRE(""):CL$=CL$+"  "+LX$+LY$+LX$+LY$+CR$
2590 ON ERROR GOTO 3100
2595 RETURN
55555 ' set up menu
2600 :
2602 IF NEED.HG = 1 AND AUTOHG=1 AND SK$<>"" THEN G$(1)="hg": QC=1: QR=1: GOSUB 3400
2605 HF%=0:IF TR$<>"" THEN PRINT CL$:LOCATE 10,SP:PRINT "*** Restoring motor positions ***":GOSUB 5100:GOSUB 7750
2610 C$=DI$:IF TR$="" THEN T0=TIMER/60
2611 GOSUB 1500
2613 IF TR$="fm" OR TR$="ma" THEN D$="Tracking Moon" ELSE D$="Tracking Sun"
2615 ED%=0:HF%=0:TA%=0:LO%=0:RM%=0:GOSUB 2080
2620 GOSUB 9891:F$="\  \##.###  \              \\    \###.##"
2630 FLAG=1:IF TR$="fm" OR TR$="ma" THEN FLAG=2
2640 GOSUB 7800:LOCATE 4,SP:PRINT USING F$;"mu=";M2;D$;"* za=";ZA
2645 LOCATE ,SP:PRINT "cm->   ";:GOSUB 50: UVCAL%=0
2650 A1$(20)="l":GOSUB 5800:IF A1$(20)="" THEN 2600
2652 Z%=Z%+1:IF A$="" AND Z%>100 THEN Z%=0:O1$="":GOSUB 9400
2655 IF A$="" THEN GOSUB 2090
2660 IF A$=CR$ THEN 2600
2665 IF A$=Q3$ THEN RUN
2670 IF A$="" THEN 2620
2675 IF (ASC(A$)<65) OR (ASC(A$)>122) THEN 2620
2680 PRINT A$;:B$=A$:GOSUB 2010:PRINT
2682 C$="":FOR I=1 TO LEN(B$):A$=MID$(B$,I,1):J=ASC(A$)
2683 IF J>64 AND J<91 THEN C$=C$+CHR$(J+32) ELSE C$=C$+A$
2685 NEXT:IF C$="sk" OR C$="skc" THEN ED%=1
2690 IF TR$="ds" THEN TR$="sa"
2692 IF TR$="fm" THEN TR$="ma"
2693 GOTO 2700
2695 AUTOHG = 0:B$=" OFF": GOTO 2697
2696 AUTOHG = 1:B$=" ON"
2697 NEED.HG=0:NEED.FR=0:HP.NOTDONE=0:B$="Auto FR/HP/HG is now"+B$: GOSUB 3050:PRINT #4, B$:RETURN
55555 ' built-in rtns
2700 :
2701 GOSUB 2900:IF FLAG=1 THEN 2600
2702 IF C$="re" THEN COM(CP%) OFF:IS%=32:GOTO 2875
2703 IF TYP$="mkiv" AND C$="sw" THEN 2875
2715 IF C$="ul" OR C$="qs" OR C$="xl" OR C$="ql" THEN GOSUB 3700
2720 IF RIGHT$(C$,2) <>"pb" AND C$<>"tt" THEN GOSUB 3300
2730 IF C$="co" THEN GOSUB 3000:GOTO 2600
2745 IF C$="ti" THEN GOSUB 7300:GOTO 2600
2750 IF C$="cy" THEN GOSUB 3600:GOTO 2600
2760 IF C$="da" THEN GOSUB 6100:GOTO 2600
2765 IF C$="ex" THEN CLS:SYSTEM
2767 IF C$="shell" THEN GOSUB 2400:GOTO 2600
2790 IF C$="tr" THEN TR$="ds":GOTO 2600
2840 GOSUB 1500:IF C$="menu" THEN 2615
2860 ON ERROR GOTO 2890
2865 RESTORE:READ PC$:IF C$=PC$ THEN 2880
2870 GOSUB 9500
2872 IF LEN(C$)=0 THEN 2600
2873 A$=C$:IF LEN(C$)>8 THEN C$=LEFT$(C$,8)
2875 GOSUB 6000:B$=A$:ON ERROR GOTO 2890:CLOSE 1:OPEN C$+E$ FOR INPUT AS 1:CLOSE 1
2876 CHAIN MERGE C$+E$, 2880, ALL, DELETE 10000-65529
2880 ON ERROR GOTO 3100:GOSUB 1000:HD$="B"
2883 GOSUB 2500
55555 ' may return with new cmd
2885 IF FG%<>1 THEN 2600
2887 FG%=0:ON ERROR GOTO 2890:GOTO 2873
2890 IF ERR<>53 THEN ON ERROR GOTO 0
55555 ' cmd seq from menu
2892 IF LEN(C$)>2 THEN RESUME 3500
2895 CLS:LOCATE 10,10:B$="Command file "+C$+" not found --- continuing": PRINT B$: PRINT#4,B$: GOSUB 3050
2896 X=1:GOSUB 7220:RESUME 2897
2897 ON ERROR GOTO 3100:GOTO 2600
55555 ' test selection
2900 :
2901 GOSUB 4700: SKN$=SK$: IF SKN$="" THEN SKN$="menu"
2902 IF C$="hg" AND TYP$="mkiii" AND HP.NOTDONE=1 AND AUTOHG = 1  AND SK$<>"" THEN JJ=JJ-1:C$="hp":IF JJ<1 THEN JJ=1
2905 FLAG=0
2910 IF NEED.FR > 2 AND SK$<>"" THEN C$="re"
2911 IF INSTR( "skc ls pr pd pf ap ds dt fm fv fz gi hg hp ql rs sc sl um uv zb zc zp zs zw", C$ )=0 THEN B$="Running "+C$+" from "+SKN$+" line "+STR$(ZS): GOSUB 3050
2912 IF C$="b0" THEN GOSUB 9896:FLAG=1
2913 IF C$="b1" THEN GOSUB 9892:FLAG=1
2914 IF C$="b2" THEN GOSUB 9894:FLAG=1
2917 IF C$="pd" THEN CLOSE 4:FP%=2:OPEN DD$+"D"+JD$+YF$+"."+NO$ FOR APPEND AS 4: FLAG=1
2918 IF C$="pf" THEN CLOSE 4:FP%=0:OPEN "scrn:" FOR OUTPUT AS 4:FLAG=1
2919 IF C$="pn" THEN CLOSE 4:FP%=1:OPEN "lpt1:" FOR OUTPUT AS 4:FLAG=1
55555 ' MKIV only
2923 IF C$="te" THEN GOSUB 2450:FLAG=1
2925 IF C$="pz" THEN GOSUB 3650:FLAG=1
2926 IF C$="af" THEN GOSUB 2695:FLAG=1
2927 IF C$="an" THEN GOSUB 2696:FLAG=1
2928 IF C$="rtn_date" THEN GOSUB 1111:FLAG=1
2934 IF C$="ua" OR C$="uf" OR C$="uv" OR C$="ux" THEN MDD2$=C$:C$="uv"
2935 IF C$="zw" THEN MDD2$=C$:C$="zs"
2940 IF C$="sw" THEN MDD2$=""
2945 IF C$="o3" OR C$="n2" THEN MDD2$=C$:C$="sw"
2950 IF C$="lo" OR C$="du" THEN FLAG=1
2990 RETURN
55555 ' comment
3000 :
3005 PRINT CL$;"Enter comment (max 75 chars)"
3010 GOSUB 2000:PRINT CL$:B$=LEFT$(B$,75):PRINT#4,B$
3015 C$="user":GOSUB 3050
3020 RETURN
55555 ' comment in buffer
3050 :
3055 A1$(IO)=LF$+"co"+CR$+TIME$+CR$+C$+": "+B$:IO=IO+1: BW$=LEFT$(B$,29): BC0%=BC%: BC%=7
3060 GOSUB 3225
3065 RETURN
55555 ' printer err
3100 :
3110 IF ERR>23 AND ERR<28 THEN 3130
3120 ON ERROR GOTO 0
3130 FP%=0:CLOSE 4
3140 OPEN "scrn:" FOR OUTPUT AS 4
3150 RESUME
55555 'get a sense of the humidity trend in the last hour
3160 IF RH<0 THEN RETURN
3161 UPDOWN$="-":IF INT(ARH)>INT(RH) OR ARH=0 THEN UPDOWN$=CHR$(25)
3162 IF INT(ARH)<INT(RH) THEN UPDOWN$=CHR$(24)
3170 IF ABS(TIMER-TRH) > 3600 THEN ARH=0: TRH=TIMER: NRH=0
3175 ARH=ARH*NRH+RH: NRH=NRH+1: ARH=ARH/NRH
3190 RETURN
55555 ' output buffer
3200 :
3210 IF IO<10 THEN RETURN
3215 IF IO<5 THEN RETURN
3220 IF IO<2 THEN RETURN
3225 IF IO=0 THEN RETURN
3227 IF BT$<>"" THEN A1$(IO)=A1$(IO-1):A1$(IO-1)=LF$+"co"+CR$+TIME$+CR$+"start"+": "+BT$:IO=IO+1:BT$=""
3230 IF DN%=0 THEN IO=0:RETURN
3231 WR=CSRLIN:WC=POS(0):LOCATE 25,40:PRINT "*** Recording data ***";
3235 LOCATE WR,WC:A$=DD$+HD$+JD$+YF$+"."+NO$
3240 I=0:ON ERROR GOTO 3285
3241 CLOSE 1:OPEN A$ FOR INPUT AS 1
3245 CLOSE 1:OPEN A$ FOR APPEND AS 1
3247 ON ERROR GOTO 3100
3250 IF I=1 AND FP% THEN PRINT#4,A$;"  data file opened at ";TIME$
3254 FOR I=0 TO IO-1:PRINT#1,A1$(I);CR$;:NEXT:CLOSE 1
3260 FOR I=0 TO IO:A1$(I)="":NEXT:MEM=FRE(""):IO=0
3262 IF IO<20 THEN A1$(20)=""
3270 GOSUB 2550
3280 LOCATE 25,40:PRINT SPACE$(30);:ON ERROR GOTO 3100:RETURN
3285 IF ERR<>53 AND ERR<>76 THEN 3291
3290 CLOSE 1:OPEN A$ FOR OUTPUT AS 1:GOSUB 4500:I=1:RESUME NEXT
3291 IF ERR=61 THEN BEEP:LOCATE ,10:B$ "DISK FULL -- Check!!!":PRINT B$:BW$=LEFT$(B$,29):GOTO 3297
3292 IF ERR<70 THEN ON ERROR GOTO 0
3293 IF ERR>70 THEN 3296
3295 BEEP:LOCATE ,10:B$ = "WRITE PROTECTED DISK -- Check!!!":PRINT B$:BW$=LEFT$(B$,29)
3296 DN%=8:IO=0:RESUME NEXT
3297 DN%=8:IO=0:RESUME 3280
55555 ' set clock from Brewer
3300 IF Q6%=1 THEN B$="Turning OFF internal Brewer clock - default now": GOSUB 3050: Q6%=0
55555 ' NOBREW mode
3302 IF Q12%=1 OR Q6%=0 THEN RETURN
55555 ' exec cmd seq
3400 :
3405 ZABEFORE=ZA:GOSUB 5800
55555 ' this can be called as a subtne and not return
3410 :
3420 RM%=1:IJ=0
3430 IJ=IJ+1:JJ=0
3431 JJ=JJ+1:GOSUB 3300:GG$=G$(JJ):IF NEED.AZ=1 AND NO$="085" THEN GG$="az":B$="Will do AZ for #085":GOSUB 3050:PRINT #4,B$:NEED.AZ=0:JJ=JJ-1:IF JJ<1 THEN JJ=1
3432 C$=GG$:A$=C$:IF LEN(C$)>8 THEN C$=LEFT$(C$,8)
3433 GOSUB 9500:GOSUB 4700:RESTORE:READ PC$:IF C$=PC$ THEN RESTORE:GOTO 3454
3434 GOSUB 2900:IF FLAG=1 THEN 3455
3437 IF (C$="ul" OR C$="qs" OR C$="xl" OR C$="ql") AND (SK$<>"") THEN 3460
3449 GOSUB 6000:ON ERROR GOTO 3493
3450 CLOSE 1:OPEN C$+E$ FOR INPUT AS 1:CLOSE 1
3451 IF TR$="fm" OR TR$="ma" THEN TR$="ma" ELSE TR$="sa"
3453 CHAIN MERGE C$+E$, 3454, ALL, DELETE 10000-65529
3454 READ PC$:ON ERROR GOTO 3100:GOSUB 1000:HD$="B"
3455 GOSUB 5800
3457 IF SGN(ZABEFORE)<>SGN(ZA) THEN ZABEFORE=ZA :GOSUB 2500
3460 IF SK$<>"" THEN IF (ZC>ZF) OR (JJ=QC AND IJ=QR) THEN JJ=-1:IJ=1:GOTO 3431
55555 ' abort
3465 IF HF%=1 THEN B$="Going to menu": GOSUB 3050: GOTO 3490
55555 'Pressing BACKSPACE will interrupt current command and will continue with the next
3468 IF HF%=2 THEN B$="Interrupting "+G$(JJ)+" and running next cmd.": GOSUB 3050: HF%=0
3470 IF JJ<QC THEN 3431
3480 IF IJ<QR THEN 3430
3485 IF JJ=QC AND ED%=2 AND G$(1)="sum" THEN PRINT#4,CHR$(12)
3490 HF%=0:GOSUB 9840:SK$="":DI$="menu":ZS=-200:ZF=0:G$(0)="":RM%=0:GOSUB 2080
3492 GOSUB 2500:GOSUB 5200:GOTO 2600
3493 IF ERR<>53 THEN ON ERROR GOTO 0
3494 RESUME 3495
3495 BEEP: ON ERROR GOTO 3100
3496 B$="Illegal command ("+C$+") in schedule "+SK$+", ignoring":GOSUB 3050:GOTO 3455
3497 CLS:LOCATE 10,10:PRINT#4,B$:GOTO 3490
55555 ' setup seq
3500 :
3510 ON ERROR GOTO 3100
55555 'Default to cal schedule for cn command if none were used
3510 IF RIGHT$(B$,2)="cn" AND MSK$="" THEN MSK$="cal":BP$=B$:B$="Defaulting to CAL.SKD for cn":GOSUB 3050:B$=BP$
3520 LE%=INT(LEN(B$)/2):QC=0:QR=1:FG=0
55555 ' next cmd
3530 FOR I=1 TO LE%:G$(I)=LEFT$(B$,2)
3540 B$=RIGHT$(B$,LEN(B$)-2)
3550 IF (QC=0) AND (VAL(B$)<>0) THEN QR=VAL(B$):QC=I
3555 IF G$(I)="ul" OR G$(I)="qs" OR G$(I)="xl" OR G$(I)="ql" THEN FG=1
3560 NEXT:IF QC=0 THEN QC=LE%
3562 IF FG<>0 THEN GOSUB 3700
3565 PRINT CL$:LOCATE 5,SP:PRINT "There are ";QC;" commands :";CR$
3570 FOR I=1 TO QC:LOCATE ,SP:PRINT "Command # ";I;" is ";G$(I);:IF G$(I)="cn" THEN PRINT " (";MSK$;")"
3571 IF G$(I)="dp" AND I<>QC THEN QC=I:QR=1:COLOR 12:PRINT " - this will be last command":COLOR 7
3572 PRINT :NEXT I
3575 PRINT:LOCATE ,SP:PRINT "Repeat command sequence ";QR;" times"
3580 LOCATE ,SP:PRINT "Press HOME if not correct":TB=300:GOSUB 7080
3585 GOSUB 2090:IF HF%=1 THEN 2600
3590 TI=TIMER*60:IF TI<TB AND A$<>CR$ THEN 3585
3595 GOTO 3400
55555 ' update cycles
3600 :
3610 PRINT CL$;"Enter cycle update (";CY$;" now)"
3620 GOSUB 2000:IF VAL(B$)<>0 THEN CY$=B$
3630 RETURN
55555 ' point zenith
3650 :
3660 M1$=STR$(ER%/2+HC%):GOSUB 9870:B$="Pointing to zenith.":GOSUB 3050:PRINT#4,B$
3670 RETURN
55555 ' lamp info for uv
3700 :
3701 IF MDD$="o3" THEN 3710
3702 B$=C$+" is not an NO2 routine":PRINT#4,B$:PRINT B$:GOSUB 3050
3703 X=2:GOSUB 7220:RETURN
3710 PRINT CL$;"Enter UV lamp to use ("+LM$+" now):"
3720 GOSUB 2000:IF B$<>"" THEN LM$=B$
3730 PRINT CL$;"Enter lamp to instrument distance (";DI;" cm now):"
3740 GOSUB 2000:IF VAL(B$)>0 THEN DI=VAL(B$)
3750 QSF%=0:RETURN
55555 ' current data
3900 IF JDAY$="" THEN CLOSE 1: RETURN
3901 CLOSE 1: OPEN DD$+"TODAY.TMP" FOR OUTPUT AS 1
3910 IF JD$<>JDAY$ AND JDAY$<>"" THEN GOSUB 6050
3920 PRINT#1,JD$;CR$;MDS;CR$;MZS;CR$;MSO2;CR$;NDS;CR$;NZS;CR$;UTIME$;CR$;ULAST;
3930 PRINT#1,CR$;STIME$;CR$;SLAST;CR$;HTIME$;CR$;HLAST%;CR$;CDS;CR$;CZS;CR$;CSO2;CAOD;MAOD;MSK$
3940 JDAY$=JD$:CLOSE 1:RETURN
55555 ' print results
4200 :
4205 GOSUB 8100:T0=S(1):GOSUB 8600:T0=TIMER/60:IF S(0)<2 THEN RETURN
4210 PRINT#4,"************* data summary *************"
4215 PRINT#4,H$;"  ";MP$;DB$;YE$;" ";S(2);S(3),TE%;"c deg",C$;AF%
4220 IF C$<>"sl" AND C$<>"su" THEN 4250
4225   FOR I=10 TO 11:PRINT#4,USING P8$;S(I);:NEXT:IF MDD$="o3" THEN SLAST=S(9):STIME$=TIME$:GOSUB 3900
4230   FOR I=4 TO 9:PRINT#4,USING  P8$;S(I);:NEXT:PRINT#4,
4235   FOR I=25 TO 26:PRINT#4,USING P8$;S(I);:NEXT
4240   FOR I=19 TO 24:PRINT#4,USING P8$;S(I);:NEXT:PRINT#4,
4245   GOTO 4295
4250 FOR I=4 TO 9:PRINT#4,USING P8$;S(I);:NEXT
4255 ST$="#.##": IF MDD$="o3" THEN ST$="#.#": GOTO 4270
4260   PRINT#4,USING "    F =####.#  NO2=##.##";S(10);S(11)
4265   GOTO 4285
4270   PRINT#4,USING "       SO2=####.#   O3=####.#";S(10);S(11)
4275   IF C$="ds" AND S(26)<=2.5 THEN CDS=S(11):MDS=MDS*NDS+CDS:CSO2=S(10):MSO2=MSO2*NDS+CSO2:NDS=NDS+1:MDS=MDS/NDS:MSO2=MSO2/NDS:GOSUB 3900
4280   IF C$="zs" AND S(26)<=2.5 THEN CZS=S(11):MZS=MZS*NZS+CZS:NZS=NZS+1:MZS=MZS/NZS:GOSUB 3900
4285 FOR I=19 TO 24:PRINT#4,USING P8$;S(I);:NEXT
4290 PRINT#4,USING P8$+ST$;S(25);S(26)
4295 GOSUB 4300:RETURN
4299 :
55555 ' sum in output buffer
4300 :
4310 A$="":IF MDD$="n2" THEN A$=MDD$
4315 A1$(IO)=R$+A$+"summary"+CR$+H$+CR$+MP$+CR$+DB$+CR$+YE$+CR$+STR$(S(2))+CR$
4320 A1$(IO)=A1$(IO)+STR$(S(3))+CR$+STR$(TE%)+CR$
4325 A1$(IO)=A1$(IO)+A$+C$+CR$+STR$(AF%)+CR$
4330 FOR I=4 TO 9:A1$(IO)=A1$(IO)+STR$(INT(S(I)+.5))+CR$:NEXT
4340 A1$(IO)=A1$(IO)+STR$(INT(S(10)*10+.5)/10)+CR$
4350 A1$(IO)=A1$(IO)+STR$(INT(S(11)*10+.5)/10)+CR$
4360 FOR I=19 TO 24:A1$(IO)=A1$(IO)+STR$(INT(S(I)+.5))+CR$:NEXT
4370 A1$(IO)=A1$(IO)+STR$(INT(S(25)*10+.5)/10)+CR$
4380 A1$(IO)=A1$(IO)+STR$(INT(S(26)*10+.5)/10):IO=IO+1
4385 PRINT#4,STRING$(40,35):PRINT#4,:RETURN
4399 :
55555 ' setup sl ds fm or zs output
4400 :
4410 IF MDD$="o3" THEN A1$(IO)=R$ ELSE A1$(IO)=R$+MDD$
4420 A1$(IO)=A1$(IO)+C$+CR$+"a"+CR$+M5$+CR$+T0$+CR$+WL$+CR$+WU$+CR$+CZ$+CR$
4430 FOR I=WL TO WU:A1$(IO)=A1$(IO)+STR$(F(I))+CR$:NEXT:RETURN
4440 A1$(IO)=A1$(IO)+"rat":FOR I=4 TO 7:A1$(IO)=A1$(IO)+CR$+STR$(MS(I))
4445 NEXT:IO=IO+1:RETURN
4449 :
55555 ' data header
4450 GOSUB 3225
4460 A1$(0)="dh"+CR$+DA$+CR$+MO$+CR$+YE$+CR$+LC$+CR$+STR$(LA)+CR$+STR$(LO)+CR$
4470 A1$(0)=A1$(0)+TE$+CR$+"pr"+CR$+PZ$:RETURN
4480:
4482 RET = 1
4485 RET = 1: A$=DD$+HD$+JD$+YF$+"."+NO$:CLOSE 1:OPEN A$ FOR APPEND AS 1
4486 PRINT#1,LF$;:IF RET=1 THEN PRINT#1,"co";CR$;time$;CR$;"const: constants":RET = 0: gosub 4516: else gosub 4505
4490 return
4499 :
55555 ' write ic to B file
4500 :
4505 PRINT#1,"version=2";CR$;
4510 PRINT#1,"dh";CR$;DA$;CR$;MO$;CR$;YE$;CR$;LC$;CR$;LA;CR$;LO;CR$;TE$;CR$;
4515 PRINT#1,"pr";CR$;PZ$;R$;"co";CR$;time$;CR$;"dh: day header";R$;
4516 PRINT#1,"co";CR$;time$;CR$;"main: version ";VER$;CR$;VERD$;R$;"co";CR$;time$;CR$;"skc: ";SK$;R$;
4517 PRINT#1,"co";CR$;time$;CR$;"autohg: ";INT(AUTOHG);R$;"co";CR$;time$;CR$;"hifw2: ";INT(HIFW2);R$;
4518 PRINT#1,"co";CR$;time$;CR$;"attn: ";LATTN!;UATTN!;R$;"co";CR$;time$;CR$;"lowds: ";INT(OPT.LOWDS);R$;"co";CR$;time$;CR$;"cubdsp: ";INT(CUB.U*CUB.F);R$;
4520 NN$=DD$+NO$+"\"+ICF$+"."+NO$:NT$="inst":GOSUB 4650
4530 IF CUB.U*CUB.F<>1 THEN NN$=DD$+NO$+"\"+DCF$+"."+NO$:NT$="disp":GOSUB 4650 ELSE NN$=DD$+NO$+"\"+LEFT$(DCF$,6)+"~1.CUB":NT$="disp3":GOSUB 4650
4560 NN$=DD$+NO$+"\"+ZSF$+"."+NO$:NT$="zeni":GOSUB 4650
4570 NN$=DD$+NO$+"\"+"op_st"+"."+NO$:NT$="op_st":GOSUB 4650
4580 IF Q8%=1 THEN NN$=DD$+NO$+"\"+UVR$+"."+NO$:NT$="uvr":GOSUB 4650
4585 IF SK$<>"" AND SK$<>"menu" THEN NN$=BREWDIR$+"\"+SK$+".skd":NT$="schedule":GOSUB 4650
4590 MEM=FRE(0):MEM=FRE(""):RETURN
4599 :
55555 ' insert one com in seq
4600 :
4610 IF QC<49 THEN FOR I=QC TO JJ STEP -1: G$(I+1) = G$(I): NEXT: QC = QC+1: RETURN: ELSE RETURN
4619 :
55555 ' Read file FN$ and save to stream #1
4650 ON ERROR GOTO 4810
4655 CLOSE 8:OPEN NN$ FOR INPUT AS 8: IF NOR%=1 THEN 4695
4658 PRINT#1,LF$;NT$;CR$;
4660 IF EOF(8) THEN 4690
4665 INPUT#8, I$: PRINT#1, I$;CR$;
4670 GOTO 4660
4690 PRINT#1,"@";CR$;
4695 CLOSE 8:ON ERROR GOTO 3100:RETURN
4699 :
55555 ' auto fr/hg
4700 :
4705 'IF AUTOHG = 0 THEN RETURN
4710 IF NEED.HG = 1 AND C$<>"hg" AND C$<>"hp"  AND SK$<>""  AND AUTOHG = 1 AND MDD$="o3" THEN JJ=JJ-1:C$="hg": ZS = -200
4715 IF NEED.FR = 1 AND C$<>"fr" AND SK$<>"" THEN JJ=JJ-1:C$="fr"
4720 IF JJ<1 THEN JJ=1
4730 RETURN
4800 '
4810 RESUME 4820
4820 ON ERROR GOTO 3100:B$ = "Cannot open "+DD$+NO$+"\"+NN$+" please check "+NT$+" record in OP_ST.": CLS: PRINT B$: END
55555 ' Test if FTMP$ file exists and create it if it doesn't
4900 ON ERROR GOTO 4940:CLOSE 1:OPEN FTMP$ FOR INPUT AS 1
4920 CLOSE 1:ON ERROR GOTO 0:SHELL "noeof "+FTMP$:SHELL "copy tmp.tmp "+FTMP$: RETURN
4930 CLOSE 1:OPEN FTMP$ FOR APPEND AS 1: GOTO 4920
4940 RESUME 4930
55555 ' error
5000 CLS:PRINT CL$:LOCATE 4,1
5010 PRINT "Cannot find ";DD$;NO$;"\OP_ST.";NO$:PRINT
5015 PRINT "Please check the following:":PRINT
5020 PRINT "  1. Contents of ";BREWDIR$;"\op_st.fil"
5025 PRINT "  2. Instrument constants in ";DD$;NO$:PRINT
5030 GOTO 3100
55555 ' tracking setup
5100 UC%=0:GOSUB 9740:M5$="192":M4$="320"
5115 IF TR$="fm" OR TR$="ma" THEN M5$="0":M4$="128"
5120 GOSUB 9790:GOSUB 9800:GOSUB 7750:O1$="R,1,1,1":GOSUB 9450
5130 RETURN
55555 ' write op state
5200 GOSUB 2550
5210 PRINT CL$:CLOSE 8:OPEN DD$+NO$+"\op_st."+NO$ FOR OUTPUT AS 8
5220 PRINT#8,NO$;R$;DD$;R$;ICF$;R$;ZSF$;R$;DCF$;R$;UVR$;R$;DA$;R$;MO$;R$;YE$;R$;
5230 PRINT#8,LO$;R$;L1$;R$;L2$;R$;L3$;R$;
5240 PRINT#8,TE$;R$;NC%;R$;HC%;R$;SR%;R$;
5250 PRINT#8,Q1%;R$;Q2%;R$;Q3%;R$;Q4%;R$;
5260 PRINT#8,Q5%;R$;Q6%;R$;Q7%;R$;Q8%;R$;Q9%;R$;
5262 PRINT#8,Q10%;R$;Q11%+2*Q16%+4*Q17%;R$;0;R$;Q13%;R$;Q14%;R$;Q15%;R$;
5265 PRINT#8,DI$;R$;MDD$;R$;SK$;R$;
5270 CLOSE 8
5280 OPEN "op_st.fil" FOR OUTPUT AS 8:PRINT#8,NO$;R$;DD$;R$;:CLOSE 8
5290 RETURN
55555 ' read op state
5300 CLOSE 8:IF NO$<>"-1" THEN GOSUB 2550:GOSUB 5280
5320 IF NO$= "-1" THEN OPEN "op_st.fil" FOR INPUT AS 8:INPUT#8,NO$,DD$:CLOSE 8:GOSUB 5395:GOSUB 2500:CL$=CHR$(12)
5330 ON ERROR GOTO 5000
5340 PRINT CL$:OPEN DD$+NO$+"\op_st."+NO$ FOR INPUT AS 8
5350 INPUT#8,A$,A$,ICF$,ZSF$,DCF$,UVR$,A$,B$,C$,LO$,L1$,L2$,L3$
5355 IF Q14%=0 OR Q6%=0 OR (Q14%=1 AND Q12%=1) THEN DA$=A$:MO$=B$:YE$=C$
5360 INPUT#8,TE$,NC%,HC%,SR%,Q1%,Q2%,Q3%,Q4%,Q5%,Q6%,Q7%,Q8%,Q9%,Q10%
55555 ' IOS uart board
5370 INPUT#8,Q11%,I,Q13%,Q14%,Q15%,DI$,MDD$,SK$:Q16%=0:IF Q11%>1 THEN Q17%=INT(Q11%/4): Q16%=INT((Q11%-Q17%*4)/2): Q11%=Q11%-Q16%*2-Q17%*4
5375 N9$="6":SE$=":":IF TYP$="mkiii" AND Q14%=1 THEN N9$="9":SE$="&"
5380 CLOSE 8:ON ERROR GOTO 3100:GOSUB 2300:GOSUB 5395
5390 RETURN
5395 IF RIGHT$(DD$,1)<>"\" THEN DD$=DD$+"\"
5396 RETURN
55555 ' read ic file
55555 '  I=0  all
55555 '  I=1 except dsp/zs
5400 ON ERROR GOTO 5000
5412 CLOSE 8:OPEN DD$+NO$+"\"+ICF$+"."+NO$ FOR INPUT AS 8
5414 ON ERROR GOTO 3100
55555 ' get O3 TC's
  5422 FOR J=2 TO 6:INPUT#8,TC(J):NEXT
5424 TC(0)=TC(2)-TC(5)-3.2*(TC(5)-TC(6)):TQ(0)=TC(0)
5426 TC(1)=TC(3)-TC(5)-.5*(TC(4)-TC(5))-1.7*(TC(5)-TC(6)):TQ(1)=TC(1)
55555 ' get other values
5432 INPUT#8,PC,A1,A2,A3,B1,B2,T1,MC$,DL$,UO$
5434 FOR J=0 TO 5:INPUT#8,AF(J):NEXT
5436 INPUT#8,ER$,TYP$,CP$
5437 N9$="6":SE$=":":IF TYP$="mkiii" AND Q14%=1 THEN N9$="9":SE$="&"
5438 OF%=148-VAL(MC$):M8$=MC$:ER%=VAL(ER$):CP%=VAL(CP$)
5440 AD=1019:IF CP%=2 THEN AD=AD-256
55555 ' must be 1 char
5442 CP$=RIGHT$(STR$(CP%),1)
55555 ' get other TC's
5452 INPUT#8,TC(7),NTC(7)
5454 FOR J=2 TO 6:INPUT#8,NTC(J):NEXT
5456 NTC(0)=NTC(2)-NTC(5)-3.2*(NTC(5)-NTC(6)):NTQ(0)=NTC(0)
5458 NTC(1)=.1*NTC(2)-.59*NTC(3)+.11*NTC(4)+1.2*NTC(5)-.82*NTC(6):NTQ(1)=NTC(1)
55555 ' get rest
5462 INPUT#8,MZ%,MY%,MX%,NA1,NB1,NB2,NMZ%,NMX%,SWITCH%,GS,GI
5464 INPUT#8,ZERO,IRIS,DELAY,NOFW1,OZFW1,POFW2,UF$,ZO%
5466 INPUT#8,ZU%:IF ZU%<999 THEN ZU%=ER%*3/4
5467 ON ERROR GOTO 5470
5468 INPUT#8,A$,A$: IF INSTR(A$,"EXTRAS")=0 THEN 5472
5469 INPUT#8,AUTOHG,HIFW2,HGFW2,LATTN!,UATTN!,CUB.U,OPT.LOWDS,DR6,DR5: GOTO 5472
5470 RESUME 5472
55555 ' get DSP/ZE coeffs
5472 IF I<>0 THEN 5484
5474 CLOSE 8:OPEN DD$+NO$+"\"+DCF$+"."+NO$ FOR INPUT AS 8
5476 FOR I=1 TO 6:FOR J=1 TO 3:INPUT#8,DC(I,J):DC(0,J)=DC(6,J):NEXT:NEXT
5478 FOR I=1 TO 6:FOR J=1 TO 3:INPUT#8,NDC(I,J): IF NDC(I,J) = 0 THEN NDC(I,J)=3.0/2.0*DC(I,J)
5479 NDC(0,J)=NDC(6,J):NEXT:NEXT
5480 CLOSE 8:OPEN DD$+NO$+"\"+ZSF$+"."+NO$ FOR INPUT AS 8
5482 FOR I=1 TO 9:INPUT#8,ZSC(I):NEXT
55555 ' Load cubic dispersion file if planing on using it
5484 CLOSE 8: CUB.F=0: IF CUB.U = 1 THEN 5500
5490 RETURN
55555 ' open cubic dispersion file
5500 ON ERROR GOTO 5595
5512 CLOSE 8:OPEN DD$+NO$+"\"+LEFT$(DCF$,6)+"~1.CUB" FOR INPUT AS 8
55555 ' get cubic dispersion file
5576 FOR I=1 TO 6:FOR J=1 TO 4:INPUT#8,DC3(I,J):DC3(0,J)=DC3(6,J):NEXT:NEXT
5578 FOR I=1 TO 6:FOR J=1 TO 4:INPUT#8,NDC3(I,J): IF NDC3(I,J) = 0 THEN NDC3(I,J)=3.0/2.0*DC3(I,J)
5579 NDC3(0,J)=NDC3(6,J):NEXT:NEXT:CUB.F=1
5580 ON ERROR GOTO 3100
5590 CLOSE 8: RETURN
5595 RESUME 5590
55555 ' set LD%/LY%
5600 IF YE%=INT(YE%/4)*4 THEN LY%=1 ELSE LY%=0
5610 IF DA%>30 THEN LD%=1
5620 IF DA%=30 THEN IF VAL(MO$)=4 OR VAL(MO$)=6 OR VAL(MO$)=9 OR VAL(MO$)=11 THEN LD%=1
5630 IF VAL(MO$)=2 AND ((DA%=28 AND LY%=0) OR (LY%=1 AND DA%=29)) THEN LD%=1
5640 RETURN
55555 ' set new month/year
5650 DA%=1:MO$=RIGHT$(STR$(VAL(MO$)+101),2)
5660 IF MO$="13" THEN MO$="01":YE$=RIGHT$(STR$(YE%+101),2):YE%=VAL(YE$)
5670 YE$=RIGHT$(STR$(YE%+100),2):MM=VAL(MO$)*3-2:MO%=VAL(MID$(MD$,MM,3))
5680 MP$=MID$(MN$,MM,3)+" "
5690 RETURN
55555 ' calculate moon's age
5730 GOSUB 7700: MOON.AGE=.20439731#+(T*365.2422-13150)*.03386319269#: MOON.AGE=MOON.AGE-INT(MOON.AGE):RETURN
55555 ' add gmtday
5800 GOSUB 5600:A$=INKEY$:FLAG=1
5815 T0 = TIMER/60
55555 ' set if pm gmt
5820 IF T0>720 THEN T0%=1
55555 ' no new gmtday
5830 IF (T0%=0) OR (T0>710) THEN GOSUB 7800:GOTO 5900
5840 T0%=0:DA%=DA%+1:IF LD%=1 THEN GOSUB 5650:LD%=0
5845 DA$=RIGHT$(STR$(DA%+100),2):DB$=DA$+"/"
5850 PRINT#4,"    Observations for ";MP$;DB$;YE$:GOSUB 3225
5860 GOSUB 2500:GOSUB 7700:GOSUB 5200:GOSUB 7800
55555 ' add local day
5900 IF A$="" THEN A$=INKEY$
55555 ' set if local pm
5910 IF ZC>0 THEN ZA%=1
55555 ' if no new local day
5920 IF (ZA%=0) OR (ZC>0) THEN GOSUB 7800:RETURN
5924 ZA%=0:ZS=-200:IF ED%=1 THEN C$="ed":AZCN%=0:GOTO 3453
5927 IF ED%=2 THEN RETURN
5930 GOSUB 6200:GOSUB 3900:GOSUB 3230:GOSUB 5200
5940 IF ED%=3 THEN ED%=1
5950 GOSUB 7800:RETURN
55555 ' merge msg
6000 PRINT CL$:LOCATE 10,20:PRINT "   ***  Merging "+C$+E$+"  ***"
6020 LOCATE 12,32:PRINT TIME$:X=.5:GOSUB 7220
6030 RETURN
55555 ' stats
6050 MDS=0:MZS=0:MSO2=0:CDS=0:CZS=0:CSO2=0:NDS=0:NZS=0:CAOD=0:MAOD=9999
6070 UTIME$="":ULAST=0:STIME$="":SLAST=0:HTIME$="":HLAST%=0:RETURN
55555 ' read in date
6100 LD%=0:ZA%=0:T0%=0:FLAG=0:CLS:PRINT CL$
6110 LOCATE 8,SP-2:PRINT "DATE: - (C.U.T.) - ";MP$;DB$;YE$:PRINT
6119 LOCATE ,SP:PRINT "If correct press Enter"
6121 LOCATE ,SP:PRINT "If incorrect enter new date"
6130 PRINT:LOCATE ,SP:PRINT "  -- DAY   - DD ";:B$="":X$="3":GOSUB 6180
6132 IF A$=CR$ THEN FLAG=0:GOTO 6172
6134 X$="9":GOSUB 6180:DA$=B$:IF VAL(B$)>31 THEN FLAG=-1
6140 PRINT:LOCATE ,SP:PRINT "  -- MONTH - MM ";:B$="":X$="1":GOSUB 6180
6142 X$="9":GOSUB 6180:MO$=B$:IF VAL(B$)<1 OR VAL(B$)>12 THEN FLAG=-1
6150 PRINT:LOCATE ,SP:PRINT "  -- YEAR  - YY ";:B$="":X$="9":GOSUB 6180
6152 X$="9":GOSUB 6180:YE$=B$
6154 IF VAL(YE$)=INT(VAL(YE$)/4)*4 THEN LY%=1 ELSE LY%=0
6156 IF INSTR("04,06,09,11",MO$)<>0 AND DA$>"30" THEN FLAG=-1
6160 IF LY%=0 AND MO$="02" AND DA$>"28" THEN FLAG=-1
6162 IF NOT FLAG THEN 6170
6164 LOCATE 18,SP:PRINT "Some parameter is wrong in New Date, try again "
6166 DA$="01":MO$="01":YE$="90":MP$="jan ":DB$="01/":X=2:GOSUB 7220
6169 GOTO 6100
6170 GOSUB 6200:IF R%=0 THEN 6110
6172 IF Q14%=1 THEN GOSUB 7400
6174 GOSUB 5200:GOSUB 4485:RETURN
6180 GOSUB 2030:IF A$>="0" AND A$<=X$ THEN B$=B$+A$ ELSE FLAG=-1
6181 RETURN
55555 ' update date vars
6200 DA%=VAL(DA$):YE%=VAL(YE$):MO%=VAL(MO$):GOSUB 5600
6220 MM=MO%*3-2:MO%=VAL(MID$(MD$,MM,3)):MP$=MID$(MN$,MM,3)+" "
6230 YF$=RIGHT$(STR$(YE%+100),2):DB$=RIGHT$(STR$(DA%+100),2)+"/"
6240 TI=TIMER*60:T0=TI/3600:GOSUB 8600:FLAG=1:GOSUB 7800
6250 JD%=MO%+DA%:IF LY%=1 AND MM>5 THEN JD%=JD%+1
6260 A$=MP$:B$=LEFT$(DB$,2):IF VAL(L2$)<=0 OR RA<0 OR H%>11 THEN 6330
6270   JD%=JD%-1:IF DA%<>1 THEN A$=MP$:B$=RIGHT$(STR$(DA%+99),2):GOTO 6330
6280     IF VAL(MO$)=1 THEN JD%=365:A$=MID$(MN$,12*3-2,3):YF$=RIGHT$(STR$(YE%+99),2):IF (YE%-1)/4=INT((YE%-1)/4) THEN JD%=366
6290     IF VAL(MO$)<>1 THEN A$=MID$(MN$,MM-3,3)
6300     A$=A$+" ":IF INSTR("05,07,10,12",MO$)<>0 THEN B$="30":GOTO 6330
6310       IF VAL(MO$)<>3 THEN B$="31" ELSE IF LY%=1 THEN B$="29" ELSE B$="28"
6330 IF VAL(L2$)>=0 OR RA>0 OR H%<13 THEN 6380
6340   JD%=JD%+1:IF LD%<>1 THEN A$=MP$:B$=RIGHT$(STR$(DA%+101),2):GOTO 6380
6350     B$="01":IF VAL(MO$)=12 THEN JD%=1:A$=MID$(MN$,1*3-2,3)+" ":YF$=RIGHT$(STR$(YE%+101),2)
6360     IF VAL(MO$)<>12 THEN A$=MID$(MN$,MM+3,3)+" "
6380 DC$=A$+B$+"/"+YF$:IF R%<>2 THEN PRINT#4,MDD$;" Observations for ";MP$;DB$;YF$
6390 JD$=RIGHT$(STR$(JD%+1000),3):GOSUB 3900:GOSUB 2550:GOSUB 7700:RETURN
55555 ' motor pos
6500 Y=0:IF Q14%=0 THEN RETURN
6510 O1$="?MOTOR.POS["+M1$+"]":GOSUB 9450:Y=VA
6515 RETURN
55555 ' motor init
6550 Y=0:IF Q12%=1 OR Q14%=0 THEN RETURN
6560 O1$="?MOTOR.ZERO.POS["+M1$+"]":GOSUB 9450:IF VA>16000 THEN VA=16000
6562 IF VA<-16000 THEN VA=-16000
6563 YY%=VA
6565 O1$="?MOTOR.ORIGIN["+M1$+"]":GOSUB 9450:YY%=YY%-VA
6570 O1$="?MOTOR.SLOPE["+M1$+"]":GOSUB 9450:IF VA<>0 THEN YY%=INT(YY%/VA+.5)
6575 IF FLAG=1 AND M1$<>"2" THEN O1$="I,"+M1$:GOSUB 9450:GOSUB 6500:IF Y<0 THEN B$=TIME$+" Motor "+M1$+" appears to be stuck - Check the instrument":PRINT#4,B$
6580 IF FLAG=1 AND M1$="2" THEN GOSUB 2300:IF FLAG=0 THEN GOSUB 2350
6585 O1$="?MOTOR.DISCREPANCY["+M1$+"]":GOSUB 9450:Y=YY%-VA
6590 RETURN
55555 ' scrn msgs
6600 :
6610 PRINT CL$:LOCATE ,SP:PRINT;"** Measurement Procedure **"
6615 LOCATE ,SP:PRINT "     Check:-":RETURN
6620 GOSUB 6640:PRINT "0":GOSUB 9780:RETURN
6630 GOSUB 6640:PRINT "1":GOSUB 9770:RETURN
6635 GOSUB 6640:PRINT "3":GOSUB 9785:RETURN
6636 GOSUB 6640:PRINT "4":GOSUB 9784:RETURN
6637 GOSUB 6640:PRINT "5":GOSUB 9787:RETURN
6640 LOCATE ,SP:PRINT "1 - Filter #1 to position # ";:RETURN
6650 GOSUB 6680:PRINT VAL(M5$)/64:GOSUB 9800:RETURN
6660 GOSUB 6640:PRINT (320-VAL(M4$))/64:GOSUB 9790:RETURN
6680 LOCATE ,SP:PRINT "2 - Filter #2 to position # ";:RETURN
6690 LOCATE ,SP:PRINT "3 - Close iris":GOSUB 9740:RETURN
6700 LOCATE ,SP:PRINT "3 - Open iris":GOSUB 9750:RETURN
55555 ' lamp warmup
6800 GOSUB 2090:IF HF%=1 THEN RETURN
6815 GOSUB 9891:GOSUB 50
6820 IF TA%=1 THEN TA%=0:RETURN
6825 TI=TIMER*60:IF (TI<216000!) AND (TA>4968000!) THEN TA=0:RETURN
6830 IF TI<TA THEN 6800
6840 RETURN
55555 '
6850 TA=300:GOSUB 7050
6860 TI=TIMER*60:IF TI>TA THEN RETURN
6870 IF IS%=42 THEN 6860
6880 RETURN
6899 :
55555 ' is lamp on?
6900 :
6910 WL$="0":WU$="7":CZ$="1":GOSUB 9700:GOSUB 9900
6915 WL$="1":WU$="1":CZ$="1":GOSUB 9700
6920 LO%=0:IF (F(4)-F(1))>498 THEN RETURN
6925 LO%=1:B$="Lamp not on... "+C$+" test terminated F1="+STR$(F(1))+" F4="+STR$(F(4)):PRINT#4,B$:GOSUB 3050
6930 RETURN
6949 :
55555 ' common subroutine for delay times handling
6950 TI=TIMER*60:IF TI>(5183880!-TDI) THEN LOCATE 14,20: COLOR 12: PRINT "!!! WAITING FOR GMT MIDNIGHT !!!  ";TIME$: COLOR 7:IF CLD%=1 THEN CLD%=0: CLS: GOTO 6950 ELSE GOTO 6950
6960 CLD%=1:TDI=TDI+TI:RETURN
55555 ' delay time
7000 TDI=TD: GOSUB 6950: TD=TDI: RETURN
55555 ' wait time
7050 TDI=TA: GOSUB 6950: TA=TDI: RETURN
55555 ' wait time
7080 TDI=TB: GOSUB 6950: TB=TDI: RETURN
7100 :
55555 ' short wait
7120 :
7130 TI=TIMER*60:TA=TA+TI
7140 TI=TIMER*60:IF TI<TA THEN 7140
7150 RETURN
7200 :
55555 ' short wait
7220 :
7230 TI=TIMER*60:TB=TI+X*60
7240 TI=TIMER*60:IF TI<TB THEN 7240
7250 RETURN
7298 :
55555 ' set PC/Brewer time
7300 :
7305 LD%=0:T0%=0:ZA%=0:GOSUB 3300:IF FP%<>0 THEN PRINT#4,"Clock checked at ";TIME$
7307 PRINT CL$:IF Q6%=1 THEN 7310
7310 LOCATE 10,PP:PRINT HO$;"time = ";TIME$;:LOCATE 12,PP
7315 PRINT "If correct press Enter":LOCATE 14,PP:PRINT " hhmmss  ";
7318 B$=INKEY$:IF (B$="") AND (B$<>CR$) THEN 7310
7320 PRINT B$;:IF B$=CR$ THEN GOSUB 7400:RETURN
7322 GOSUB 2010
7324 BADTIME%=0
7326 IF LEN(B$)<>6 THEN 7339
7328 FOR CHARPOS%=1 TO 6
7330   IF INSTR("1,2,3,4,5,6,7,8,9,0",MID$(B$,CHARPOS%,1))=0 THEN BADTIME%=-1
7332 NEXT
7334 IF VAL(LEFT$(B$,2))>23 OR VAL(MID$(B$,3,2))>59 THEN BADTIME%=-1
7336 IF VAL(RIGHT$(B$,2))>59 THEN BADTIME%=-1
7338 IF NOT BADTIME% THEN TIME$=LEFT$(B$,2)+":"+MID$(B$,3,2)+":"+RIGHT$(B$,2):GOTO 7307
7339 LOCATE 18,PP:PRINT "Wrong time try again"
7340 X=2:GOSUB 7220
7344 GOTO 7307
7345 IF R%=0 THEN 7310
7350 RETURN
7399 :
55555 ' set Brewer clock from PC
7400 :
7405 IF Q6%=0 THEN RETURN
7410 A=1900+YE%:IF YE%<50 THEN A=2000+YE%
7415 A$="!TIME"+STR$(A)+","+STR$(JD%)+","
7420 CLS:TI=TIMER*60:T0=(TI+180)/3600:GOSUB 8600:O1$="L,59391,23"
7430 X=H%:GOSUB 7470:O1$=O1$+",59388,"+N$:A$=A$+STR$(H%)+","
7440 X=M%:GOSUB 7470:O1$=O1$+",59387,"+N$:A$=A$+STR$(M%)+","
7450 X=S%:GOSUB 7470:O1$=O1$+",59386,"+N$:A$=A$+STR$(S%)
7460 IF Q14%=1 THEN O1$=A$
7465 GOSUB 9400:GOSUB 6200:RETURN
7470 IF X>99 THEN X=99
7480 IF X<0 THEN X=0
7490 N$=STR$(X+INT(X/10+.01)*6):RETURN
7499 :
55555 ' test and adjust attenuation with neutral density filters
7500 :
7510 SO$="":SO%=0
7520 WL$="0":WU$="6":CZ$="1":GOSUB 9700:GOSUB 9900:FS=F(5):WL$="0":WU$="6"
7522 FS = -1: FOR W=0 TO 6: IF F(W)>FS THEN FS = F(W)
7525 NEXT
7530 IF FS>UATTN! THEN A=INT(1+LOG(FS/UATTN!)*2/CO):GOTO 7570
7540 IF FS=0 THEN FS=1
7550 IF FS<LATTN! THEN A=INT(LOG(FS/LATTN!)*2/CO):IF M5$<>"0" THEN 7570
7560 RETURN
7570 N$=SO$:SO$=M5$:SO%=SO%+1
7580 A=A*64+VAL(M5$):IF A<0 THEN A=0
7590 IF A>320 THEN A=320
7600 PRINT CL$:A$=STR$(A):M5$=RIGHT$(A$,LEN(A$)-1)
7610 IF N$=M5$ OR SO%>3 THEN B$="Suppressed oscillation of filter wheel.":PRINT#4,B$:GOSUB 3050:GOTO 7640
7620 GOSUB 9800:LOCATE ,SP
7630 PRINT "Change filter #2 to position #";INT(A/64+.5):M5P%=A:GOSUB 9650:GOTO 7520
7640 IF VAL(SO$)*HIFW2>VAL(M5$)*HIFW2 OR HIFW2 = 0 THEN M5$=SO$: ELSE GOSUB 9800
7650 RETURN
7699 :
55555 ' calc year # from 1965
7700 :
7710 T=0:IF MO%<35 AND YE%/4=INT(YE%/4) THEN T=-1
7720 T=(T+DA%+25+INT(YE%/4)+(YE%+35)*365-16+MO%)/365.2422
7725 IF YE%>50 THEN T=T-25/365.2422
7730 RETURN
7749 :
55555 ' update tracker pos
7750 :
7755 FLAG=2:IF TR$="ds" OR TR$="sa" THEN FLAG=1
7760 GOSUB 7800:GOSUB 7950
7765 RETURN
7799 :
55555 ' solar angles (FLAG=3/4 supply TI/T0; 1/3 sun; 2/4 moon)
7800 :
7805 IF FLAG<3 THEN TI=TIMER*60:T0=TI/3600
7810 EP=.999999999#:I=(279.4574+360*T+T0/1460.97)*P0
7815 IF FLAG=2 OR FLAG=4 THEN GOSUB 8400:GOTO 7830
7818 E=4.2*SIN(3*I)-2*COS(2*I)+596.5*SIN(2*I)-12.8*SIN(4*I)+19.3*COS(3*I)
7820 E=E-(102.5+.142*T)*SIN(I)+(.033*T-429.8)*COS(I):RA=(T0+E/60+720-LO*4)*P0/4
7825 A=ATN(.4336*SIN(I-E*P0/240))
7830 E=COS(RA)*COS(A)*COS(LA*P0)+SIN(LA*P0)*SIN(A):IF E=>1 THEN E=EP
7831 IF E=<-1 THEN E=-EP
7835 AZ=(SIN(A)-E*SIN(LA*P0))/SQR(1-E*E)/COS(LA*P0):IF AZ<=-1 THEN AZ=-EP
7836 IF AZ=>1 THEN AZ=EP
7840 IF AZ=0 THEN AZ=.0000001
7841 IF E=0 THEN E=.0000001
55555 ' CONVERSION ERROR
7845 AZ=ATN(SQR(1-AZ*AZ)/AZ)/P0:E=ATN(SQR(1-E*E)/E):IF E<0 THEN E=E+PI
55555 ' parallax correction
7850 IF FLAG=2 OR FLAG=4 THEN E=E+PL*P0*SIN(E)
7852 RB=COS(RA)
7855 RA=SIN(RA):IF AZ<=0 AND RA<=0 THEN AZ=180+AZ
7860 IF RA>0 THEN IF AZ<=0 THEN AZ=180-AZ ELSE AZ=360-AZ
7862 IF AZ=180 AND RB>0 AND LA<0 THEN AZ=0
7865 M2=.999216*SIN(E):GOSUB 7920:M3=M2:M2=.99656*SIN(E):GOSUB 7920
55555 ' correct refraction
7870 IF E>90.5*P0 THEN 7885
7875   C1=COS(E):D1=1/(.955+(20.267*C1))-.047121
7880   C1=C1+.0083*D1:IF C1>-EP AND C1<EP THEN E=PI/2-ATN(C1/SQR(1-C1*C1))
7885 ZA=INT(P3%*E/P0+.5)/P3%:ZC=ZA:IF RA<0 THEN ZC=-ZA
7890 AZC%=SR%*AZ/360+.5+NC%+UC%:IF AZC%>SR% THEN AZC%=AZC%-SR%
7891 IF AZC%<0 THEN AZC%=AZC%+SR%
7895 ZEC%=ER%*(1-ZA/180)/2+.5+HC%:RETURN
7920 M2=1/COS(ATN(M2/SQR(1-M2*M2)))
7930 M2=INT(P3%*M2+.5)/P3%:M2$=STR$(M2):RETURN
7949 :
55555 ' move az/ze to AZC%/ZEC%
7950 :
7955 IF TR$="" THEN RETURN
7960 O1$="":IF Q2%=0 THEN 7980
7965 O1$="M,2,"+STR$(AZC%):AZ%=AZC%
7970 IF ABS(AZC%-AP%)>1000 AND Q14%=0 THEN O1$="L,19414,120:"+O1$+":L,19414,255"
7972 AZCW%=0:IF ABS(AZCP%-AZC%) > .8*SR% AND AZCP%<>0 THEN AZCW%=-1:IF NEED.AZ<>1 THEN NEED.AZ=1
7973 AZCP%=AZC%
7975 AP%=AZC%:IF Q1%=1 AND (TR$="fm" OR TR$="ds") THEN O1$=O1$+":"
7980 IF Q1%=1 AND (TR$="fm" OR TR$="ds") THEN O1$=O1$+"M,1,"+STR$(ZEC%):ZE%=ZEC%
7985 IF O1$<>"" THEN GOSUB 9400
7990 RETURN
7999 :
55555 ' zero s(i)
8000 :
8005 S(0)=0
8010 FOR I=1 TO 40:MS(I)=0:S(I)=0:NEXT
8015 RETURN
8024 :
55555 ' zero z(i)
8025 :
8030 Z(0)=0
8035 FOR I=1 TO 40:MZ(I)=0:Z(I)=0:NEXT
8040 RETURN
8049 :
55555 ' sum s(i)
8050 :
8055 S(0)=S(0)+1
8060 FOR I=1 TO MS(0):S(I)=S(I)+MS(I):S(I+15)=S(I+15)+MS(I)^2:NEXT
8065 RETURN
8074 :
55555 ' sum z(i)
8075 :
8080 Z(0)=Z(0)+1
8085 FOR I=1 TO MZ(0):Z(I)=Z(I)+MZ(I):Z(I+15)=Z(I+15)+MZ(I)^2:NEXT
8090 RETURN
8099 :
55555 ' calc mean,stddev
8100 :
8105 FOR I=1 TO MS(0)
8110   IF S(0)<2 THEN E=0:GOTO 8120
8115     E=(S(0)*S(I+15)-S(I)*S(I))/S(0)/(S(0)-1):IF E<0 THEN E=0
8120   S(I+15)=INT(10*SQR(E)+.5)/10
8125   IF S(0)<>0 THEN S(I)=INT(P3%*S(I)/S(0))/P3%
8130 NEXT:RETURN
8149 :
55555 ' calc mean,stddev
8150 :
8155 FOR I=1 TO MZ(0)
8160   IF Z(0)<2 THEN E=0:GOTO 8170
8165     E=(Z(0)*Z(I+15)-Z(I)*Z(I))/Z(0)/(Z(0)-1):IF E<0 THEN E=0
8170   Z(I+15)=INT(10*SQR(E)+.5)/10
8175   IF Z(0)<>0 THEN Z(I)=INT(P3%*Z(I)/Z(0))/P3%
8180 NEXT:RETURN
8199 :
55555 ' print counts,corr logs
8200 :
8205 AF%=INT(VAL(M5$)/64+.5):T0=TQ:GOSUB 8600
8210 PRINT#4,USING "\          \ ###";C$+RIGHT$(STR$(AF%),1)+" "+H$;CY;
8215 FLAG=4:IF TR$="ds" OR TR$="sa" THEN FLAG=3
8220 GOSUB 7800:PRINT#4,USING " ###.##";ZA;
8225 FOR I=WL TO WU
8230   IF I=1 THEN PRINT#4,USING " ####";F(I);:GOTO 8240
8235   PRINT#4,USING " #######";F(I);
55555 ' SL needs raw counts
8240 NEXT:PRINT#4,:MS(10)=F(2):MS(11)=F(6)
8245 GOSUB 8300
8270 GOSUB 8700:GOSUB 4440
8275 IF MDD$="o3" THEN GOSUB 8800 ELSE GOSUB 8900
8280 GOSUB 8050:RETURN
8299 :
55555 ' calc corr F's
8300 :
8305 FOR I=WL TO WU:IF I=1 THEN 8335
8310   VA=F(I):GOSUB 8350
8315   F(I)=LOG(VA)/CO*P4%:J=I:IF J=0 THEN J=7
8320   IF MDD$="o3" THEN X=TC(J) ELSE X=NTC(J)
8325   F(I)=F(I)+X*TE%+AF(AF%)
55555 ' rayleigh
8330   IF C$="ds" OR C$="fz" OR C$="sc" OR C$="fm" THEN F(I)=F(I)+BE(I)*M3*PZ%/1013
8335 NEXT:RETURN
8349 :
55555 ' correct VA for dark/dead time
8350 :
8355 VA=(VA-F(1))*2/CY/IT:IF VA>1E+07 THEN VA=1E+07
8360 IF VA<2 THEN VA=2
8365 F1=VA:FOR J=0 TO 8:VA=F1*EXP(VA*T1):NEXT
8370 RETURN
8399 :
55555 ' calc lunar position
8400 :
8402 TT=(YE%-84)*365+INT((YE%-80)/4):IF YE%<50 THEN TT=(YE%+16)*365+INT((YE%+20)/4)
8403 IF MO%<35 AND YE%/4=INT(YE%/4) THEN TT=TT-1
8404 TT=(TT-5845.5+MO%+DA%+T0/1440)/36525!
8406 T2=TT*TT:T3=T2*TT:E=(84381.448#-46.815*TT-.00059*T2+.001813*T3)/3600
8408 C=36000.7701#:GOSUB 8462:DT=100.460618#+C+(.093104*T2-.0000062*T3)/240+T0/4
8410 XX=DT:GOSUB 8464:DT=XX
8412 C=481267.883#:GOSUB 8462:LB=218.32+C
8414 C=477198.85#:GOSUB 8462:LB=LB+6.29*SIN((134.9+C)*P0)
8416 C=413335.38#:GOSUB 8462:LB=LB-1.27*SIN((259.2-C)*P0)
8418 C=890534.23#:GOSUB 8462:LB=LB+.66*SIN((235.7+C)*P0)
8420 C=954397.7#:GOSUB 8462:LB=LB+.21*SIN((269.9+C)*P0)
8422 C=35999.05:GOSUB 8462:LB=LB-.19*SIN((357.5+C)*P0)
8424 C=966404.05#:GOSUB 8462:LB=LB-.11*SIN((186.6+C)*P0)
8426 XX=LB:GOSUB 8464:LB=XX
8428 C=483202.03#:GOSUB 8462:BT=5.13*SIN((93.3+C)*P0)
8430 C=960400.87#:GOSUB 8462:BT=BT+.28*SIN((228.2+C)*P0)
8432 C=6003.18:GOSUB 8462:BT=BT-.28*SIN((318.3+C)*P0)
8434 C=407332.2#:GOSUB 8462:BT=BT-.17*SIN((217.6-C)*P0)
8436 C=477198.85#:GOSUB 8462:PL=.9508+.0518*COS((134.9+C)*P0)
8438 C=413335.38#:GOSUB 8462:PL=PL+.0095*COS((259.2-C)*P0)
8440 C=890534.23#:GOSUB 8462:PL=PL+.0078*COS((235.7+C)*P0)
8442 C=954397.7#:GOSUB 8462:PL=PL+.0028*COS((269.9+C)*P0)
8444 SD=.2725*PL:R=1/SIN(PL*P0)
8446 L=COS(BT*P0)*COS(LB*P0)
8448 M=COS(E*P0)*COS(BT*P0)*SIN(LB*P0)-SIN(E*P0)*SIN(BT*P0)
8450 N=SIN(E*P0)*COS(BT*P0)*SIN(LB*P0)+COS(E*P0)*SIN(BT*P0)
8452 DC=ATN(N/SQR(1-N*N))/P0:LF=SGN(L*M)*90
8454 IF L<>0 THEN LF=ATN(M/L)/P0
8456 IF L<0 THEN LF=LF+180
8458 HA=DT-LF:XX=HA:GOSUB 8464:HA=XX
8460 RA=(HA-LO)*P0:A=DC*P0:RETURN
8462 C=C*(TT-INT(TT*C/360)*360/C):RETURN
8464 XX=XX-INT(XX/360)*360:RETURN
8499 :
55555 ' WV,SQ,GS,GI -> M1,M2
8500 :
8505 M1=DC(SQ,2)*DC(SQ,2)-4*(DC(SQ,1)-WV)*DC(SQ,3): M2=0
8510 IF MDD$="n2" THEN M1=NDC(SQ,2)*NDC(SQ,2)-4*(NDC(SQ,1)-WV)*NDC(SQ,3)
8515 IF M1<0 THEN M1=0:B$="CALCULATED M1<0 SET TO 0": PRINT#4, B$:GOSUB 3050
8520 IF M1>0 AND MDD$="o3" THEN M1=(-DC(SQ,2)+SQR(M1))/2/DC(SQ,3)
8521 IF M1>0 AND MDD$="n2" THEN M1=(-NDC(SQ,2)+SQR(M1))/2/NDC(SQ,3)
8522 IF CUB.U*CUB.F = 0 THEN 8530
8523 M1.C = (3*DC3(SQ,4)*M1*M1+2*DC3(SQ,3)*M1+DC3(SQ,2))*(3*DC3(SQ,4)*M1*M1+2*DC3(SQ,3)*M1+DC3(SQ,2))-4*(3*DC3(SQ,4)*M1+DC3(SQ,3))*(DC3(SQ,4)*M1*M1*M1+DC3(SQ,3)*M1*M1+DC3(SQ,2)*M1+DC3(SQ,1)-WV)
8524 IF MDD$="n2" THEN M1.C = (3*NDC3(SQ,4)*M1*M1+2*NDC3(SQ,3)*M1+NDC3(SQ,2))*(3*NDC3(SQ,4)*M1*M1+2*NDC3(SQ,3)*M1+NDC3(SQ,2))-4*(3*NDC3(SQ,4)*M1+NDC3(SQ,3))*(NDC3(SQ,4)*M1*M1*M1+NDC3(SQ,3)*M1*M1+NDC3(SQ,2)*M1+NDC3(SQ,1)-WV)
8525 IF M1.C>0 AND MDD$="o3" THEN M1=M1+(-(3*DC3(SQ,4)*M1*M1+2*DC3(SQ,3)*M1+DC3(SQ,2))+SQR(M1.C))/2/(3*DC3(SQ,4)*M1+DC3(SQ,3))
8526 IF M1.C>0 AND MDD$="n2" THEN M1=M1+(-(3*NDC3(SQ,4)*M1*M1+2*NDC3(SQ,3)*M1+NDC3(SQ,2))+SQR(M1.C))/2/(3*NDC3(SQ,4)*M1+NDC3(SQ,3))
8530 IF M1>=0 THEN M2=M1*GS+GI
8532 IF M2<0 THEN M2=0:B$= "CALCULATED M2<0 SET TO 0": PRINT#4,B$:GOSUB 3050
8535 M1=INT(M1+.5):M2=INT(M2+.5)
8540 RETURN
8549 :
55555 ' t0 -> h% m% s%
8600 :
8610 H%=INT(T0/60):M%=INT(T0-H%*60):S%=INT((T0-H%*60-M%)*60)
8615 IF H%>23 THEN H%=H%-24
8620 H$=RIGHT$(STR$(H%+100),2)
8625 H$=H$+":"+RIGHT$(STR$(M%+100),2)+":"+RIGHT$(STR$(S%+100),2):RETURN
8699 :
55555 ' calc ratios
8700 :
8705 IF MDD$="n2" THEN 8730
55555 ' single ratios
8710   FOR I=4 TO 6:MS(I)=F(5)-F(I-2):NEXT:MS(7)=F(6)-F(5)
55555 ' SO2 ratio
8715   MS(8)=MS(4)-3.2*MS(7)
55555 ' O3 ratio
8720   MS(9)=MS(5)-.5*MS(6)-1.7*MS(7)
8725   GOTO 8760
8730   FOR IM=4 TO 8:MS(IM)=F(IM-2):NEXT
55555 ' F ratio
8735   MS(9)=.1*F(2)-.59*F(3)+.11*F(4)+1.2*F(5)-.82*F(6)
8760 MS(1)=T0:MS(2)=ZA:MS(3)=M2:IF MDD$="o3" THEN MS(12)=F(2):MS(13)=F(6)
8790 RETURN
8799 :
55555 ' calc ozone
8800 :
8805 IF C$="sl" OR C$="um" OR C$="up" OR C$="su" THEN RETURN
8810 IF C$="zb" OR C$="zc" OR C$="zp" OR C$="zs" THEN 8830
55555 ' direct data
8815   MS(11)=(MS(9)-B1)/A1/M2
8820   MS(10)=((MS(8)-B2)/A3/M2-MS(11))/A2
8825   GOTO 8860
55555 ' zenith data
8830   A=ZSC(1)+ZSC(2)*M2+ZSC(3)*M2*M2-(MS(9)-B1)/P4%
8835   B=ZSC(4)+ZSC(5)*M2+ZSC(6)*M2*M2
8840   C=ZSC(7)+ZSC(8)*M2+ZSC(9)*M2*M2
8845   A=B*B-4*A*C:IF A<0 THEN A=0
8850   MS(11)=(-B+SQR(A))/2/C*P4%
8855   MS(10)=((MS(8)-B2)/A3/M2-MS(11))/A2
8860 MS(10)=INT(MS(10)+.5)/10:MS(11)=INT(MS(11)+.5)/10
8865 PRINT#4,USING SPACE$(19)+"O3 =####.#  SO2 =####.#";MS(11);MS(10)
8870 RETURN
8899 :
55555 ' calc NO2
8900 :
8905 IF C$="sl" OR C$="um" THEN RETURN
8910 IF C$="zb" OR C$="zc" OR C$="zp" OR C$="zs" OR C$="zw" THEN 8930
55555 ' direct data
8915   MS(11)=(MS(9)-NB1)/NA1/M2
8920   MS(10)=MS(9)
8925   GOTO 8940
55555 ' zenith data
8930   MS(11)=(MS(9)-NB2)/NA1/M2
8935   MS(10)=MS(9)
8940 MS(10)=INT(10*MS(10)+.5)/10:MS(11)=INT(MS(11)+.5)/10
8945 PRINT#4,USING SPACE$(19)+"NO2=####.#    F =####.#";MS(11);MS(10)
8950 RETURN
8999 :
55555 ' start up rs232
9000 IF Q12%=1 THEN RETURN:
55555 ' I/O status
9030 OS%=32:IS%=32
55555 ' input len
9040 IN%=0:DEF SEG=&HB000
9060 CR$=CHR$(13):CLOSE 7:R$=CR$+CHR$(10)
9082 IF A$<>"p" THEN OPEN "com"+CP$+COMBR$+COMSET$ AS 7
9095 RETURN
9099 :
55555 ' get i$
9100 :
9190 I$=LEFT$(I1$,IN%):VA=VAL(I$)
9195 RETURN
9199 :
55555 ' set rs232 speed
9200 :
9250 IF Q14%=1 THEN RETURN
9255 COM(CP%) OFF:O1$="F,0,2;V,30,1":IF Q16%=1 THEN O1$="F,0,2:L,16820,165"
9256 GOSUB 9450
9260 CLOSE 7:OPEN "com"+CP$+COMBR0$+COMSET$ AS 7
9265 RETURN
9270 IF Q14%=1 THEN RETURN
9275 COM(CP%) OFF: IF Q16%=0 THEN O1$="F,0,2;V,120,1":GOSUB 9450
9276 IF Q16%=1 THEN O1$="F,0,2:L,16820,141"+CR$:PRINT#7,O1$
9277 TA=2*60:GOSUB 7120
9280 CLOSE 7:OPEN "com"+CP$+COMBR$+COMSET$ AS 7
9290 RETURN
9299 :
55555 ' input routine
9300 :
9301 COM(CP%) OFF
9303 TDTMP=TD:TD=900:GOSUB 7000
9306 I1$="":ON ERROR GOTO 9345
9307 SF=TIMER*60
9308 TI=TIMER*60:IF TI<SF+DELAY*60 THEN 9308
55555 ' bufcount
9309 BC=LOC(7):IF BC=0 THEN 9324:
55555 ' bufcontents
9312 I2$=INPUT$(BC,7)
9315 I1$=I1$+I2$
9318 RX=INSTR(I1$,EL$)
9321 IF RX>0 THEN IS%=32:GOTO 9327
9324 TI=TIMER*60:IF TI<TD THEN 9309
9327 COM(CP%) OFF:ATTEMPT%=0
9330 I2$="":IN%=LEN(I1$)-3:IF IN%<1 THEN IN%=0
9333 I$=LEFT$(I1$,IN%):VA=VAL(I$)
9336 WX=CSRLIN:WY=POS(0)
9339 LOCATE 2,55:PRINT CHR$(IS%):LOCATE WX,WY
9342 ON ERROR GOTO 3100:TD=TDTMP:RETURN
9345 COM(CP%) OFF:WR=CSRLIN:WC=POS(0):LOCATE 24,40:PRINT "COM error at ";TIME$;
9346 IF FP%<>0 THEN PRINT#4,"COM error ";ERR;"(";ERL;") at ";TIME$;" during ";C$
55555 ' cnt, rx/tx
9348 IF FP%<>0 THEN PRINT#4,"[";BC;"]","(";I2$;")","(";O1$;")"
55555 ' ack COM error, reset TP
9351 LOCATE WR,WC:TD=TDTMP:RESUME 9357
9357 ATTEMPT%=ATTEMPT%+1
55555 ' reXmit
9360 :
55555 ' restart
9363 IF ATTEMPT%=6 THEN COM (CP%) OFF:GOTO 9610
9364 ON COM(CP%) GOSUB 9300:COM(CP%) ON
9366 O1$="T":RETURN
55555 ' flush buf
9375 :
9380 IF LOC(7)=0 THEN RETURN
9382 SF=TIMER*60
9384 TI=TIMER*60:IF TI<SF+DELAY*60 THEN 9384
9385 I1$=INPUT$(LOC(7),7)
9390 RETURN
9399 :
55555 ' send o1$ to brewer
9400 :
9405 GOSUB 9500
9410 WR=CSRLIN:WC=POS(0):LOCATE 25,40:PRINT SPACE$(30);
9415 OS%=42:LOCATE 2,61:PRINT CHR$(OS%):LOCATE WR,WC:OS%=32
55555 ' NOBREW mode
9420 IS%=42:IF Q12%=1 THEN IS%=32:GOTO 9430
55555 ' ip on
9425 ON COM(CP%) GOSUB 9300:COM(CP%) ON
9429 O$=O1$+CR$:PRINT#7,O$;
9430 WR=CSRLIN:WC=POS(0):LOCATE 2,55:PRINT CHR$(IS%)
9435 LOCATE 2,61:PRINT CHR$(OS%):LOCATE WR,WC
55555 ' set wait time
9440 O1$="":TD=3500:GOSUB 7000:RETURN
9445 :
55555 ' send, wait for response
9450 ATTEMPT%=0:GOSUB 9400:GOSUB 9500:RETURN
9455 :
55555 ' set break bit on UART
9460 BV=INP(AD)
9470 OUT AD,(BV OR 64):TA=60:GOSUB 7100
55555 ' reset break
9480 OUT AD,(INP(AD) AND 191)
9490 RETURN
9499 :
55555 ' brewer ready?
9500 :
9510 IF HF%=0 THEN GOSUB 2090:IF HF%=1 THEN GOSUB 9460:RETURN
9520 GOSUB 9891:WR=CSRLIN:WC=POS(0):IF IS%=42 THEN GOSUB 9550:GOTO 9510
9530 IF PF%<>0 THEN LOCATE 25,40:PRINT "Brewer responded at ";TIME$;
9535 IF C$<>"re" AND INSTR(I$,"SCI-TEC")>0 THEN PRINT#4,"Brewer reset at ";TIME$:RUN
9540 PF%=0:LOCATE WR,WC:RETURN
9549 :
55555 ' prompt Brewer
9550 :
9560 IF AZCW%=-1 THEN TD=TD+6000
55555 ' still waiting
9562 TI=TIMER*60:IF TI<TD THEN RETURN
55555 ' prompt flag++
9565 :
9570 IF TI<TD THEN RETURN
55555 ' NOBREW mode
9572 COM(CP%) OFF:PF%=PF%+1:IS%=42:IF Q12%=1 THEN IS%=32
9575 ON COM(CP%) GOSUB 9300:COM(CP%) ON
55555 ' prompt, set delay
9580 O$=CR$:PRINT#7,O$;:TD=2000:GOSUB 7000
9590 WR=CSRLIN:WC=POS(0):LOCATE 25,40:PRINT "Waiting for Brewer... (";PF%;")";
9600 LOCATE WR,WC:IF PF%<5 THEN RETURN
9610 CLS:BEEP:LOCATE 8,SP:B$="Brewer failed to respond 5 times": PRINT B$: GOSUB 3050
9620 LOCATE 9,SP:PRINT "Check Brewer power & cables etc."
9630 GOSUB 9650
9635 SHELL "setdate.exe":GOSUB 5300:GOSUB 6200
9640 PRINT#4,"Brewer reset at ";TIME$:RUN
9650 LOCATE ,SP:PRINT "  Press return when ready"
9654 IF RM%=0 THEN GOSUB 2030
9660 PRINT:RETURN
9670 PRINT CL$:LOCATE ,SP
9672 WR=CSRLIN:WC=POS(0)
9673 LOCATE 2,55:PRINT CHR$(IS%):LOCATE WR,WC
9674 LOCATE 2,61:PRINT CHR$(OS%):LOCATE WR,WC
9675 PRINT "*** Taking ";C$;" measurement ***";: IF C$="ds" THEN PRINT "  ND=";VAL(M5$)/64 ELSE PRINT
9676 LOCATE ,SP:PRINT "   Press HOME to stop":GOSUB 50:RETURN
55555 '
9699 :
55555 ' misc cmds; update pos (TR$)
9700 :
9710 GOSUB 9880:WT=(WU-WL+1)*VAL(CZ$)*18/7*IT:FLAG=4:IF TR$="ds" OR TR$="sa" THEN FLAG=3
9712 T0=TIMER/60+WT/120:GOSUB 7800:GOSUB 7950
9715 O1$="R,"+WL$+","+WU$+","+CZ$
55555 ' start obs at ts
9720 GOSUB 9400:TI=TIMER*60:TS=TI/7200
55555 ' wait time
9730 TD=200+WT*60:GOSUB 7000:RETURN
9739 :
9740 M3$="0":GOTO 9760
9750 M3$=STR$(IRIS)
55555 ' iris
9760 O1$="M,3,"+M3$:GOSUB 9400:RETURN
9770 M4$="256":GOTO 9790
9780 M4$="320":GOTO 9790
9784 M4$="64":GOTO 9790
9785 M4$="128":GOTO 9790
9787 M4$="0"
55555 ' FW#1
9790 O1$="M,4,"+M4$:GOSUB 9400:RETURN
55555 ' FW#2
9800 O1$="M,5,"+M5$:GOSUB 9400:RETURN
9805 GOSUB 9810:IF TYP$="mkiii" THEN GOSUB 9815:RETURN ELSE RETURN
9810 O1$="M,10,"+M8$:GOSUB 9400:TD=3000:GOSUB 7000:RETURN
9815 O1$="M,"+N9$+","+M8$:GOSUB 9400:TD=3000:GOSUB 7000:RETURN
55555 ' std lamp
9820 M9$="2":GOTO 9850
55555 ' merc lamp
9830 M9$="1":GOTO 9850
55555 ' lamps off
9840 GOSUB 2080:M9$="0":O1$="B,0":GOSUB 9450:RETURN
9850 O1$="B,"+M9$:TA=18000:GOSUB 7050:GOSUB 9450:RETURN
55555 ' ze to lamps
9860 M1$="0"
9870 O1$="M,1,"+M1$:ZE%=VAL(M1$):GOSUB 9450:RETURN
9880 WL=VAL(WL$):WU=VAL(WU$):CY=VAL(CZ$):RETURN
55555 '   Print humidity if available and colour it red if too high
9888 IF (Q14%=1 AND Q15%=1 AND RH*AH<>99*99) OR RH*AH<>99*99 THEN LOCATE 2,36: IF RH>18 THEN COLOR 12: PRINT USING"RH = ###% !";RH;UPDOWN$: ELSE PRINT USING"RH = ###% !";RH;UPDOWN$ :	
9889 COLOR SKCNAZ%+7: LOCATE 2,63:PRINT USING"####.## \   \";ZA;M2$:LOCATE 2,1:PRINT " ";C$;"    ";SK$;"  ";:COLOR 7:IF JJ<QC AND RM%=1 THEN PRINT G$(JJ+1) ELSE PRINT "--      "
9890 LOCATE 1,50:PRINT " C.U.T. ";CT$(Q6%);TIME$;" ";VER$;:IF Q16%=1 THEN PRINT " U":RETURN ELSE RETURN
9891 WR=CSRLIN:WC=POS(0):GOSUB 9888:LOCATE WR,WC:RETURN
9892 GOSUB 9830:IF TI>5130000! THEN RETURN
9893 PRINT#4,"**** HG lamp on at ";TIME$;" ****":RETURN
9894 GOSUB 9820:IF TI>5130000! THEN RETURN
9895 PRINT#4,"**** Standard lamp on at ";TIME$;" ****":RETURN
9896 GOSUB 9840:IF TI>5130000! THEN RETURN
9897 PRINT#4,"**** Both lamps off at ";TIME$;" ****":RETURN
9899 :
55555 ' get last obs numbers
9900 :
9910 GOSUB 9500:TI=TIMER*60:TQ=TS+TI/7200:IF TQ>TI/3600 THEN TQ=TQ+720
9920 IF (T0%=1) AND (TQ<720) THEN TQ=TQ+1440
9930 T0$=STR$(INT(100*TQ+.5)/100)
55555 ' NOBREW mode
9932 O1$="O":IF Q12%=1 THEN TA=0:GOTO 7120
9935 GOSUB 9450:O1$="T":IF IN%<10 THEN 9935
9940 O1$="":CA=0:XN=LEN(I$)
9945 CA=CA+1:IF CA>XN THEN XN=1:B$="bad data":PRINT#4,B$:PRINT#4,I$:GOSUB 3050:RETURN
9950 IF (VAL(I$)=0) AND (ASC(MID$(I$,CA,1))<>32) THEN 9945
9960 MM=WL
9965 XI=CA
9970 IF XI>XN THEN 9995
9975 CA=CA+1
9977 IF CA>=XN THEN 9985
9980 IF MID$(I$,CA,1)<>"," THEN 9975
9985 F(MM)=VAL(MID$(I$,XI,CA-XI))
9986 CA=CA+1
9990 MM=MM+1:IF MM=<WU THEN 9965
9995 XN=0:RETURN
9999 :
55555 ' dummy routine
10000 :
10001 REM
10005 DATA dummy
55555 '  **** proper last line must be 65529 *****
55555 'eof
65529 :
