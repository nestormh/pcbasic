10000 REM ****************** fr routine 12/08/98 *********************
55555 REM         MKII/MKIII/MKIV Micrometer and FW#3 Reset
55555 REM        
55555 REM             (SCI-TEC Instruments, August 1998)
55555 REM
55555 REM  See also:
55555 REM ************************************************************
55555 REM
55555 REM  This routine performs a reset of the micrometer(s) (and
55555 REM  filterwheel #3 if present) for both O3 and NO2 modes of
55555 REM  operation.
55555 REM
55555 REM  Side Effects:
55555 REM
55555 REM  Global Variables Referenced:
55555 REM    SPACE$()
55555 REM    PC MX% MY% MZ% NMX% NMZ% TE%
55555 REM    DD$ JD$ M8$ MC$ MDD$ NO$ O1$ TYP$ YF$
55555 REM
55555 REM  Exits: 12040
55555 REM
55555 REM  Uses:  3100, 9190, 9450
55555 REM
55555 REM ************************************************************
55555 REM  History: dd/mm/yy
55555 REM  25/06/02 - Added temperature check to fix MIOAVG file
55555 REM  22/05/98 - Replaced new electronics reset algorithm
55555 REM  18/02/98 - Q9% support added
55555 REM  14/07/95 - Fixed MIOAVG file output, based on Brewer type
55555 REM  19/01/95 - Supports new Brewer electronics
55555 REM  14/09/94 - Corrected operation of routine on MKIII Brewer
55555 REM  18/08/94 - MX%/MY%/MZ% confusion done away with
55555 REM  07/07/94 - Merged all FR and MR routines into one version
55555 REM  24/05/94 - References to mode.fil removed, modified to
55555 REM  handle both o3 and no2 modes using MDD$, MIOAVG.nnn file
10610 REM  now contains mode distinction o3 or n2
10620 REM ************************************************************
10700 DATA fr
11000 '
11001 ' *** Perform Search and Reset Micrometer (#1) ***
11002 '
11003 GOSUB 2450 	'get temperature
11010 B$="Reset for "+TYP$+" "+MDD$+" - Searching for micrometer #1 reference"
11020 PRINT#4,:PRINT#4,B$:PRINT B$:Y=0:VA=0
11030 IF Q14%=1 THEN FLAG=1:M1$="10":GOSUB 6550:GOTO 11210
11040 O1$="R,1,1,1:G,544":GOSUB 9450:GOSUB 9190:IF (VA AND 4)<>4 THEN 11070
11050 O1$="M,10,-500;G,544":Y=Y-500:PRINT Y,VA:GOSUB 9450:GOSUB 9190
11060 IF Y<=-3000 THEN PRINT#4, "-3000 steps - QUIT - Micrometer #1 jammed at MIN. end ?":GOTO 11400
11070 IF (VA AND 4)=4 THEN 11050
11080 O1$="M,10,-1:M,10,501:G,544":Y=Y-1+501:PRINT Y,VA:GOSUB 9450:GOSUB 9190
11090 IF Y>11000 THEN PRINT#4, "11000 steps - QUIT - Micrometer #1 reference not found":GOTO 11400
11100 IF (VA AND 4)=0 THEN 11080
11110 O1$="M,10,-500":GOSUB 9450
11120 Y=Y-500
11130 O1$="M,10,-1:M,10,51:G,544":Y=Y-1+51:PRINT Y,VA:GOSUB 9450:GOSUB 9190
11140 IF (VA AND 4)=0 THEN 11130
11150 O1$="M,10,-100":GOSUB 9450
11160 Y=Y-100
11170 YY%=0
11180 YY%=YY%+1
11190 O1$="M,10,"+MID$(STR$(YY%),2)+":G,544":Y=Y+1:LOCATE 22,30:PRINT Y,VA:GOSUB 9450:GOSUB 9190
11200 IF (VA AND 4)=0 THEN 11180
11210 IF MDD$="o3" THEN 11220 ELSE 11300
11220    B$="Micrometer #1 reference found at step"+STR$(Y)+"(+CAL)="+STR$(INT(Y)+VAL(MC$)):IF Q14%=1 THEN B$="Micrometer #1 discrepancy="+STR$(-Y)
11230    VAR4$=STR$(INT(MZ%-PC*TE%+.5)):B$=B$+" Now set to "+MDD$+" offset ="+VAR4$
11240    PRINT#4,B$:PRINT B$
11250    IF Q14%=0 THEN M8$="-"+VAR4$:GOSUB 9810
11260    IF Q14%=1 THEN M8$="0":GOSUB 9810:M8$=STR$(1-INT(MZ%-PC*TE%+.5)):GOSUB 9810:M8$="-1":GOSUB 9810
11270    M8$=MC$:GOSUB 9810:GOSUB 3050:GOTO 11400
11300    B$="Micrometer #1 reference found at step"+STR$(Y)+"(+CAL)="+STR$(INT(Y)+VAL(MC$)):IF Q14%=1 THEN B$="Micrometer #1 discrepancy="+STR$(-Y)
11310    VAR4$=STR$(INT(NMZ%-PC*TE%+.5)):B$=B$+" Now set to "+MDD$+" offset ="+VAR4$
11320    PRINT#4,B$:PRINT B$
11330    IF Q14%=0 THEN M8$="-2000":GOSUB 9810:M8$="-"+STR$(INT(NMZ%-2000-PC*TE%+.5)):GOSUB 9810
11340    IF Q14%=1 THEN M8$="0":GOSUB 9810:M8$=STR$(1-INT(NMZ%-PC*TE%+.5)):GOSUB 9810:M8$="-1":GOSUB 9810
11350    M8$=MC$:GOSUB 9810:GOSUB 3050
11400 PRINT#4," ":VAR3$=STR$(Y+VAL(M8$)):IF Q14%=1 THEN VAR3$=STR$(-Y)
11420 VAR7$="0":VAR8$="0"
12000 IF (VA AND 8)=8 THEN PRINT#4,"MICROMETER #1 APPEARS TO BE JAMMED AT MAX. END, You must re-position by HAND"
12010 IF TYP$="mkiii" THEN GOSUB 20000:REM Reset Micrometer #2
12020 IF (TYP$="mkiii" OR TYP$="mkiv") AND Q9%=1 THEN GOSUB 30000:REM Zero FW#3
12030 GOSUB 32000:REM Save Average File
12040 RETURN
20000 '
20001 ' *** Reset Micrometer #2 ***
20002 '
20010 B$="Searching for micrometer #2 reference":PRINT#4,B$:PRINT B$:Y=0:VA=0
20015 IF Q14%=1 THEN FLAG=1:M1$=N9$:GOSUB 6550:GOTO 20200
20020 O1$="R,1,1,1:G,800":GOSUB 9450:GOSUB 9190:IF (VA AND 32)<>0 THEN 20060
20030   O1$="M,6,-500;G,800":Y=Y-500:PRINT Y,VA:GOSUB 9450:GOSUB 9190
20040   IF Y<-3000 THEN PRINT#4, "-3000 steps - QUIT - Micrometer #2 jammed at MIN end ?":GOTO 21000
20050   IF (VA AND 32)=0 THEN 20030
20060 O1$="M,6,-1:M,6,501:G,800":Y=Y-1+501:PRINT Y,VA:GOSUB 9450:GOSUB 9190
20070 IF Y>11000 THEN PRINT#4, "11000 steps - QUIT - Micrometer #2 reference not found":GOTO 21000
20080 IF (VA AND 32)=32 THEN 20060
20090 O1$="M,6,-500":GOSUB 9450
20100 Y=Y-500
20110 O1$="M,6,-1:M,6,51:G,800":Y=Y-1+51:PRINT Y,VA:GOSUB 9450:GOSUB 9190
20120 IF (VA AND 32)=32 THEN 20110
20130 O1$="M,6,-100":GOSUB 9450
20140 Y=Y-100
20150 YY%=0
20160 YY%=YY%+1
20170 O1$="M,6,"+MID$(STR$(YY%),2)+":G,800":Y=Y+1:LOCATE 22,30:PRINT Y,VA:GOSUB 9450:GOSUB 9190
20180 IF (VA AND 32)=32 THEN 20160
20200 B$="Micrometer #2 reference found at step"+STR$(Y):IF Q14%=1 THEN B$="Micrometer #2 discrepancy="+STR$(-Y)
20210 VAR6$=STR$(INT(MY%-PC*TE%+.5)):B$=B$+" Now set to offset ="+VAR6$
20220 PRINT#4,B$:PRINT B$
20230 IF Q14%=0 THEN M8$="-"+VAR6$:GOSUB 9815:M8$="-"+MC$:GOSUB 9815
20240 IF Q14%=1 THEN M8$="0":GOSUB 9815:M8$=STR$(1-INT(MY%-PC*TE%+.5)):GOSUB 9815:M8$="-1":GOSUB 9815
21000 M8$=MC$:GOSUB 9815:GOSUB 3050:PRINT#4," ":VAR5$=STR$(-Y)
21010 RETURN
30000 '
30001 ' *** Zero Filterwheel #3 ***
30002 '
30010 IF Q14%=1 THEN FLAG=1:M1$="6":GOSUB 6550:GOTO 30230
30020 IF TYP$="mkiii" THEN O1$="M,10,-" + MC$: GOSUB 9450:REM Remember pos of first micrometer
30030 PRINT CL$;"Zeroing filterwheel #3":PRINT#4,"Zeroing filterwheel #3":Y=9
30040 IF TYP$="mkiv" THEN O1$="M,6,-1:M,6,10:G,800" ELSE O1$="B,3:M,10,-1:M,10,10:G,544"
30050 GOSUB 9450:GOSUB 9190
30060 YY=VA:PRINT Y
30070 IF TYP$="mkiv" THEN O1$="M,6,-25:G,800" ELSE O1$="B,3:M,10,-25:G,544"
30080 Y=Y-25:GOSUB 9450:GOSUB 9190
30090 IF Y<-600 THEN PRINT#4, "Filterwheel #3 reference not found":GOTO 31000
30100 IF(YY-VA)<>32 THEN 30060
30110 IF TYP$="mkiv" THEN O1$="M,6,25:G,800" ELSE O1$="B,3:M,10,25:G,544"
30120 Y=Y+25:GOSUB 9450:GOSUB 9190
30130 YY=VA:PRINT Y
30140 IF TYP$="mkiv" THEN O1$="M,6,-10:G,800" ELSE O1$="B,3:M,10,-10:G,544"
30150 Y=Y-10:GOSUB 9450:GOSUB 9190
30160 IF(YY-VA)<>32 THEN 30130
30170 IF TYP$="mkiv" THEN O1$="M,6,10:G,800" ELSE O1$="B,3:M,10,10:G,544"
30180 Y=Y+10:GOSUB 9450:GOSUB 9190
30190 YY=VA:PRINT Y
30200 IF TYP$="mkiv" THEN O1$="M,6,-1:G,800" ELSE O1$="B,3:M,10,-1:G,544"
30210 Y=Y-1:GOSUB 9450:GOSUB 9190
30220 IF(YY-VA)<>32 THEN 30190
30230 B$="Found reference at step "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
30240 IF Q14%=1 THEN B$="FW#3 discrepancy "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
30250 IF MDD$="o3" THEN B$=B$+STR$(MX%) ELSE B$=B$+STR$(NMX%)
30260 PRINT B$:PRINT#4,B$:PRINT#4,
31000 '
31010 VAR7$=STR$(Y)                 'Step for FW#3
31020 IF TYP$="mkiii" AND Q14%=0 THEN 31030 ELSE 31070
31030   VAR8$=MID$(STR$(MX%),2)             'FW#3 Position
31040   O1$="M,10,"+VAR8$:GOSUB 9450:GOSUB 9840:O1$="M,10,-1;M,10,2;M,10,-1:M,10,"+MC$
31060   GOSUB 9450:GOTO 31200:REM Restore first micrometer pos
31070 IF MDD$="o3" THEN 31080 ELSE 31100
31080   VAR8$=MID$(STR$(MX%),2)             'FW#3 Position
31090   O1$="M,6,"+VAR8$:GOSUB 9450:GOTO 31200
31100   VAR8$=MID$(STR$(NMX%),2)            'FW#3 Position
31110   O1$="M,6,"+VAR8$:GOSUB 9450
31200 RETURN
32000 '
32010 ' ***  Append to Average File ***
32020 '
32030 ON ERROR GOTO 32300
32040 AVGFILE$="MIOAVG"                 'Average File Name
32050 VAR1$=STR$(TE%)                   'Temperature
32060 VAR2$=M8$                         'Last Cal Step
32070 OPEN DD$+AVGFILE$+"."+NO$ FOR APPEND AS #6
32080 IF LEN(VAR1$) < 10 THEN VAR1$=SPACE$(7-LEN(VAR1$))+VAR1$
32090 IF LEN(VAR2$) < 10 THEN VAR2$=SPACE$(10-LEN(VAR2$))+VAR2$
32100 IF LEN(VAR3$) < 10 THEN VAR3$=SPACE$(10-LEN(VAR3$))+VAR3$
32110 IF LEN(VAR4$) < 10 THEN VAR4$=SPACE$(10-LEN(VAR4$))+VAR4$
32120 IF LEN(VAR5$) < 10 THEN VAR5$=SPACE$(10-LEN(VAR5$))+VAR5$
32130 IF LEN(VAR6$) < 10 THEN VAR6$=SPACE$(10-LEN(VAR6$))+VAR6$
32140 IF LEN(VAR7$) < 10 THEN VAR7$=SPACE$(10-LEN(VAR7$))+VAR7$
32150 IF LEN(VAR8$) < 10 THEN VAR8$=SPACE$(10-LEN(VAR8$))+VAR8$
32160 '
32170 PRINT#6,JD$+YF$;" ";MDD$;" ";VAR1$;" ";VAR2$;" ";VAR3$;" ";VAR4$;
32180 IF TYP$="mkii"  THEN PRINT#6," "
32190 IF TYP$="mkiii" THEN PRINT#6," ";VAR5$;" ";VAR6$;
32200 IF TYP$<>"mkii" THEN IF Q9%=1 THEN PRINT#6," ";VAR7$;" ";VAR8$ ELSE PRINT#6," "
32210 CLOSE 6
32220 ON ERROR GOTO 3100 : RETURN
32300 IF ERR = 53 AND ERL = 32070 THEN RESUME 32210 ELSE RESUME 32220
65529 REM proper last line
