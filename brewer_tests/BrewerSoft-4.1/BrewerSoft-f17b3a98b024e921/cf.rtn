10000 REM ************ cf.rtn 02/12/2010 09:50:00 ************ 
55555 REM Please keep line 10000 updated when changes are made. Volodya Savastiouk
10001 REM       MKII/MKIII/MKIV Instrument Constants Editor
10002 REM  
10004 REM          (SCI-TEC Instruments, January 1999)
10005 REM  *****************************************************
10006 DATA cf
10030 ON ERROR GOTO 10300
10040 OP=63:REM Number of entries in name.fil and icfval.nnn
10050 DIM ZB$(OP,1):A$="name.fil":IF Q14%=1 THEN A$="name2.fil"
10100 CF=0:CLOSE 1:OPEN A$ FOR INPUT AS 1
10110 FOR I=1 TO OP:INPUT# 1,ZB$:ZB$(I,0)=LEFT$(ZB$+SP$,20)
10120 ZB$(I,1)=LEFT$(SP$,13):NEXT
10130 CLOSE 1:OPEN DD$+NO$+"\"+ICF$+"."+NO$ FOR INPUT AS 1
10210 FOR I=1 TO OP
10212 IF EOF(1) AND I=52 THEN ZB$(52,1)=ZB$(51,1):ZB$(51,1)=STR$(ER%*3/4)
10213 IF EOF(1) AND I=53 THEN ZB$(53,1)="EXTRAS":ZB$(54,1)=STR$(AUTOHG):ZB$(55,1)=STR$(HIFW2):ZB$(56,1)=STR$(HGFW2):ZB$(57,1)=STR$(LATTN!):ZB$(58,1)=STR$(UATTN!):ZB$(59,1)=STR$(CUB.U):ZB$(60,1)=STR$(OPT.LOWDS):ZB$(61,1)=STR$(DR6):ZB$(62,1)=STR$(DR5)
10214 IF EOF(1) THEN 10225
10215 INPUT#1,ZB$:ZB$(I,1)=LEFT$(ZB$+SP$,13)
10220 NEXT
10225 ON ERROR GOTO 10500
10230 CLOSE 1
10240 GOTO 11000
10300 '
10301 ' *** Error Handling ***
10302 '
10310 IF ERL<>10100 and ERL<>10110 and ERL<>10130 and ERL<>10215 THEN 10340
10320   CLS:LOCATE 10,SP:PRINT "Name file not found -- CF routine terminated"
10330   GOTO 10400
10340 IF ERR=62 THEN RESUME 10230
10350 LOCATE ,PP: PRINT "Error ";ERR;" in CF on line ";ERL
10400 FOR I=1 TO 3000: NEXT
10410 ERASE ZB$:ON ERROR GOTO 3100: RETURN
10500 ERASE ZB$:GOTO 3100
11000 '
11001 ' *** Editor ***
11002 '
11100 PRINT CL$
11104 LOCATE 4,1:PRINT "File:   ";ICF$;".";NO$
11105 LOCATE 6,1:PRINT "Instr. #";NO$
11106 LOCATE 8,1:PRINT "Press Control-END":LOCATE ,5:PRINT "to exit"
11110 LOCATE 4,PP:PRINT B1$;LEFT$(LY$,20);B2$;LEFT$(LY$,15);B3$
11120 LOCATE ,PP:PRINT BR$"        name        ";BR$;"     value     ";BR$
11130 LOCATE ,PP:PRINT B7$;LEFT$(LY$,20);B8$;LEFT$(LY$,15);B9$
11140 FOR I=1 TO 17:LOCATE ,PP:PRINT BR$;"                    ";BR$;"               ";BR$:NEXT
11150 LOCATE ,PP:PRINT B4$;LEFT$(LY$,20);B5$;LEFT$(LY$,15);B6$;
11160 P=0:V=1:C=1
11170 GOSUB 31000
11180 ZB$=ZB$(V+P,1):LL$=LEFT$(ZB$,C-1):T$=MID$(ZB$,C,1):RR$=RIGHT$(ZB$,13-C)
11190 OV=V:OC=C:VP=5+V:HP=PP+22+C-1:GOSUB 30000:COLOR 0,7:PRINT T$;
11200 COLOR 7,0:A$=INKEY$:IF A$="" THEN 11200
11210 A=ASC(A$):IF A$=QB$ THEN 12000
11220 IF (A$=QE$) AND (P>0) THEN A=0:P=P-17+(P-17)*(P<17):GOSUB 31000:A$=Q2$
11230 IF (A$=QF$) AND (OP>P+17) THEN A=0:P=P+17+(P+34-OP)*(OP<P+34):GOSUB 31000:A$=Q2$
11240 IF A$=Q2$ THEN V=1:C=1
11250 IF A$=Q3$ THEN V=1:P=0:C=1:GOSUB 31000
11260 IF ((A$=CR$) OR (A$=QC$)) THEN C=1:A=13
11290 IF ((A$=Q8$) OR (A$=CR$)) AND (OP>V+P) THEN V=V+1:IF V>17 THEN V=17:P=P+1:GOSUB 31000
11300 IF ((A$=Q7$) OR (A$=QD$)) AND (V+P>1) THEN V=V-1:IF V<1 THEN V=1:P=P-1:GOSUB 31000
11400 IF A$=Q6$ THEN C=C+1
11410 IF A$=Q4$ THEN C=C-1
11420 IF (A$=Q0$) AND (RIGHT$(RR$,1)=" ") AND (CF=0) THEN ZB$(V+P,1)=LL$+" "+T$+RR$
11430 IF (A$=Q1$ OR A$=Q5$) AND (C>1) AND (CF=0) THEN ZB$(V+P,1)=LL$+RR$+" ":C=C-1
11440 A=(A AND 127):IF (A>31) AND (A<123) THEN ZB$(V+P,1)=LL$+A$+RR$:C=C+1
11450 IF C<1 THEN C=1
11460 IF C>13 THEN C=13
11470 ZB$(V+P,1)=LEFT$(ZB$(V+P,1)+SP$,13)
11480 LOCATE 6+OV,PP+23
11490 PRINT ZB$(OV+P,1);
11500 GOTO 11180
12000 '
12001 ' *** Save Instrument Constants and Quit ***
12002 '
12010 A$=ZB$(23,1):REM              Fix TYP$ entry by removing extra spaces
12020 IF LEFT$(A$,1)=" " THEN A$=MID$(A$,2):REM           Remove left spaces
12030 IF RIGHT$(A$,1)=" " THEN A$=LEFT$(A$,LEN(A$)-1):REM Remove right spaces
12040 ZB$(23,1)=A$
12100 CLS:LOCATE 9,10
12110 LOCATE ,10: PRINT "Do you wish to write the constants to a new file? ";:GOSUB 2030
12120 IF A$="y" OR A$="Y" THEN 12130 ELSE 12200
12130   LOCATE 12,20: PRINT "Enter a filename (no extension): ";:GOSUB 2000
12140   IF B$="" THEN LOCATE ,30: PRINT "Aborted":GOTO 12110
12150   IF LEN(B$)>8 OR LEN(B$)<1 OR INSTR(B$,".")>0 THEN LOCATE ,30: PRINT "Bad filename ";B$:GOTO 12130
12160   ICF$=B$
12200 CLS:LOCATE 10,PP
12210 PRINT "Writing constants to ";ICF$;".";NO$:ZB$(OP,1)=DATE$
12220 OPEN DD$+NO$+"\"+ICF$+"."+NO$ FOR OUTPUT AS 1
12230 FOR I=1 TO OP:PRINT#1,ZB$(I,1):NEXT
12240 CLOSE 1:FOR I=1 TO 1000:NEXT:GOSUB 5200
12250 ERASE ZB$
12260 I=0:GOSUB 5400:GOSUB 4482:RETURN
30000 '
30001 ' *** Cursor Positioning ***
30002 '
30110 IF VP<0 THEN 30140
30120 IF HP<0 THEN 30140
30130 LOCATE VP+1,HP+1
30140 RETURN
31000 '
31001 ' *** Output Table of Schedules ***
31002 '
31100 VP=7:HP=PP:K=OP:IF K>P+17 THEN K=P+17
31110 FOR I=P+1 TO K:LOCATE VP+I-P-1,HP+1:PRINT ZB$(I,0);
31120 LOCATE VP+I-P-1,HP+23:PRINT ZB$(I,1);
31130 NEXT:RETURN
65529 REM proper last line
